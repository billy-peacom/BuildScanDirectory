-* File: ftw_weekly_wab_list.fex
-*Procedure by Pranav Dobhal AC044640 Started at 12/24/2012 1:32:27 PM
-* Modified by Konstantin Koton AC048724, 2015-12-02 (Migrated to WF8)


-*INCLUDE IBFS:/WFC/Repository/SOC/Production/Supporting_Files/common_dates_ibi.fex
-*INCLUDE IBFS:/WFC/Repository/SOC/Production/FlightWatch_Reports/ftw_weekly_netline_extract.fex
-INCLUDE IBFS:/EDA/EDASERVE/Ops_Analytics/procedures/common_dates_ibi.fex
-INCLUDE ./ftw_weekly_netline_extract.fex

SET TITLELINE = SKIP
SET DROPBLNKLINE = ON
SET NODATA = ''

-SET &STR_DT = AYMD(&LD_DT, 1, 'I8YYMD');
-SET &END_DT = AYMD(&LD_DT, 32, 'I8YYMD');

DEFINE FILE LEG_FLIGHTWATCH
	REGS_JOIN/A10 =  IF ACFT_REGS_CDE = 'XXXXX' THEN '0' ELSE TRIM('L',ACFT_REGS_CDE,10,'AC',2,'A10');
END
TABLE FILE LEG_FLIGHTWATCH
PRINT
     LEG_ID
	 FLIGHT_ORIG_LCL_DTE
	 CARRIER_CDE
     FLIGHT_NUM
     LEG_SCHD_ORIG
     LEG_SCHD_DEP_TME
     LEG_SCHD_DEST
     LEG_SCHD_ARR_TME
     ACFT_SUBTYPE_CDE
	 ACFT_REGS_CDE
	 REGS_JOIN
	 PSGR_CABJ_CNT
	 PSGR_CABY_CNT
	 PSGR_MALE_CNT
     PSGR_FEMALE_CNT
     PSGR_CHILD_CNT
	 PSGR_INFANT_CNT
	 PNR_PSGR_CNT
	 PNR_SPEQ_CNT
     PNR_SPEQ_MEDA_CNT
     PNR_SPEQ_SKI_CNT
     PNR_SPEQ_HOCKEY_CNT
     PNR_SPEQ_BIKE_CNT
     PNR_SPEQ_GOLF_CNT
     PNR_SPEQ_WCHR_CNT
     PNR_SPEQ_OXGN_CNT
     PNR_WCHR_CNT
	 PNR_WCBD_CNT
     PNR_WCBW_CNT
     PNR_WCMP_CNT
     PNR_WCHC_CNT
     PNR_WCOB_CNT
     PNR_MEDA_CNT
     PNR_CBBG_CNT
     PNR_EXST_CNT

WHERE FLIGHT_ORIG_LCL_DTE GE &LD_DT  AND FLIGHT_ORIG_LCL_DTE LE &END_DT ;
BY FLIGHT_ORIG_LCL_DTE NOPRINT
BY CARRIER_CDE NOPRINT
BY FLIGHT_NUM NOPRINT
ON TABLE HOLD AS FTW_H1 FORMAT FOCUS
END

JOIN
 LEFT_OUTER FILE FTW_NETLINE_EXTRACT AT FTW_NETLINE_EXTRACT.SEG01.LEG_NO
 TO MULTIPLE FILE FTW_H1 AT FTW_H1.SEG01.LEG_ID TAG J0 AS J0
 WHERE FTW_NETLINE_EXTRACT.SEG01.LEG_NO EQ J0.SEG01.LEG_ID;
 END

DEFINE FILE FTW_NETLINE_EXTRACT
	PNR_PSGR/I6 = IF PNR_PSGR_CNT EQ MISSING THEN ACT_PAX_BOOKED_COUNT ELSE PNR_PSGR_CNT;
	PSGR_MALE_CNT2/I6 = IF PNR_PSGR_CNT GT AIRCRAFT_SEAT_CAPACITY_TOTAL THEN AIRCRAFT_SEAT_CAPACITY_TOTAL - PSGR_FEMALE_CNT - PSGR_CHILD_CNT - PNR_CBBG_CNT - PNR_EXST_CNT ELSE PNR_PSGR_CNT - PSGR_FEMALE_CNT - PSGR_CHILD_CNT - PNR_CBBG_CNT - PNR_EXST_CNT;
END
TABLE FILE FTW_NETLINE_EXTRACT
PRINT
     LOCAL_DATE AS 'DATE'
     LEG_NO NOPRINT  AS 'LEG #'
     FN_CARRIER AS 'CARRIER'
     FLIGHT_NUMBER AS 'FLT #'
     DEP_AP_SCHED AS 'DEP'
     ARR_AP_SCHED AS 'ARR'
     AC_REGISTRATION AS 'Fin #'
     ACFT_SUBTYPE_CDE AS 'Fleet'
     SCHED_DEP_DATE_UTC/HHI AS 'DEP Time'
     SCHED_ARR_DATE_UTC/HHI AS 'ARR Time'
     ACT_PAX_BOOKED_COUNT/I6 AS 'BOOKED'
     AIRCRAFT_SEAT_CAPACITY_TOTAL/I6 AS 'CAPACITY'
     PSGR_CABJ_CNT AS 'J Bkd'
     PSGR_CABY_CNT AS 'Y Bkd'
     PSGR_MALE_CNT2 AS 'Male'
     PSGR_FEMALE_CNT AS 'Female'
     PSGR_CHILD_CNT AS 'Child'
     PSGR_INFANT_CNT AS 'Infant'
     PNR_PSGR AS 'PAX Ttl'
     COMPUTE ANTCPTD_BAGS/I6 = ( PNR_PSGR * BAG_PAX_RATIO ) + ( PNR_SPEQ_CNT ) + ( PNR_WCBD_CNT * 4.5 ) + ( PNR_WCBW_CNT * 4.5 ) + PNR_WCMP_CNT + PSGR_INFANT_CNT; AS 'Ant. Bags'
     COMPUTE ACFT_BAG_CAP/I6 = ( BAG_CAPACITY ); AS 'Bag Cap.'
     COMPUTE BAG_SPC_REM/D12% = ( ( ACFT_BAG_CAP - ANTCPTD_BAGS ) / ACFT_BAG_CAP ) * 100; AS 'Bag Spc Remain'
     BAG_PAX_RATIO AS 'Avg Chk Bags'
     BAG_WEIGHT AS 'Avg Bag Wt'
     AIRCRAFT_SEAT_CAPACITY_TOTAL/I6 AS 'CAPACITY'
     AIRCRAFT_SEAT_CAPACITY_TOTAL_J/I6 AS 'CAPACITY J'
     AIRCRAFT_SEAT_CAPACITY_TOTAL_Y/I6 AS 'CAPACITY Y'
     ALLWD_WT_EST
     ULD_TYPE
     ULD_CAPACITY
     BAG_PER_ULD
     ULD_WT

BY  LOWEST LOCAL_DATE NOPRINT
BY TOTAL LOWEST COMPUTE BAG_SPC_REM/D12% = ( ( ACFT_BAG_CAP - ANTCPTD_BAGS ) / ACFT_BAG_CAP ) * 100; NOPRINT
WHERE FN_CARRIER EQ 'AC';
WHERE LOCAL_DATE GE &STR_DT;
ON TABLE SET PAGE-NUM NOLEAD
ON TABLE NOTOTAL
ON TABLE PCHOLD FORMAT EXL07
ON TABLE SET STYLE *
-*     INCLUDE = IBFS:/WFC/Repository/SOC/Production/Supporting_Files/endeflt.sty,
	INCLUDE = IBFS:/EDA/EDASERVE/Ops_Analytics/styles/endeflt.sty,
$
TYPE=REPORT,
     GRAPHCOLOR='GREEN',
$
TYPE=DATA,
     JUSTIFY=RIGHT,
$
TYPE=TITLE,
     JUSTIFY=LEFT,
$
TYPE=REPORT,
     OBJECT=MENU,
     COLOR='WHITE',
     HOVER-COLOR=RGB(66 70 73),
     BACKCOLOR=RGB(102 102 102),
     HOVER-BACKCOLOR=RGB(218 225 232),
     BORDER-COLOR='WHITE',
$
TYPE=REPORT,
     OBJECT=STATUS-AREA,
     COLOR='WHITE',
     BACKCOLOR=RGB(102 102 102),
$
TYPE=REPORT,
     OBJECT=CURRENT-ROW,
     HOVER-BACKCOLOR=RGB(218 225 232),
     BACKCOLOR=RGB(200 200 200),
$
TYPE=REPORT,
     OBJECT=CALC-AREA,
     COLOR='WHITE',
     BACKCOLOR=RGB(102 102 102),
$
ENDSTYLE
END
