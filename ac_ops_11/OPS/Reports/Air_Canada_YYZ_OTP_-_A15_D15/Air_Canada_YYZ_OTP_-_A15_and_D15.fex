-* File: Air_Canada_YYZ_OTP_-_A15_and_D15.fex
-* Created by Konstantin Koton AC048724, 2024-01-24

-INCLUDE /EDA/EDASERVE/Ops_Analytics/procedures/common_dates_ibi.fex

SET BYDISPLAY=ON


-SET &START_DATE = &LW_BDT;
-SET &END_DATE = &LW_EDT;


DEFINE FILE LEG_FLOWN
	D15_FLAG/I11 = IF (LEG_DEP_DELAY_MINS FROM -15 TO 15) THEN 1 ELSE 0;
	A15_FLAG/I11 = IF (LEG_ARR_DELAY_MINS FROM -15 TO 15) THEN 1 ELSE 0;
END
TABLE FILE LEG_FLOWN
PRINT
	IATA_CDE
	FLIGHT_NUM
	FLIGHT_ORIG_LCL_DTE
	LEG_ACTL_ORIG
	LEG_ACTL_DEST
	LEG_SCHD_DEP_LCL_TMS
	LEG_ACTL_DEP_LCL_TMS
	LEG_SCHD_ARR_LCL_TMS
	LEG_ACTL_ARR_LCL_TMS
	LEG_DEP_DELAY_MINS
	LEG_ARR_DELAY_MINS
	FLIGHTS
	D15_FLAG
	A15_FLAG

WHERE (FLIGHT_ORIG_LCL_DTE FROM '&START_DATE' TO '&END_DATE');
WHERE ((LEG_ACTL_ORIG EQ 'YYZ') OR (LEG_ACTL_DEST EQ 'YYZ'));
WHERE (OTP_INCLUDE_FLAG EQ 1);
ON TABLE HOLD AS AC_YYZ_OTP_D15_A15_HOLD FORMAT XFOCUS
END


TABLE FILE AC_YYZ_OTP_D15_A15_HOLD
SUM
	D15_FLAG
	FLIGHTS AS 'DEP_FLIGHTS'

BY FLIGHT_ORIG_LCL_DTE
WHERE (LEG_ACTL_ORIG EQ 'YYZ');
ON TABLE HOLD AS AC_YYZ_OTP_D15_DAILY_HOLD FORMAT XFOCUS INDEX FLIGHT_ORIG_LCL_DTE
END


TABLE FILE AC_YYZ_OTP_D15_A15_HOLD
SUM
	A15_FLAG
	FLIGHTS AS 'ARR_FLIGHTS'

BY FLIGHT_ORIG_LCL_DTE
WHERE (LEG_ACTL_DEST EQ 'YYZ');
ON TABLE HOLD AS AC_YYZ_OTP_A15_DAILY_HOLD FORMAT XFOCUS INDEX FLIGHT_ORIG_LCL_DTE
END

JOIN INNER
	FLIGHT_ORIG_LCL_DTE IN AC_YYZ_OTP_D15_DAILY_HOLD TO UNIQUE
	FLIGHT_ORIG_LCL_DTE IN AC_YYZ_OTP_A15_DAILY_HOLD TAG J0 AS J0
END

TABLE FILE AC_YYZ_OTP_D15_DAILY_HOLD
SUM
	COMPUTE D15/P12.1% = (D15_FLAG / DEP_FLIGHTS) * 100; AS 'D15,'
	COMPUTE A15/P12.1% = (A15_FLAG / ARR_FLIGHTS) * 100; AS 'A15,'
	DEP_FLIGHTS AS 'Departed,Flights'
	ARR_FLIGHTS AS 'Arrived,Flights'

ON TABLE SET PAGE-NUM NOLEAD
ON TABLE NOTOTAL
ON TABLE PCHOLD FORMAT EXL07 OPEN NOBREAK
ON TABLE SET HTMLCSS ON
ON TABLE SET STYLE *
    INCLUDE = /EDA/EDASERVE/ops_analytics/styles/endeflt.sty,
$
	TITLETEXT='Summary',
$
ENDSTYLE
END

TABLE FILE AC_YYZ_OTP_D15_DAILY_HOLD
SUM
	COMPUTE D15/P12.1% = (D15_FLAG / DEP_FLIGHTS) * 100; AS 'D15,'
	COMPUTE A15/P12.1% = (A15_FLAG / ARR_FLIGHTS) * 100; AS 'A15,'
	DEP_FLIGHTS AS 'Departed,Flights'
	ARR_FLIGHTS AS 'Arrived,Flights'

BY FLIGHT_ORIG_LCL_DTE AS 'Date,'
ON TABLE SET PAGE-NUM NOLEAD
ON TABLE NOTOTAL
ON TABLE PCHOLD FORMAT EXL07
ON TABLE SET HTMLCSS ON
ON TABLE SET STYLE *
    INCLUDE = /EDA/EDASERVE/ops_analytics/styles/endeflt.sty,
$
ENDSTYLE
END



TABLE FILE AC_YYZ_OTP_D15_A15_HOLD
PRINT
	LEG_ACTL_DEST AS 'Actl Arr,Station'
	LEG_SCHD_DEP_LCL_TMS AS 'Schd Dep,Local'
	LEG_ACTL_DEP_LCL_TMS AS 'Actl Dep,Local'
	LEG_DEP_DELAY_MINS AS 'Dep Delay,Minutes'
	D15_FLAG AS 'Dep +/- 15,Count'
	FLIGHTS AS 'Flight,Count'

BY FLIGHT_ORIG_LCL_DTE AS 'Date,'
BY IATA_CDE AS 'Carrier,'
BY FLIGHT_NUM AS 'Flight,'
WHERE (LEG_ACTL_ORIG EQ 'YYZ');
ON TABLE SET PAGE-NUM NOLEAD
ON TABLE NOTOTAL
ON TABLE PCHOLD FORMAT EXL07
ON TABLE SET HTMLCSS ON
ON TABLE SET STYLE *
    INCLUDE = /EDA/EDASERVE/ops_analytics/styles/endeflt.sty,
$
	TITLETEXT='Departures',
$
ENDSTYLE
END





TABLE FILE AC_YYZ_OTP_D15_A15_HOLD
PRINT
	LEG_ACTL_ORIG AS 'Actl Dep,Station'
	LEG_SCHD_ARR_LCL_TMS AS 'Schd Arr,Local'
	LEG_ACTL_ARR_LCL_TMS AS 'Actl Arr,Local'
	LEG_ARR_DELAY_MINS AS 'Arr Delay,Minutes'
	A15_FLAG AS 'Arr +/- 15,Count'
	FLIGHTS AS 'Flight,Count'

BY FLIGHT_ORIG_LCL_DTE AS 'Date,'
BY IATA_CDE AS 'Carrier,'
BY FLIGHT_NUM AS 'Flight,'
WHERE (LEG_ACTL_DEST EQ 'YYZ');
ON TABLE SET PAGE-NUM NOLEAD
ON TABLE NOTOTAL
ON TABLE PCHOLD FORMAT EXL07 CLOSE
ON TABLE SET HTMLCSS ON
ON TABLE SET STYLE *
    INCLUDE = /EDA/EDASERVE/ops_analytics/styles/endeflt.sty,
$
	TITLETEXT='Arrivals',
$
ENDSTYLE
END














