-* File: Weekly_CNLs_YoY_Report.fex
-* Created by Konstantin Koton AC048724, 2019-02-27

-INCLUDE /EDA/EDASERVE/ops_analytics/procedures/utility_functions.fex




SET BYDISPLAY = ON
SET ASNAMES = ON
SET HOLDLIST = PRINTONLY
SET NODATA = ''
SET PAGE = NOLEAD
SET TITLELINE = SKIP
SET SQUEEZE = ON
SET ACROSSPRT = COMPRESS
SET DROPBLNKLINE = ALL
SET CENT-ZERO = ON

-*-SET &YYMD = 20160229;

-DEFAULTH &PERIOD_COUNT = 0;

-SET &LD_DT = DATECVT(DATEADD(DATECVT(&YYMD, 'I8YYMD', 'YYMD'), 'D', -1), 'YYMD', 'I8YYMD');
-SET &TM_BDT = DATECVT(DATEMOV(DATECVT(&LD_DT, 'I8YYMD', 'YYMD'), 'BOM'), 'YYMD', 'I8YYMD');
-SET &LM_EDT = DATECVT(DATEADD(DATECVT(&TM_BDT, 'I8YYMD', 'YYMD'), 'D', -1), 'YYMD', 'I8YYMD');
-SET &LM_BDT = DATECVT(DATEMOV(DATECVT(&LM_EDT, 'I8YYMD', 'YYMD'), 'BOM'), 'YYMD', 'I8YYMD');
-SET &PM_EDT = DATECVT(DATEADD(DATECVT(&LM_BDT, 'I8YYMD', 'YYMD'), 'D', -1), 'YYMD', 'I8YYMD');
-SET &TY_BDT = DATECVT(DATEMOV(DATECVT(&LD_DT, 'I8YYMD', 'YYMD'), 'BOY'), 'YYMD', 'I8YYMD');

-SET &DAYS_IN_YEAR = IF ((IMOD(INT(&YYMD / 10000), 4, 'I1') EQ 0) AND (EDIT(&YYMD, '$$$$9999') GE 0229)) THEN 366 ELSE 365;

-*-SET &YYMD_YOY = (INT(&YYMD / 10000) - 1) || EDIT(&YYMD, '$$$$9999');
-SET &YYMD_YOY = DATECVT(DATEADD(DATECVT(&YYMD, 'I8YYMD', 'YYMD'), 'D', -&DAYS_IN_YEAR), 'YYMD', 'I8YYMD');
-SET &LD_DT_YOY = DATECVT(DATEADD(DATECVT(&YYMD_YOY, 'I8YYMD', 'YYMD'), 'D', -1), 'YYMD', 'I8YYMD');
-SET &TM_BDT_YOY = DATECVT(DATEMOV(DATECVT(&LD_DT_YOY, 'I8YYMD', 'YYMD'), 'BOM'), 'YYMD', 'I8YYMD');
-SET &LM_EDT_YOY = DATECVT(DATEADD(DATECVT(&TM_BDT_YOY, 'I8YYMD', 'YYMD'), 'D', -1), 'YYMD', 'I8YYMD');
-SET &LM_BDT_YOY = DATECVT(DATEMOV(DATECVT(&LM_EDT_YOY, 'I8YYMD', 'YYMD'), 'BOM'), 'YYMD', 'I8YYMD');
-SET &PM_EDT_YOY = DATECVT(DATEADD(DATECVT(&LM_BDT_YOY, 'I8YYMD', 'YYMD'), 'D', -1), 'YYMD', 'I8YYMD');
-SET &TY_BDT_YOY = DATECVT(DATEMOV(DATECVT(&LD_DT_YOY, 'I8YYMD', 'YYMD'), 'BOY'), 'YYMD', 'I8YYMD');


-SET &YYMD_YOY_2019 = '2019' | EDIT(&YYMD, '$$$$9999');
-SET &LD_DT_YOY_2019 = DATECVT(DATEADD(DATECVT(&YYMD_YOY_2019, 'I8YYMD', 'YYMD'), 'D', -1), 'YYMD', 'I8YYMD');
-SET &TM_BDT_YOY_2019 = DATECVT(DATEMOV(DATECVT(&LD_DT_YOY_2019, 'I8YYMD', 'YYMD'), 'BOM'), 'YYMD', 'I8YYMD');
-SET &LM_EDT_YOY_2019 = DATECVT(DATEADD(DATECVT(&TM_BDT_YOY_2019, 'I8YYMD', 'YYMD'), 'D', -1), 'YYMD', 'I8YYMD');
-SET &LM_BDT_YOY_2019 = DATECVT(DATEMOV(DATECVT(&LM_EDT_YOY_2019, 'I8YYMD', 'YYMD'), 'BOM'), 'YYMD', 'I8YYMD');
-SET &PM_EDT_YOY_2019 = DATECVT(DATEADD(DATECVT(&LM_BDT_YOY_2019, 'I8YYMD', 'YYMD'), 'D', -1), 'YYMD', 'I8YYMD');
-SET &TY_BDT_YOY_2019 = DATECVT(DATEMOV(DATECVT(&LD_DT_YOY_2019, 'I8YYMD', 'YYMD'), 'BOY'), 'YYMD', 'I8YYMD');


-TYPE **************************
-TYPE *** &|DAYS_IN_YEAR: &DAYS_IN_YEAR ***
-TYPE **************************

-TYPE ***********************
-TYPE *** &|YYMD: &YYMD ***
-TYPE ***********************
-TYPE *************************
-TYPE *** &|LD_DT: &LD_DT  ***
-TYPE *** &|TM_BDT: &TM_BDT ***
-TYPE *** &|LM_BDT: &LM_BDT ***
-TYPE *** &|LM_EDT: &LM_EDT ***
-TYPE *** &|PM_EDT: &PM_EDT ***
-TYPE *** &|TY_BDT: &TY_BDT ***
-TYPE *************************
-TYPE ***************************
-TYPE *** &|YYMD_YOY: &YYMD_YOY ***
-TYPE ***************************
-TYPE *****************************
-TYPE *** &|LD_DT_YOY: &LD_DT_YOY  ***
-TYPE *** &|TM_BDT_YOY: &TM_BDT_YOY ***
-TYPE *** &|LM_BDT_YOY: &LM_BDT_YOY ***
-TYPE *** &|LM_EDT_YOY: &LM_EDT_YOY ***
-TYPE *** &|PM_EDT_YOY: &PM_EDT_YOY ***
-TYPE *** &|TY_BDT_YOY: &TY_BDT_YOY ***
-TYPE *****************************
-TYPE ***************************
-TYPE *** &|YYMD_YOY_2019: &YYMD_YOY_2019 ***
-TYPE ***************************
-TYPE *****************************
-TYPE *** &|LD_DT_YOY_2019: &LD_DT_YOY_2019  ***
-TYPE *** &|TM_BDT_YOY_2019: &TM_BDT_YOY_2019 ***
-TYPE *** &|LM_BDT_YOY_2019: &LM_BDT_YOY_2019 ***
-TYPE *** &|LM_EDT_YOY_2019: &LM_EDT_YOY_2019 ***
-TYPE *** &|PM_EDT_YOY_2019: &PM_EDT_YOY_2019 ***
-TYPE *** &|TY_BDT_YOY_2019: &TY_BDT_YOY_2019 ***
-TYPE *****************************


-SET &YEAR = INT(&LD_DT / 10000);
-SET &YEAR_YOY = &YEAR - 1;
-SET &YEAR_YOY_2019 = '2019';
-SET &TITLE_YTD = 'Jan 1 - ' | DECODE_MONTH(EDIT(EDIT(&LD_DT, '$$$$99$$')), 3, 'MIXED') || ( ' ' | EDIT(EDIT(&LD_DT, '$$$$$$99'))) || (' (&YEAR_YOY.EVAL vs. &YEAR.EVAL)');
-SET &TITLE_LM  = DECODE_MONTH(EDIT(EDIT(&LM_BDT, '$$$$99$$')), 3, 'MIXED') || ( ' ' | EDIT(EDIT(&LM_BDT, '$$$$$$99'))) || (' - ' | DECODE_MONTH(EDIT(EDIT(&LM_EDT, '$$$$99$$')), 3, 'MIXED') || ( ' ' | EDIT(EDIT(&LM_EDT, '$$$$$$99'))) || (' (&YEAR_YOY.EVAL vs. &YEAR.EVAL)'));
-SET &TITLE_MTD = DECODE_MONTH(EDIT(EDIT(&TM_BDT, '$$$$99$$')), 3, 'MIXED') || ( ' ' | EDIT(EDIT(&TM_BDT, '$$$$$$99'))) || (' - ' | DECODE_MONTH(EDIT(EDIT(&LD_DT, '$$$$99$$')), 3, 'MIXED') || ( ' ' | EDIT(EDIT(&LD_DT, '$$$$$$99'))) || (' (&YEAR_YOY.EVAL vs. &YEAR.EVAL)'));
-SET &TITLE_YTD_2019 = 'Jan 1 - ' | DECODE_MONTH(EDIT(EDIT(&LD_DT, '$$$$99$$')), 3, 'MIXED') || ( ' ' | EDIT(EDIT(&LD_DT, '$$$$$$99'))) || (' (&YEAR_YOY.EVAL vs. 2019)');
-SET &TITLE_LM_2019  = DECODE_MONTH(EDIT(EDIT(&LM_BDT, '$$$$99$$')), 3, 'MIXED') || ( ' ' | EDIT(EDIT(&LM_BDT, '$$$$$$99'))) || (' - ' | DECODE_MONTH(EDIT(EDIT(&LM_EDT, '$$$$99$$')), 3, 'MIXED') || ( ' ' | EDIT(EDIT(&LM_EDT, '$$$$$$99'))) || (' (&YEAR_YOY.EVAL vs. 2019)'));
-SET &TITLE_MTD_2019 = DECODE_MONTH(EDIT(EDIT(&TM_BDT, '$$$$99$$')), 3, 'MIXED') || ( ' ' | EDIT(EDIT(&TM_BDT, '$$$$$$99'))) || (' - ' | DECODE_MONTH(EDIT(EDIT(&LD_DT, '$$$$99$$')), 3, 'MIXED') || ( ' ' | EDIT(EDIT(&LD_DT, '$$$$$$99'))) || (' (&YEAR_YOY.EVAL vs. 2019)'));
-TYPE &TITLE_YTD
-TYPE &TITLE_LM
-TYPE &TITLE_MTD
-TYPE &TITLE_YTD_2019
-TYPE &TITLE_LM_2019
-TYPE &TITLE_MTD_2019

-SET &TODAY_LABEL = EDIT(&YYMD, '9999/99/99');

-*-EXIT

-*-SET &DATE_RANGE_DISP = EDIT(&TY_BDT, '9999/99/99') | ' - ' | EDIT(&LD_DT, '9999/99/99');



-*-SET &ECHO = ALL;
-*-SET &PERIOD_COUNT = 6;
-*-SET &TEST = (&PERIOD_COUNT / 3.00);
-*-TYPE &TEST
-*
-*-SET &PAGES = INT((&PERIOD_COUNT / 3) + 0.8);
-*-TYPE PAGES: &PAGES
-*-EXIT

-SET &START_DATE = MIN(&TY_BDT, &LM_BDT);
-SET &END_DATE = &LD_DT;

-SET &START_DATE_YOY = MIN(&TY_BDT_YOY, &LM_BDT_YOY);
-SET &END_DATE_YOY = &LD_DT_YOY;

-SET &START_DATE_YOY_2019 = MIN(&TY_BDT_YOY_2019, &LM_BDT_YOY_2019);
-SET &END_DATE_YOY_2019 = &LD_DT_YOY_2019;

DEFINE FILE LEG_FLOWN
	YEAR/YY = FLIGHT_ORIG_LCL_DTE;
	MONTH/M = FLIGHT_ORIG_LCL_DTE;
-*	PERIOD_NAME/A10V = IF (FLIGHT_ORIG_LCL_DTE FROM '&TM_BDT' TO '&LD_DT') OR (FLIGHT_ORIG_LCL_DTE FROM '&TM_BDT_YOY' TO '&LD_DT_YOY') THEN 'MTD' ELSE
-*						IF (FLIGHT_ORIG_LCL_DTE FROM '&LM_BDT' TO '&LM_EDT') OR (FLIGHT_ORIG_LCL_DTE FROM '&LM_BDT_YOY' TO '&LM_EDT_YOY') THEN 'LM' ELSE '';
-*	PERIOD_NAME/A10V = IF (FLIGHT_ORIG_LCL_DTE FROM '&TM_BDT' TO '&LD_DT') OR (FLIGHT_ORIG_LCL_DTE FROM '&TM_BDT_YOY' TO '&LD_DT_YOY') THEN 'MTD' ELSE
-*						IF (FLIGHT_ORIG_LCL_DTE FROM '&TY_BDT' TO '&LM_EDT') OR (FLIGHT_ORIG_LCL_DTE FROM '&TY_BDT_YOY' TO '&LM_EDT_YOY') THEN DECODE_MONTH(MONTH, 3, 'UPPER') ELSE '';
	PERIOD_NAME/A10V = IF (FLIGHT_ORIG_LCL_DTE FROM '&TM_BDT' TO '&LD_DT')
							OR (FLIGHT_ORIG_LCL_DTE FROM '&TM_BDT_YOY' TO '&LD_DT_YOY')
							OR (FLIGHT_ORIG_LCL_DTE FROM '&TM_BDT_YOY_2019' TO '&LD_DT_YOY_2019') THEN 'MTD' ELSE
					   IF (FLIGHT_ORIG_LCL_DTE FROM '&TY_BDT' TO '&LM_EDT')
					   		OR (FLIGHT_ORIG_LCL_DTE FROM '&TY_BDT_YOY' TO '&LM_EDT_YOY')
							OR (FLIGHT_ORIG_LCL_DTE FROM '&TY_BDT_YOY_2019' TO '&LM_EDT_YOY_2019') THEN DECODE_MONTH(MONTH, 3, 'UPPER') ELSE '';
-*	CARRIER_NME_1/A40V = IF (CARRIER_NME EQ 'Air Canada Rouge') THEN 'Rouge' ELSE
-*						 IF (CARRIER_NME EQ 'Air Canada Jazz') THEN 'Jazz' ELSE CARRIER_NME;
	CARRIER_NME_1/A40V = IF (CARRIER_NME EQ 'Air Canada Rouge') THEN 'Rouge' ELSE
						 IF (CARRIER_NME EQ 'Air Canada') THEN CARRIER_NME ELSE 'AC Express';
	IATA_CDE_1/A2 = IF (IATA_CDE IN ('8K', 'QK', 'RS', 'ZX')) THEN 'AE' ELSE IATA_CDE;
END
TABLE FILE LEG_FLOWN
SUM
	FLIGHTS
	SEATS_TOTAL AS 'TTL_SEATS'
	PAX_FLOWN
	COMPUTE JOIN_KEY/A20V = DATECVT(FLIGHT_ORIG_LCL_DTE, 'YYMD', 'A8YYMD') || IATA_CDE_1;

BY IATA_CDE_1 AS 'IATA_CDE'
BY CARRIER_NME_1 AS 'CARRIER_NME'
BY YEAR/I4
BY MONTH/I2
BY FLIGHT_ORIG_LCL_DTE
BY PERIOD_NAME
WHERE (FLIGHT_ORIG_LCL_DTE FROM '&START_DATE' TO '&END_DATE')
	OR (FLIGHT_ORIG_LCL_DTE FROM '&START_DATE_YOY' TO '&END_DATE_YOY')
	OR (FLIGHT_ORIG_LCL_DTE FROM '&START_DATE_YOY_2019' TO '&END_DATE_YOY_2019');
WHERE (OTP_INCLUDE_FLAG EQ 1);
-*WHERE (CARRIER_NME_1 NE 'Air Georgian');
ON TABLE HOLD AS CNLS_STATS_FLOWN_DATA FORMAT XFOCUS INDEX JOIN_KEY
END
-*-EXIT


DEFINE FILE LEG_INCIDENT
	YEAR/YY = FLIGHT_ORIG_LCL_DTE;
	MONTH/M = FLIGHT_ORIG_LCL_DTE;
-*	PERIOD_NAME/A10V = IF (FLIGHT_ORIG_LCL_DTE FROM '&TM_BDT' TO '&LD_DT') OR (FLIGHT_ORIG_LCL_DTE FROM '&TM_BDT_YOY' TO '&LD_DT_YOY') THEN 'MTD' ELSE
-*						IF (FLIGHT_ORIG_LCL_DTE FROM '&LM_BDT' TO '&LM_EDT') OR (FLIGHT_ORIG_LCL_DTE FROM '&LM_BDT_YOY' TO '&LM_EDT_YOY') THEN 'LM' ELSE '';
-*	PERIOD_NAME/A10V = IF (FLIGHT_ORIG_LCL_DTE FROM '&TM_BDT' TO '&LD_DT') OR (FLIGHT_ORIG_LCL_DTE FROM '&TM_BDT_YOY' TO '&LD_DT_YOY') THEN 'MTD' ELSE
-*						IF (FLIGHT_ORIG_LCL_DTE FROM '&TY_BDT' TO '&LM_EDT') OR (FLIGHT_ORIG_LCL_DTE FROM '&TY_BDT_YOY' TO '&LM_EDT_YOY') THEN DECODE_MONTH(MONTH, 3, 'UPPER') ELSE '';
	PERIOD_NAME/A10V = IF (FLIGHT_ORIG_LCL_DTE FROM '&TM_BDT' TO '&LD_DT')
							OR (FLIGHT_ORIG_LCL_DTE FROM '&TM_BDT_YOY' TO '&LD_DT_YOY')
							OR (FLIGHT_ORIG_LCL_DTE FROM '&TM_BDT_YOY_2019' TO '&LD_DT_YOY_2019') THEN 'MTD' ELSE
					   IF (FLIGHT_ORIG_LCL_DTE FROM '&TY_BDT' TO '&LM_EDT')
					   		OR (FLIGHT_ORIG_LCL_DTE FROM '&TY_BDT_YOY' TO '&LM_EDT_YOY')
							OR (FLIGHT_ORIG_LCL_DTE FROM '&TY_BDT_YOY_2019' TO '&LM_EDT_YOY_2019') THEN DECODE_MONTH(MONTH, 3, 'UPPER') ELSE '';
-*	CARRIER_NME_1/A40V = IF (CARRIER_NME EQ 'Air Canada Rouge') THEN 'Rouge' ELSE
-*						 IF (CARRIER_NME EQ 'Air Canada Jazz') THEN 'Jazz' ELSE CARRIER_NME;
	CARRIER_NME_1/A40V = IF (CARRIER_NME EQ 'Air Canada Rouge') THEN 'Rouge' ELSE
						 IF (CARRIER_NME EQ 'Air Canada') THEN CARRIER_NME ELSE 'AC Express';


	TRANS_TMS/HYYMDS = HINPUT(17, (DATECVT(POSTDATE, 'YYMD', 'A8YYMD') | ' ' | HCNVRT(POSTTIME, '(HHIS)', 8, 'A8')), 8, TRANS_TMS);
	EXCLUSIONS_PRE/I11 = IF (CANCEL_REASON_CODE_NETLINE IN ('COMM', 'POLI', 'RTNS')) OR (CANCEL_REASON_CODE_NETLINE IS MISSING) THEN 0 ELSE LEG_CNL_Count;
	EXCLUSIONS_POST/I11 = IF (HADD(TRANS_TMS, 'HOUR', 48, 8, 'HYYMDS') GE LEG_SCHD_DEP_TMS)
							THEN IF (CANCEL_REASON_CODE_NETLINE IN ('COMM')) OR (LEG_DVRT_IND IN ('D', 'R')) THEN 0 ELSE LEG_CNL_Count
							ELSE 0;
	CANCELLATIONS/I11 = IF (FLIGHT_ORIG_LCL_DTE GE '2022-08-01') THEN EXCLUSIONS_POST ELSE EXCLUSIONS_PRE;


	SEATS_PRE/I11 = IF (CANCEL_REASON_CODE_NETLINE IN ('COMM', 'POLI', 'RTNS')) OR (CANCEL_REASON_CODE_NETLINE IS MISSING) THEN 0 ELSE SEATS_TOTAL;
	SEATS_POST/I11 = IF (HADD(TRANS_TMS, 'HOUR', 48, 8, 'HYYMDS') GE LEG_SCHD_DEP_TMS)
							THEN IF (CANCEL_REASON_CODE_NETLINE IN ('COMM')) OR (LEG_DVRT_IND IN ('D', 'R')) THEN 0 ELSE SEATS_TOTAL
							ELSE 0;
	SEATS_CANCELLED/i11 = IF (FLIGHT_ORIG_LCL_DTE GE '2022-08-01') THEN SEATS_POST ELSE SEATS_PRE;


	IATA_CDE_1/A2 = IF (IATA_CDE IN ('8K', 'QK', 'RS', 'ZX')) THEN 'AE' ELSE IATA_CDE;
END
TABLE FILE LEG_INCIDENT
SUM
-*	LEG_CNL_Count AS 'CANCELLATIONS'
	CANCELLATIONS
-*	SEATS_TOTAL AS 'SEATS_CANCELLED'
	SEATS_CANCELLED
	COMPUTE JOIN_KEY/A20V = DATECVT(FLIGHT_ORIG_LCL_DTE, 'YYMD', 'A8YYMD') || IATA_CDE_1;

BY IATA_CDE_1 AS 'IATA_CDE'
BY CARRIER_NME_1 AS 'CARRIER_NME'
BY YEAR/I4
BY MONTH/I2
BY FLIGHT_ORIG_LCL_DTE
BY PERIOD_NAME
WHERE (FLIGHT_ORIG_LCL_DTE FROM '&START_DATE' TO '&END_DATE')
	OR (FLIGHT_ORIG_LCL_DTE FROM '&START_DATE_YOY' TO '&END_DATE_YOY')
	OR (FLIGHT_ORIG_LCL_DTE FROM '&START_DATE_YOY_2019' TO '&END_DATE_YOY_2019');
WHERE (OPRN_INCL_FLG EQ 1);
-*WHERE (CNL_PRMY_CDE NE 'SKD');
-*WHERE NOT (CANCEL_REASON_CODE_NETLINE IN ('COMM', 'POLI', 'RTNS'));
WHERE (LEG_STATE_CDE EQ 'CNL');
-*WHERE (LEG_DVRT_IND IS MISSING);
-*WHERE (CARRIER_NME_1 NE 'Air Georgian');
ON TABLE HOLD AS CNLS_STATS_CNLS_DATA FORMAT XFOCUS INDEX JOIN_KEY
END
-*-EXIT



JOIN LEFT_OUTER
	JOIN_KEY IN CNLS_STATS_FLOWN_DATA TO UNIQUE
	JOIN_KEY IN CNLS_STATS_CNLS_DATA TAG J0 AS J0
END

DEFINE FILE CNLS_STATS_FLOWN_DATA
	MONTH_1/I2 = IF (PERIOD_NAME EQ 'MTD') THEN 90 ELSE MONTH;
END
TABLE FILE CNLS_STATS_FLOWN_DATA
SUM
-*	COMPUTE MONTH/I2 = IF (PERIOD_NAME EQ 'MTD') THEN 90 ELSE MONTH;
	FLIGHTS
	TTL_SEATS
	PAX_FLOWN
	J0.CANCELLATIONS
	J0.SEATS_CANCELLED

	MIN.FLIGHT_ORIG_LCL_DTE WITHIN MONTH_1 AS 'START_DATE'
	MAX.FLIGHT_ORIG_LCL_DTE WITHIN MONTH_1 AS 'END_DATE'

BY IATA_CDE
BY CARRIER_NME
BY YEAR/I4
BY MONTH_1 AS 'MONTH'
BY FLIGHT_ORIG_LCL_DTE
BY PERIOD_NAME
ON TABLE  HOLD AS CNLS_STATS_COMBINED_DATA FORMAT XFOCUS
END
-*-EXIT



TABLE FILE CNLS_STATS_COMBINED_DATA
SUM
	COMPUTE MONTH/I2 = 99;
-*	COMPUTE MONTH_LABEL/A20V = EDIT(MAX.YEAR) | ' YTD';
	COMPUTE PERIOD_NAME/A10V = 'YTD';
	FLIGHTS
	TTL_SEATS
	PAX_FLOWN
	CANCELLATIONS
	SEATS_CANCELLED

	MIN.FLIGHT_ORIG_LCL_DTE AS 'START_DATE'
	MAX.FLIGHT_ORIG_LCL_DTE AS 'END_DATE'

BY IATA_CDE
BY CARRIER_NME
BY YEAR
WHERE (FLIGHT_ORIG_LCL_DTE FROM '&TY_BDT' TO '&LD_DT')
	OR (FLIGHT_ORIG_LCL_DTE FROM '&TY_BDT_YOY' TO '&LD_DT_YOY')
	OR (FLIGHT_ORIG_LCL_DTE FROM '&TY_BDT_YOY_2019' TO '&LD_DT_YOY_2019');
ON TABLE  HOLD AS CNLS_STATS_YTD_DATA FORMAT XFOCUS
END
-*-EXIT



DEFINE FILE CNLS_STATS_COMBINED_DATA
	PERIOD_NAME_1/A10V = IF (MONTH FROM 1 TO 3) THEN 'Q1' ELSE IF (MONTH FROM 4 TO 6) THEN 'Q2' ELSE IF (MONTH FROM 7 TO 9) THEN 'Q3' ELSE IF (MONTH FROM 10 TO 12) THEN 'Q4';
	MONTH_1/I2 = IF (MONTH FROM 1 TO 3) THEN 54 ELSE IF (MONTH FROM 4 TO 6) THEN 53 ELSE IF (MONTH FROM 7 TO 9) THEN 52  ELSE IF (MONTH FROM 10 TO 12) THEN 51;
END
TABLE FILE CNLS_STATS_COMBINED_DATA
SUM
	FLIGHTS
	TTL_SEATS
	PAX_FLOWN
	CANCELLATIONS
	SEATS_CANCELLED

	MIN.FLIGHT_ORIG_LCL_DTE AS 'START_DATE'
	MAX.FLIGHT_ORIG_LCL_DTE AS 'END_DATE'

BY IATA_CDE
BY CARRIER_NME
BY YEAR
BY MONTH_1 AS 'MONTH'
BY PERIOD_NAME_1 AS 'PERIOD_NAME'
WHERE (FLIGHT_ORIG_LCL_DTE FROM '&TY_BDT' TO '&LM_EDT')
	OR (FLIGHT_ORIG_LCL_DTE FROM '&TY_BDT_YOY' TO '&LM_EDT_YOY')
	OR (FLIGHT_ORIG_LCL_DTE FROM '&TY_BDT_YOY_2019' TO '&LM_EDT_YOY_2019');
ON TABLE  HOLD AS CNLS_STATS_Q_DATA FORMAT XFOCUS
END
-*-EXIT
-RUN


TABLE FILE CNLS_STATS_COMBINED_DATA
SUM
	FLIGHTS
	TTL_SEATS
	PAX_FLOWN
	CANCELLATIONS
	SEATS_CANCELLED
-*	COMPUTE START_MONTH/M = START_DATE; NOPRINT
-*	COMPUTE END_MONTH/M = END_DATE; NOPRINT
-*	COMPUTE START_DAY/D = START_DATE; NOPRINT
-*	COMPUTE END_DAY/D = END_DATE; NOPRINT
	COMPUTE START_MONTH/M = START_DATE;
	COMPUTE END_MONTH/M = END_DATE;
	COMPUTE START_DAY/D = START_DATE;
	COMPUTE END_DAY/D = END_DATE;
-*	COMPUTE YEAR_CMP/A20V = IF (MIN.YEAR EQ 2019) THEN ' (&YEAR_YOY_2019.EVAL vs. &YEAR.EVAL)' ELSE ' (&YEAR_YOY.EVAL vs. &YEAR.EVAL)'; NOPRINT
-*-*	COMPUTE TITLE/A50V = DECODE_MONTH(START_MONTH, 3, 'MIXED') || ( ' ' | FTOA(START_DAY,'(D2c)', 'A2V')) ||  (' - ' | DECODE_MONTH(END_MONTH, 3, 'MIXED') || ( ' ' | FTOA(END_DAY,'(D2c)', 'A2V')) || (' (&YEAR_YOY.EVAL vs. &YEAR.EVAL)'));
-*	COMPUTE TITLE/A50V = DECODE_MONTH(START_MONTH, 3, 'MIXED') || ( ' ' | FTOA(START_DAY,'(D2c)', 'A2V')) ||  (' - ' | DECODE_MONTH(END_MONTH, 3, 'MIXED') || ( ' ' | FTOA(END_DAY,'(D2c)', 'A2V')) || YEAR_CMP);

BY IATA_CDE
BY CARRIER_NME
BY YEAR
BY MONTH
BY PERIOD_NAME
BY START_DATE
BY END_DATE
ON TABLE HOLD AS CNLS_STATS_ALL_DATA FORMAT XFOCUS
MORE
FILE CNLS_STATS_YTD_DATA
MORE
FILE CNLS_STATS_Q_DATA
END
-*-EXIT



TABLE FILE CNLS_STATS_ALL_DATA
SUM
	FLIGHTS
	TTL_SEATS
	PAX_FLOWN
	CANCELLATIONS
	SEATS_CANCELLED
	MAX.START_MONTH AS 'START_MONTH'
	MAX.END_MONTH AS 'END_MONTH'
	MAX.START_DAY AS 'START_DAY'
	MAX.END_DAY AS 'END_DAY'

BY COMPUTE IATA_CDE/A2 = '';
BY COMPUTE CARRIER_NME/A40V = 'AC System';
BY YEAR
BY MONTH
BY PERIOD_NAME
WHERE (PERIOD_NAME IN ('YTD', 'MTD', 'JAN', 'FEB', 'MAR', 'APR', 'MAY', 'JUN', 'JUL', 'AUG', 'SEP', 'OCT', 'NOV', 'DEC', 'Q1', 'Q2', 'Q3', 'Q4'));
ON TABLE HOLD AS CNLS_STATS_FINAL_SYSTEM_DATA FORMAT XFOCUS
END

TABLE FILE CNLS_STATS_ALL_DATA
SUM
	FLIGHTS
	TTL_SEATS
	PAX_FLOWN
	CANCELLATIONS
	SEATS_CANCELLED
	MAX.START_MONTH AS 'START_MONTH'
	MAX.END_MONTH AS 'END_MONTH'
	MAX.START_DAY AS 'START_DAY'
	MAX.END_DAY AS 'END_DAY'

BY IATA_CDE
BY CARRIER_NME
BY YEAR
BY MONTH
BY PERIOD_NAME
WHERE (PERIOD_NAME IN ('YTD', 'MTD', 'JAN', 'FEB', 'MAR', 'APR', 'MAY', 'JUN', 'JUL', 'AUG', 'SEP', 'OCT', 'NOV', 'DEC', 'Q1', 'Q2', 'Q3', 'Q4'));
ON TABLE HOLD AS CNLS_STATS_FINAL_DATA FORMAT XFOCUS
MORE
FILE CNLS_STATS_FINAL_SYSTEM_DATA
END
-*-EXIT



DEFINE FILE CNLS_STATS_FINAL_DATA
	CARRIER_SORT/I2 = DECODE IATA_CDE ('AC' 1 'RV' 2 'QK' 3 'ZX' 4 '8K' 5 'RS' 6 'AE' 7 ELSE 99);
END
TABLE FILE CNLS_STATS_FINAL_DATA
SUM
	FLIGHTS
	TTL_SEATS
	PAX_FLOWN/P12.1
	CANCELLATIONS/P12.1
	COMPUTE CF/P12.1% = (FLIGHTS / (FLIGHTS + CANCELLATIONS)) * 100;
	COMPUTE SEAT_COMP/P12.1% = (TTL_SEATS / (TTL_SEATS + SEATS_CANCELLED)) * 100;
	MAX.START_MONTH AS 'START_MONTH'
	MAX.END_MONTH AS 'END_MONTH'
	MAX.START_DAY AS 'START_DAY'
	MAX.END_DAY AS 'END_DAY'

BY CARRIER_SORT
BY IATA_CDE
BY CARRIER_NME
BY YEAR
BY MONTH
BY PERIOD_NAME
BY TOTAL COMPUTE YEAR_LABEL/A14V = FTOA(YEAR, '(D12c)', 'A12V');
BY TOTAL COMPUTE YEAR_SORT/I4 = YEAR;
WHERE (YEAR NE '2019');
ON TABLE HOLD AS CNLS_STATS_FINAL_DATA_WITH_MEASURES FORMAT XFOCUS
END
-*-EXIT



TABLE FILE CNLS_STATS_FINAL_DATA
SUM
	FLIGHTS
	TTL_SEATS
	PAX_FLOWN/P12.1
	CANCELLATIONS/P12.1
	COMPUTE CF/P12.1% = (FLIGHTS / (FLIGHTS + CANCELLATIONS)) * 100;
	COMPUTE SEAT_COMP/P12.1% = (TTL_SEATS / (TTL_SEATS + SEATS_CANCELLED)) * 100;
	MAX.START_MONTH AS 'START_MONTH'
	MAX.END_MONTH AS 'END_MONTH'
	MAX.START_DAY AS 'START_DAY'
	MAX.END_DAY AS 'END_DAY'

BY CARRIER_SORT
BY IATA_CDE
BY CARRIER_NME
BY YEAR
BY MONTH
BY PERIOD_NAME NOPRINT
BY TOTAL COMPUTE PERIOD_NAME/A10V = PERIOD_NAME || '_2019'; AS 'PERIOD_NAME'
BY TOTAL COMPUTE YEAR_LABEL/A14V = FTOA(YEAR, '(D12c)', 'A12V');
BY TOTAL COMPUTE YEAR_SORT/I4 = YEAR;
WHERE (YEAR IN ('2019', '&YEAR'));
ON TABLE HOLD AS CNLS_STATS_FINAL_DATA_WITH_MEASURES_2019 FORMAT XFOCUS
END
-*-EXIT



TABLE FILE CNLS_STATS_FINAL_DATA_WITH_MEASURES
SUM
	COMPUTE PAX_FLOWN_DELTA/P12.1 MISSING ON = IF (YEAR EQ LAST YEAR + 1) THEN PAX_FLOWN - LAST PAX_FLOWN ELSE MISSING; AS 'PAX_FLOWN'
	COMPUTE CANCELLATIONS_DELTA/P12.1 MISSING ON = IF (YEAR EQ LAST YEAR + 1) THEN CANCELLATIONS - LAST CANCELLATIONS ELSE MISSING; AS 'CANCELLATIONS'
	COMPUTE CF_DELTA/P12.1% MISSING ON = IF (YEAR EQ LAST YEAR + 1) THEN CF - LAST CF ELSE MISSING; AS 'CF'
	COMPUTE SEAT_COMP_DELTA/P12.1% MISSING ON = IF (YEAR EQ LAST YEAR + 1) THEN SEAT_COMP - LAST SEAT_COMP ELSE MISSING; AS 'SEAT_COMP'
	MAX.START_MONTH AS 'START_MONTH'
	MAX.END_MONTH AS 'END_MONTH'
	MAX.START_DAY AS 'START_DAY'
	MAX.END_DAY AS 'END_DAY'

BY CARRIER_SORT
BY IATA_CDE
BY CARRIER_NME
BY PERIOD_NAME
BY MONTH
BY YEAR
BY TOTAL COMPUTE YEAR_SORT/I4 = 9998;
BY TOTAL COMPUTE YEAR_LABEL/A14V = 'YoY Change';
BY CARRIER_NME
WHERE TOTAL (PAX_FLOWN_DELTA NE MISSING);
ON TABLE HOLD AS CNLS_STATS_DELTA_DATA_WITH_MEASURES FORMAT XFOCUS
END
-*-EXIT



TABLE FILE CNLS_STATS_FINAL_DATA_WITH_MEASURES
SUM
	COMPUTE PAX_FLOWN_DELTA/P12.1 MISSING ON = IF (YEAR EQ LAST YEAR + 1) THEN ((PAX_FLOWN - LAST PAX_FLOWN) / LAST PAX_FLOWN) * 100 ELSE MISSING; AS 'PAX_FLOWN'
	COMPUTE CANCELLATIONS_DELTA/P12.1 MISSING ON = IF (YEAR EQ LAST YEAR + 1) THEN ((CANCELLATIONS - LAST CANCELLATIONS) / LAST CANCELLATIONS) * 100 ELSE MISSING; AS 'CANCELLATIONS'
	COMPUTE CF_DELTA/P12.1% MISSING ON = MISSING; AS 'CF'
	COMPUTE SEAT_COMP_DELTA/P12.1% MISSING ON = MISSING; AS 'SEAT_COMP'
	MAX.START_MONTH AS 'START_MONTH'
	MAX.END_MONTH AS 'END_MONTH'
	MAX.START_DAY AS 'START_DAY'
	MAX.END_DAY AS 'END_DAY'

BY CARRIER_SORT
BY IATA_CDE
BY CARRIER_NME
BY PERIOD_NAME
BY MONTH
BY YEAR
BY TOTAL COMPUTE YEAR_SORT/I4 = 9999;
BY TOTAL COMPUTE YEAR_LABEL/A14V = 'YoY Change, %';
WHERE TOTAL (PAX_FLOWN_DELTA NE MISSING);
ON TABLE HOLD AS CNLS_STATS_DELTA_DATA_WITH_MEASURES2 FORMAT XFOCUS
END
-*-EXIT



-SET &YEAR_DELTA = &YEAR - 2019;
-TYPE YEAR_DELTA: &YEAR_DELTA
TABLE FILE CNLS_STATS_FINAL_DATA_WITH_MEASURES_2019
SUM
	COMPUTE PAX_FLOWN_DELTA/P12.1 MISSING ON = IF (YEAR EQ LAST YEAR + &YEAR_DELTA) THEN PAX_FLOWN - LAST PAX_FLOWN ELSE MISSING; AS 'PAX_FLOWN'
	COMPUTE CANCELLATIONS_DELTA/P12.1 MISSING ON = IF (YEAR EQ LAST YEAR + &YEAR_DELTA) THEN CANCELLATIONS - LAST CANCELLATIONS ELSE MISSING; AS 'CANCELLATIONS'
	COMPUTE CF_DELTA/P12.1% MISSING ON = IF (YEAR EQ LAST YEAR + &YEAR_DELTA) THEN CF - LAST CF ELSE MISSING; AS 'CF'
	COMPUTE SEAT_COMP_DELTA/P12.1% MISSING ON = IF (YEAR EQ LAST YEAR + &YEAR_DELTA) THEN SEAT_COMP - LAST SEAT_COMP ELSE MISSING; AS 'SEAT_COMP'
	MAX.START_MONTH AS 'START_MONTH'
	MAX.END_MONTH AS 'END_MONTH'
	MAX.START_DAY AS 'START_DAY'
	MAX.END_DAY AS 'END_DAY'

BY CARRIER_SORT
BY IATA_CDE
BY CARRIER_NME
BY PERIOD_NAME
BY MONTH
BY YEAR
BY TOTAL COMPUTE YEAR_SORT/I4 = 9998;
BY TOTAL COMPUTE YEAR_LABEL/A14V = 'YoY Change';
WHERE TOTAL (PAX_FLOWN_DELTA NE MISSING);
ON TABLE HOLD AS CNLS_STATS_DELTA_DATA_WITH_MEASURES_2019 FORMAT XFOCUS
END
-*-EXIT



TABLE FILE CNLS_STATS_FINAL_DATA_WITH_MEASURES_2019
SUM
	COMPUTE PAX_FLOWN_DELTA/P12.1 MISSING ON = IF (YEAR EQ LAST YEAR + &YEAR_DELTA) THEN ((PAX_FLOWN - LAST PAX_FLOWN) / LAST PAX_FLOWN) * 100 ELSE MISSING; AS 'PAX_FLOWN'
	COMPUTE CANCELLATIONS_DELTA/P12.1 MISSING ON = IF (YEAR EQ LAST YEAR + &YEAR_DELTA) THEN ((CANCELLATIONS - LAST CANCELLATIONS) / LAST CANCELLATIONS) * 100 ELSE MISSING; AS 'CANCELLATIONS'
	COMPUTE CF_DELTA/P12.1% MISSING ON = MISSING; AS 'CF'
	COMPUTE SEAT_COMP_DELTA/P12.1% MISSING ON = MISSING; AS 'SEAT_COMP'
	MAX.START_MONTH AS 'START_MONTH'
	MAX.END_MONTH AS 'END_MONTH'
	MAX.START_DAY AS 'START_DAY'
	MAX.END_DAY AS 'END_DAY'

BY CARRIER_SORT
BY IATA_CDE
BY CARRIER_NME
BY PERIOD_NAME
BY MONTH
BY YEAR
BY TOTAL COMPUTE YEAR_SORT/I4 = 9999;
BY TOTAL COMPUTE YEAR_LABEL/A14V = 'YoY Change, %';
WHERE TOTAL (PAX_FLOWN_DELTA NE MISSING);
ON TABLE HOLD AS CNLS_STATS_DELTA_DATA_WITH_MEASURES2_2019 FORMAT XFOCUS
END
-*-EXIT



TABLE FILE CNLS_STATS_FINAL_DATA_WITH_MEASURES
SUM
	PAX_FLOWN
	CANCELLATIONS
	COMPUTE CF_1/P12.1% MISSING ON        = IF (YEAR_SORT EQ '9999') THEN MISSING ELSE CF; AS 'CF'
	COMPUTE SEAT_COMP_1/P12.1% MISSING ON = IF (YEAR_SORT EQ '9999') THEN MISSING ELSE SEAT_COMP; AS 'SEAT_COMP'
-*	MAX.START_MONTH AS 'START_MONTH'
-*	MAX.END_MONTH AS 'END_MONTH'
-*	MAX.START_DAY AS 'START_DAY'
-*	MAX.END_DAY AS 'END_DAY'
	COMPUTE YEAR_CMP/A20V = IF (SUBSTRING(PERIOD_NAME, CHAR_LENGTH(PERIOD_NAME)-3, 4) EQ '2019') THEN ' (&YEAR_YOY_2019.EVAL vs. &YEAR.EVAL)' ELSE ' (&YEAR_YOY.EVAL vs. &YEAR.EVAL)'; NOPRINT
	COMPUTE TITLE/A50V = DECODE_MONTH(MAX.START_MONTH, 3, 'MIXED') || ( ' ' | FTOA(MAX.START_DAY,'(D2c)', 'A2V')) ||  (' - ' | DECODE_MONTH(MAX.END_MONTH, 3, 'MIXED') || ( ' ' | FTOA(MAX.END_DAY,'(D2c)', 'A2V')) || YEAR_CMP);
-*	COMPUTE TEST/I2 = CHAR_LENGTH(PERIOD_NAME);
-*	COMPUTE TEST2/A4 = SUBSTRING(PERIOD_NAME, TEST-3, 4);
-*	COMPUTE TEST3/A4 = SUBSTRING(PERIOD_NAME, CHAR_LENGTH(PERIOD_NAME)-3, 4);

BY CARRIER_SORT
BY IATA_CDE
BY CARRIER_NME
BY MONTH
BY PERIOD_NAME
BY YEAR_SORT
BY YEAR
BY YEAR_LABEL
ON TABLE HOLD AS CNLS_STATS_FINAL_DATA_WITH_MEASURES_AND_DELTAS FORMAT XFOCUS
-*ON TABLE PCHOLD FORMAT EXL07
MORE
FILE CNLS_STATS_DELTA_DATA_WITH_MEASURES
MORE
FILE CNLS_STATS_DELTA_DATA_WITH_MEASURES2
MORE
FILE CNLS_STATS_FINAL_DATA_WITH_MEASURES_2019
MORE
FILE CNLS_STATS_DELTA_DATA_WITH_MEASURES_2019
MORE
FILE CNLS_STATS_DELTA_DATA_WITH_MEASURES2_2019
END
-RUN
-*-EXIT



TABLE FILE CNLS_STATS_FINAL_DATA_WITH_MEASURES_AND_DELTAS
SUM
	COMPUTE MEASURE_NAME/A30V = 'Flight Completion';
	COMPUTE MEASURE_SORT/I11 = 1;
	COMPUTE MEASURE/A20V = FTOA(CF,'(D12.1c)', 'A14V') || '%';

BY CARRIER_SORT
BY IATA_CDE
BY CARRIER_NME
BY MONTH
BY PERIOD_NAME
BY TITLE
BY YEAR_SORT
BY YEAR
BY YEAR_LABEL
ON TABLE HOLD AS CNLS_STATS_CF_DATA FORMAT XFOCUS
END
-*-EXIT



TABLE FILE CNLS_STATS_FINAL_DATA_WITH_MEASURES_AND_DELTAS
SUM
	COMPUTE MEASURE_NAME/A30V = 'Total Cancelled Flights';
	COMPUTE MEASURE_SORT/I11 = 2;
	COMPUTE MEASURE/A20V = IF (YEAR_SORT IN ('9997', '9999')) THEN FTOA(CANCELLATIONS, '(D12.1c)', 'A16V') || '%' ELSE FTOA(CANCELLATIONS, '(D12)', 'A16V');

BY CARRIER_SORT
BY IATA_CDE
BY CARRIER_NME
BY MONTH
BY PERIOD_NAME
BY TITLE
BY YEAR_SORT
BY YEAR
BY YEAR_LABEL
ON TABLE HOLD AS CNLS_STATS_CNLS_DATA FORMAT XFOCUS
END



TABLE FILE CNLS_STATS_FINAL_DATA_WITH_MEASURES_AND_DELTAS
SUM
	COMPUTE MEASURE_NAME/A30V = 'Total Passengers';
	COMPUTE MEASURE_SORT/I11 = 3;
	COMPUTE MEASURE/A20V = IF (YEAR_SORT IN ('9997', '9999')) THEN FTOA(PAX_FLOWN, '(D12.1c)', 'A16V') || '%' ELSE FTOA(PAX_FLOWN, '(D12)', 'A16V');

BY CARRIER_SORT
BY IATA_CDE
BY CARRIER_NME
BY MONTH
BY PERIOD_NAME
BY TITLE
BY YEAR_SORT
BY YEAR
BY YEAR_LABEL
ON TABLE HOLD AS CNLS_STATS_PAX_FLOWN_DATA FORMAT XFOCUS
END



TABLE FILE CNLS_STATS_FINAL_DATA_WITH_MEASURES_AND_DELTAS
SUM
	COMPUTE MEASURE_NAME/A30V = 'Seats Completed';
	COMPUTE MEASURE_SORT/I11 = 4;
	COMPUTE MEASURE/A20V = FTOA(SEAT_COMP,'(D12.1c)', 'A14V') || '%';

BY CARRIER_SORT
BY IATA_CDE
BY CARRIER_NME
BY MONTH
BY PERIOD_NAME
BY TITLE
BY YEAR_SORT
BY YEAR
BY YEAR_LABEL
ON TABLE HOLD AS CNLS_STATS_SEATS_COMP_DATA FORMAT XFOCUS
END
-*-EXIT



TABLE FILE CNLS_STATS_CF_DATA
PRINT
	COMPUTE COMPONENT_NUM/I11 = IF (LAST PERIOD_NAME NE PERIOD_NAME) THEN COMPONENT_NUM + 1 ELSE COMPONENT_NUM;

BY HIGHEST MONTH
BY PERIOD_NAME
BY TITLE
BY CARRIER_SORT
BY IATA_CDE
BY CARRIER_NME
BY YEAR_SORT
BY YEAR
BY YEAR_LABEL
BY MEASURE_SORT
BY MEASURE_NAME
BY MEASURE
ON TABLE HOLD AS CNLS_STATS_FINALIZED_DATA FORMAT XFOCUS
-*ON TABLE PCHOLD FORMAT EXL07
MORE
FILE CNLS_STATS_CNLS_DATA
MORE
FILE CNLS_STATS_PAX_FLOWN_DATA
MORE
FILE CNLS_STATS_SEATS_COMP_DATA
END
-RUN
-*-EXIT


-*TABLE FILE CNLS_STATS_COMBINED_DATA
-*BY HIGHEST MONTH
-*BY CARRIER_SORT
-*BY IATA_CDE
-*BY CARRIER_NME
-*BY YEAR_SORT
-*BY YEAR
-*BY YEAR_LABEL
-*BY MEASURE_SORT
-*BY MEASURE_NAME
-*BY MEASURE
-*BY PERIOD_NAME
-*BY TITLE
-*WHERE (YEAR_SORT IN ('2019', '9996', '9997', '&YEAR'));
-*ON TABLE HOLD AS CNLS_STATS_COMBINED_DATA FORMAT XFOCUS
-*END
-*-*-EXIT
-*
-*
-*
-*TABLE FILE CNLS_STATS_COMBINED_DATA
-*PRINT
-*	COMPUTE PERIOD_NAME/A10V = PERIOD_NAME || '_2019'; AS 'PERIOD_NAME'
-*	COMPUTE TITLE/A50V = IF (YEAR EQ '2019') THEN TITLE ELSE LAST TITLE; AS 'TITLE'
-*
-*BY HIGHEST MONTH
-*BY CARRIER_SORT
-*BY IATA_CDE
-*BY CARRIER_NME
-*BY YEAR_SORT
-*BY YEAR
-*BY YEAR_LABEL
-*BY MEASURE_SORT
-*BY MEASURE_NAME
-*BY MEASURE
-*-*WHERE (YEAR NE '2019');
-*-*WHERE NOT (YEAR_SORT IN ('9996', '9997'));
-*-*ON TABLE HOLD AS CNLS_STATS_FINALIZED_DATA FORMAT XFOCUS
-*ON TABLE PCHOLD FORMAT EXL07
-*END
-*-EXIT


TABLE FILE CNLS_STATS_FINALIZED_DATA
BY PERIOD_NAME
ON TABLE HOLD
END
-RUN

-SET &PERIOD_COUNT = &LINES;
-TYPE PERIOD_COUNT: &PERIOD_COUNT
-*-EXIT



SET HTMLARCHIVE=ON
COMPOUND LAYOUT PCHOLD FORMAT PDF
UNITS=IN, $
-*SECTION=section1, LAYOUT=ON, MERGE=OFF, ORIENTATION=LANDSCAPE, PAGESIZE=Letter, SHOW_GLOBALFILTER=OFF, $
SECTION=section1, LAYOUT=ON, MERGE=OFF, ORIENTATION=LANDSCAPE, PAGESIZE=Legal, SHOW_GLOBALFILTER=OFF, $
PAGELAYOUT=ALL, NAME='Page Master', $
OBJECT=IMAGE, NAME='image101', IMAGE=ppt_template_2020.png, ALT='', POSITION=(0.0 0.0), DIMENSION=(14.0 8.5), $
OBJECT=STRING, NAME='text001', TEXT='<font face="VERDANA" size=10 color=#FFFFFF><right>Page <ibi-page-number/> of <ibi-total-pages/></right></font>', POSITION=(10.8 8.0), MARKUP=ON, WRAP=ON, DIMENSION=(3.2 0.2), $
COMPONENT='DfltCmpt1', POSITION=(0 0), DIMENSION=(0 0),  $

PAGELAYOUT=1, NAME='Page layout 1', text='Page layout 1', TOC-LEVEL=1, BOTTOMMARGIN=0, TOPMARGIN=0.5, $
OBJECT=IMAGE, NAME='image101', IMAGE=ppt_template_2020_cover.png, ALT='', POSITION=(0.0 0.0), DIMENSION=(14.0 8.5), $
-*OBJECT=STRING, NAME='text101', TEXT='<font face="VERDANA" size=36 color=#4B4F54>Flight & Seat Cancellations<br>YTD Year-Over-Year Comparison</font>', POSITION=(0.6 3.0), MARKUP=ON, WRAP=ON, DIMENSION=(9.0 1.5), $
OBJECT=STRING, NAME='text101', TEXT='<font face="VERDANA" size=36 color=#FFFFFF>Flight & Seat Cancellations<br>YTD Year-Over-Year Comparison</font>', POSITION=(0.6 4.0), MARKUP=ON, WRAP=ON, DIMENSION=(9.0 1.5), $
-*OBJECT=STRING, NAME='text102', TEXT='<B><font face="VERDANA" size=18 color=#B79A7E>OPS BI</font></B>', POSITION=(0.6 6.2), MARKUP=ON, WRAP=ON, DIMENSION=(5.3 0.5), $
OBJECT=STRING, NAME='text102', TEXT='<B><font face="VERDANA" size=18 color=#FFFFFF>OPS BI</font></B>', POSITION=(0.6 6.2), MARKUP=ON, WRAP=ON, DIMENSION=(5.3 0.5), $
-*OBJECT=STRING, NAME='text103', TEXT='<font face="VERDANA" size=12 color=#4B4F54>&TODAY_LABEL.EVAL</font>', POSITION=(0.6 6.8), MARKUP=ON, WRAP=ON, DIMENSION=(2.8 0.5), $
OBJECT=STRING, NAME='text103', TEXT='<font face="VERDANA" size=12 color=#7F7F7F>&TODAY_LABEL.EVAL</font>', POSITION=(0.6 6.8), MARKUP=ON, WRAP=ON, DIMENSION=(2.8 0.5), $
COMPONENT='DfltCmpt2', POSITION=(0 0), DIMENSION=(0 0),  $


-*-SET &ECHO = ALL;
-SET &PAGES = INT((&PERIOD_COUNT / 3) + 0.7) + 1;
-TYPE PAGES: &PAGES


-REPEAT :PAGE_ITEMS FOR &PAGE_NUM FROM 2 TO &PAGES;

PAGELAYOUT=&PAGE_NUM, NAME='Page layout &PAGE_NUM.EVAL', text='Page layout &PAGE_NUM.EVAL', TOC-LEVEL=1, BOTTOMMARGIN=0, TOPMARGIN=0.5, $
-*OBJECT=STRING, NAME='text&PAGE_NUM.EVAL01', TEXT='<font face="VERDANA" size=32 color=#FFFFFF>Cancelled Flights & Seats Comparison</font>', POSITION=(1.5 0.6), MARKUP=ON, WRAP=ON, DIMENSION=(9.0 1), $
OBJECT=STRING, NAME='text&PAGE_NUM.EVAL01', TEXT='<B><font face="VERDANA" size=24 color=#000000>Cancelled Flights & Seats Comparison</font></B>', POSITION=(0.25 0.5), MARKUP=ON, WRAP=ON, DIMENSION=(9.0 1), $
-SET &ITEM2 = ((&PAGE_NUM - 2) * 3) + 2;
-SET &ITEM3 = ((&PAGE_NUM - 2) * 3) + 3;

-*COMPONENT='report&PAGE_NUM.EVAL01', TEXT='report&PAGE_NUM.EVAL01', TOC-LEVEL=2, POSITION=(0.5 1.5), DIMENSION=(* *), BYTOC=0,  ARREPORTSIZE=DIMENSION, $
COMPONENT='report&PAGE_NUM.EVAL01', TEXT='report&PAGE_NUM.EVAL01', TOC-LEVEL=2, POSITION=(0.25 1.0), DIMENSION=(* *), BYTOC=0,  ARREPORTSIZE=DIMENSION, $
-IF (&ITEM2 GT &PERIOD_COUNT) GOTO :SKIP_ITEM2;
-*COMPONENT='report&PAGE_NUM.EVAL02', TEXT='report&PAGE_NUM.EVAL02', TOC-LEVEL=2, POSITION=(0.5 3.75), DIMENSION=(* *), BYTOC=0,  ARREPORTSIZE=DIMENSION, $
COMPONENT='report&PAGE_NUM.EVAL02', TEXT='report&PAGE_NUM.EVAL02', TOC-LEVEL=2, POSITION=(0.25 3.25), DIMENSION=(* *), BYTOC=0,  ARREPORTSIZE=DIMENSION, $
-:SKIP_ITEM2;

-IF (&ITEM3 GT &PERIOD_COUNT) GOTO :SKIP_ITEM3;
-*COMPONENT='report&PAGE_NUM.EVAL03', TEXT='report&PAGE_NUM.EVAL03', TOC-LEVEL=2, POSITION=(0.5 6.0), DIMENSION=(* *), BYTOC=0,  ARREPORTSIZE=DIMENSION, $
COMPONENT='report&PAGE_NUM.EVAL03', TEXT='report&PAGE_NUM.EVAL03', TOC-LEVEL=2, POSITION=(0.25 5.5), DIMENSION=(* *), BYTOC=0,  ARREPORTSIZE=DIMENSION, $
-:SKIP_ITEM3;
-:PAGE_ITEMS

END
-*-EXIT


SET COMPONENT='DfltCmpt1'
TABLE FILE SYSCOLUM
" "
SUM TBNAME NOPRINT
IF READLIMIT EQ 1
ON TABLE SET PREVIEW ON
ON TABLE SET PAGE-NUM NOLEAD
ON TABLE PCHOLD FORMAT PDF
ON TABLE SET STYLE *
END



SET COMPONENT='DfltCmpt2'
TABLE FILE SYSCOLUM
" "
SUM TBNAME NOPRINT
IF READLIMIT EQ 1
ON TABLE SET PREVIEW ON
ON TABLE SET PAGE-NUM NOLEAD
ON TABLE PCHOLD FORMAT PDF
ON TABLE SET STYLE *
END


-REPEAT :PAGE_COMPS FOR &PAGE_NUM FROM 2 TO &PAGES;
-REPEAT :PAGE_SUB_COMPS FOR &SUB_COMP_NUM FROM 1 TO 3;

-SET &COMP = ((&PAGE_NUM - 2) * 3) + &SUB_COMP_NUM;
-IF (&COMP GT &PERIOD_COUNT) GOTO :SKIP_COMP;
SET COMPONENT='report&PAGE_NUM.EVAL0&SUB_COMP_NUM.EVAL'

DEFINE FILE CNLS_STATS_FINALIZED_DATA
	POSITIVE/I1 = DECODE MEASURE_NAME('Flight Completion' 1 'Total Cancelled Flights' 2 'Total Passengers' 1 'Seats Completed' 1);
	NEGATIVE/I1 = DECODE MEASURE_NAME('Flight Completion' 2 'Total Cancelled Flights' 1 'Total Passengers' 2 'Seats Completed' 2);
END
TABLE FILE CNLS_STATS_FINALIZED_DATA
SUM
	MEASURE
	COMPUTE COLOR/I2 = IF (YEAR_SORT IN (9998, 9999)) THEN IF (POSIT(MEASURE, 20, '-', 1, 'I2') EQ 0) THEN POSITIVE ELSE NEGATIVE ELSE 0; NOPRINT

BY CARRIER_SORT NOPRINT
BY CARRIER_NME AS ''
ACROSS MEASURE_SORT NOPRINT
ACROSS MEASURE_NAME AS ''
ACROSS YEAR_SORT NOPRINT
-*ACROSS YEAR NOPRINT
ACROSS YEAR_LABEL AS ''
HEADING
"<TITLE"
WHERE NOT ((YEAR_SORT EQ 9999) AND (MEASURE_SORT IN (1,4)));
-*WHERE (COMPONENT_NUM EQ '1');
WHERE (COMPONENT_NUM EQ '&COMP');
ON TABLE PCHOLD FORMAT PDF

ON TABLE SET HTMLCSS ON
ON TABLE SET STYLE *
    INCLUDE = /EDA/EDASERVE/ops_analytics/styles/endeflt.sty,
$
	PAGESIZE='Letter',
	ORIENTATION=LANDSCAPE,
	LEFTMARGIN=0,
	RIGHTMARGIN=0,
	TOPMARGIN=0,
	BOTTOMMARGIN=0,
$
TYPE=REPORT,
	FONT='VERDANA',
	SIZE=7,
	COLOR=RGB(66 70 73),
	STYLE=NORMAL,
	TOPGAP=0.025,
	BOTTOMGAP=0.025,
	LEFTGAP=0.015,
	RIGHTGAP=0.015,
	BACKCOLOR=('WHITE' RGB(225 225 225)),
	BORDER-COLOR=RGB(102 102 102),
$
TYPE=REPORT,
	COLUMN=CARRIER_NME,
	WRAP=0.7,
$
TYPE=DATA,
	COLUMN=CARRIER_NME,
	JUSTIFY=LEFT,
$
TYPE=TITLE,
	JUSTIFY=CENTER,
	BORDER-BOTTOM=LIGHT,
$
TYPE=DATA,
	JUSTIFY=CENTER,
	BACKCOLOR=('WHITE' RGB(225 225 225)),
$
TYPE=ACROSSVALUE,
	STYLE=BOLD,
	COLOR='WHITE',
	BACKCOLOR=RGB(102 102 102),
	JUSTIFY=CENTER,
$
TYPE=DATA,
	ACROSSCOLUMN=MEASURE,
	WHEN=YEAR_SORT EQ &YEAR_YOY,
	BORDER-LEFT=LIGHT,
	LEFTGAP=0.05,
	RIGHTGAP=0.05,
$
TYPE=DATA,
	ACROSSCOLUMN=MEASURE,
	WHEN=COLOR EQ 1,
	COLOR='GREEN',
$
TYPE=DATA,
	ACROSSCOLUMN=MEASURE,
	WHEN=COLOR EQ 2,
	COLOR='RED',
$
TYPE=ACROSSVALUE,
	ACROSS=2,
	SIZE=10,
	BORDER-LEFT=LIGHT,
	BORDER-LEFT-COLOR=RGB(225 225 225),
$
TYPE=ACROSSVALUE,
	ACROSS=4,
	BORDER-LEFT=LIGHT,
	BORDER-LEFT-COLOR=RGB(225 225 225),
$
ENDSTYLE
END

-:SKIP_COMP
-:PAGE_SUB_COMPS
-:PAGE_COMPS


-*COMPOUND END
