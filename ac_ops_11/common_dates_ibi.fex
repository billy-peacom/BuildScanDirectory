-*SET &ECHO = ALL ;
-*------------------------------------------------------------------------------
-* Module Name     : common_dates_ibi.fex
-* Remarks         : Set of default dates used by the business.
-*                   Dates are related to the current system date, unless otherwise
-*                   specified by &ASOFDATE.
-*
-*
-* Called By       : All WebFOCUS report programs
-* Calls/Includes  : None
-*
-* Input Variables : &ASOFDATE : Base date to be used to determine all other business dates
-*
-* Output Variables:
-*   Name        Description                      Format
-*   ---------   -----------                      ------
-*   &ASOFDATE   Default to Current Date (YYMD)   YYYYMMDD
-*   &TOD1       Time of day
-*   &DOW        Day of the week                  MON
-*   &TW_BDT     This week begin date             YYYYMMDD
-*   &TW_EDT     This week End date               YYYYMMDD
-*   &LW_BDT     Last week Begin date             YYYYMMDD
-*   &LW_EDT     Last week End date               YYYYMMDD
-*   &LD_DT      Last day date (e.g. yesterday)   YYYYMMDD
-*   &PD_DT      Previous day date (yest - 1)     YYYYMMDD
-*   &TM_BDT     This month Begin date            YYYYMMDD
-*   &TM_EDT     This month End date              YYYYMMDD
-*   &LM_EDT     Last month End Date              YYYYMMDD
-*   &LM_BDT     Last month Begin Date            YYYYMMDD
-*   &LM_YTD_BDT Last month YTD Begin Date        YYYYMMDD
-*   &TY_BDT     This year Begin Date             YYYYMMDD
-*   &TY_EDT     This year End Date               YYYYMMDD
-*   &LY_BDT     Last year Begin Date             YYYYMMDD
-*   &LY_EDT     Last year End Date               YYYYMMDD
-*   &TM_LBDT    This month Last business day     YYYYMMDD
-*   &LM_LBDT    Last month Last businees day     YYYYMMDD
-*   &MM_BDT     Mid Month Begin date             YYYYMMDD
-*   &MM_EDT     Mid Month end date               YYYYMMDD
-*   &Q1_BDT     Q1 Begin date                    YYYYMMDD
-*   &Q1_EDT     Q1 End date                      YYYYMMDD
-*   &Q2_BDT     Q2 Begin date                    YYYYMMDD
-*   &Q2_EDT     Q2 End date                      YYYYMMDD
-*   &Q3_BDT     Q3 Begin date                    YYYYMMDD
-*   &Q3_EDT     Q3 End date                      YYYYMMDD
-*   &Q4_BDT     Q4 Begin date                    YYYYMMDD
-*   &Q4_EDT     Q4 End date                      YYYYMMDD
-*   &Q1LY_BDT   Q1 Last Year Begin date          YYYYMMDD
-*   &Q1LY_EDT   Q1 Last Year End date            YYYYMMDD
-*   &Q2LY_BDT   Q2 Last Year Begin date          YYYYMMDD
-*   &Q2LY_EDT   Q2 Last Year End date            YYYYMMDD
-*   &Q3LY_BDT   Q3 Last Year Begin date          YYYYMMDD
-*   &Q3LY_EDT   Q4 Last Year End date            YYYYMMDD
-*   &Q4LY_BDT   Q4 Last Year Begin date          YYYYMMDD
-*   &Q4LY_EDT   Q4 Last Year End date            YYYYMMDD
-*   &TQ_BDT     This Quarter Begin date          YYYYMMDD
-*   &TQ_EDT     This Quarter End date            YYYYMMDD
-*   &LQ_BDT     Last Quarter Begin date          YYYYMMDD
-*   &LQ_EDT     Last Quarter End date            YYYYMMDD
-*   &TQ_QY      This Quarter Quarter-Year        QQ-YYYY
-*   &LQ_QY      Last Quarter Quarter-Year        QQ-YYYY
-*   &CPP_BDT    Current Pay Period Begin Date    YYYYMMDD
-*   &CPP_EDT    Current Pay Period End Date      YYYYMMDD
-*   &LPP_BDT    Last Pay Period Begin Date       YYYYMMDD
-*   &LPP_EDT    Last Pay Period End Date         YYYYMMDD
-*
-* Input Files     : None
-* Output Files    : None
-* Developed By    : Information Builders Canada
-* Date Developed  : June 2011
-*
-* Update on 2012-10-20 : corrected calculation of &TW_BDT on SAT and SUN (Anton, Vinoth)
-*
-* Update on 2012-10-22 : added &ISO_YEAR and &ISO_WEEK variables to get
-*   week number and year number by ISO standard (Anton)
-*
-*----------------------------------------------------------------------
SET WEEKFIRST = ISO2
SET DEFCENT = 20
-RUN
-*-DEFAULTH &ASOFDATE = '2011/06/18';
-IF &ASOFDATE.EXISTS THEN GOTO :OKASOFDATE;
-SET &ASOFDATE = &YYMD;
-:OKASOFDATE
 
-SET &ASOFDATE = IF ARGLEN (&ASOFDATE.LENGTH, &ASOFDATE, 'I4') EQ 8 THEN &ASOFDATE
-                ELSE EDIT(&ASOFDATE, '9999$99$99');
-SET &TOD1 = EDIT(&TOD,'99:$99');
 
-SET &LW_DATE = HADD(HGETC(8,'HYYMDS'),'DAY',-7,8,'HYYMDS');
 
-SET &RN_YYYY  = EDIT(&ASOFDATE, '9999$$$$');
-SET &RN_MM    = EDIT(&ASOFDATE, '$$$$99$$');
-SET &RN_DD    = EDIT(&ASOFDATE, '$$$$$$99');
 
-SET &DOW       = DOWK(&ASOFDATE, 'DW');
-SET &WK_NO     = HPART(HGETC(8, 'HYYMDS'), 'week', 'I2');
-SET &YR_NO     = HPART(HGETC(8, 'HYYMDS'), 'year', 'I4');
 
-*-SET &TW_BDT    = DATECVT(DATEMOV(DATECVT(&ASOFDATE,'I8YYMD','YYMD'), 'BOW'),'YYMD','I8YYMD') ;
 
-SET &TW_BDT    =
-  IF &DOW EQ 'SAT' OR 'SUN'
-  THEN DATECVT(DATEMOV(DATECVT(AYMD(&ASOFDATE, -2, 'I8YYMD'),'I8YYMD','YYMD'), 'BOW'),'YYMD','I8YYMD')
-  ELSE DATECVT(DATEMOV(DATECVT(&ASOFDATE,'I8YYMD','YYMD'), 'BOW'),'YYMD','I8YYMD');
 
-SET &TW_EDT    = AYMD (&TW_BDT, 6, 'I8YYMD');
 
-* Determine the beginning and end of the previous week.
-SET &LW_BDT    = AYMD (&TW_BDT, -7, 'I8YYMD');
-SET &LW_EDT    = AYMD (&TW_BDT, -1, 'I8YYMD');
 
-SET &PW_BDT    = AYMD (&LW_BDT, -7, 'I8YYMD');
-SET &PW_EDT    = AYMD (&LW_BDT, -1, 'I8YYMD');
 
-SET &LW_WK_NO  = HPART(&LW_DATE, 'week', 'I2');
-SET &LW_YR_NO  = HPART(&LW_DATE, 'year', 'I4');
-SET &LW_DT     = DATECVT(HDATE(&LW_DATE, 'YYMD'),'YYMD','I8YYMD');
 
 
-* Yesterday's date and Beginning date for last 12 Full weeks
-SET &LD_DT     = DATECVT(DATEADD(DATECVT(&ASOFDATE,'I8YYMD','YYMD'), 'D', -1),'YYMD','I8YYMD') ;
-SET &12WK_BDT  = AYMD (&TW_BDT, -84,'I8YYMD') ;
-SET &PD_DT = AYMD(&ASOFDATE, -2, 'I8');
-*-SET &PD_DT     = DATECVT(DATEADD(DATECVT(&ASOFDATE,'I8YYMD','YYMD'), 'D', -2),'YYMD','I8YYMD') ;
 
 
-* Determine the beginning and end of current and last month.
-SET &TM_BDT    = DATECVT(DATEMOV(DATECVT(&ASOFDATE,'I8YYMD','YYMD'), 'BOM'),'YYMD','I8YYMD') ;
-SET &TM_EDT    = DATECVT(DATEMOV(DATECVT(&ASOFDATE,'I8YYMD','YYMD'), 'EOM'),'YYMD','I8YYMD') ;
-SET &LM_EDT    = AYMD (DATECVT(DATEMOV(DATECVT(&ASOFDATE,'I8YYMD','YYMD'), 'BOM'),'YYMD','I8YYMD'), -1,'I8YYMD') ;
-SET &LM_BDT    = DATECVT(DATEMOV(DATECVT(&LM_EDT,'I8YYMD','YYMD'), 'BOM'),'YYMD','I8YYMD');
-SET &LM_YTD_BDT= EDIT(&LM_BDT,'9999') || '0101';
 
-* Beginning and end of the current and previous year
-SET &TY_BDT = EDIT(&ASOFDATE, '9999$$$$') || '0101';
-SET &TY_EDT = EDIT(&ASOFDATE, '9999$$$$') || '1231';
-SET &LY_EDT = DATECVT(DATEADD(DATECVT(&TY_BDT,'I8YYMD','YYMD'), 'D', -1),'YYMD','I8YYMD');
-SET &LY_BDT = DATECVT(DATEMOV(DATECVT(&LY_EDT,'I8YYMD','YYMD'), 'BOY'),'YYMD','I8YYMD');
 
-* PAWOB DATES MONTHLY
-SET &LAST_MONTH = EDIT(&LM_BDT,'$$$$99');
-SET &LAST_MONTH_YEAR = EDIT(&LM_BDT,'9999$$');
-SET &LAST_MONTH_LAST_YEAR = &LAST_MONTH_YEAR -'1';
 
-SET &LY_YYYY= EDIT(&LY_BDT, '9999$$$$');
 
-* Last Business day of the month
-SET &TM_LBDT    = IF DOWK(&TM_EDT,DW) NE 'SAT' OR 'SUN' THEN &TM_EDT
-                  ELSE DATECVT(DATEMOV(DATECVT(&TM_EDT,'I8YYMD','YYMD'), 'BD-'),'YYMD','I8YYMD');
 
-* Payroll Dates
-SET &CPP_EDT = IF &RN_DD GT 15 THEN &TM_EDT ELSE &RN_YYYY || &RN_MM || '15';
-SET &CPP_BDT = IF &RN_DD GT 15 THEN &RN_YYYY || &RN_MM || '16' ELSE &TM_BDT;
-SET &LPP_EDT = IF &RN_DD GT 15 THEN &RN_YYYY || &RN_MM || '15' ELSE &LM_EDT;
-SET &LPP_BDT = IF &RN_DD GT 15 THEN &TM_BDT ELSE EDIT(&LPP_EDT, '999999') || '16';
 
-* Middle month dates (from 16th last month to 15th of current month)
-SET &MM_BDT  = EDIT(&LM_BDT, '999999') || '16';
-SET &MM_EDT  = EDIT(&TM_BDT, '999999') || '15';
 
-* Quarters begin and end dates
-SET &Q1_BDT  = &RN_YYYY || '01' || '01';
-SET &Q1_EDT  = &RN_YYYY || '03' || '31';
-SET &Q2_BDT  = &RN_YYYY || '04' || '01';
-SET &Q2_EDT  = &RN_YYYY || '06' || '30';
-SET &Q3_BDT  = &RN_YYYY || '07' || '01';
-SET &Q3_EDT  = &RN_YYYY || '09' || '30';
-SET &Q4_BDT  = &RN_YYYY || '10' || '01';
-SET &Q4_EDT  = &RN_YYYY || '12' || '31';
-SET &Q1LY_BDT = EDIT(&LY_BDT, '9999$$$$') || '01' || '01';
-SET &Q1LY_EDT = EDIT(&LY_BDT, '9999$$$$') || '03' || '31';
-SET &Q2LY_BDT = EDIT(&LY_BDT, '9999$$$$') || '04' || '01';
-SET &Q2LY_EDT = EDIT(&LY_BDT, '9999$$$$') || '06' || '30';
-SET &Q3LY_BDT = EDIT(&LY_BDT, '9999$$$$') || '07' || '01';
-SET &Q3LY_EDT = EDIT(&LY_BDT, '9999$$$$') || '09' || '30';
-SET &Q4LY_BDT = EDIT(&LY_BDT, '9999$$$$') || '10' || '01';
-SET &Q4LY_EDT = EDIT(&LY_BDT, '9999$$$$') || '12' || '31';
 
-*  Current and previous quarters.
-SET &CUR_QTR= DECODE &RN_MM ('01' Q1 '02' Q1 '03' Q1 '04' Q2 '05' Q2 '06' Q2 '07' Q3 '08' Q3 '09' Q3 '10' Q4 '11' Q4 '12' Q4);
-SET &TQ_QY = &CUR_QTR || '-' || &RN_YYYY;
-SET &LST_QTR= DECODE &CUR_QTR (Q1 Q4 Q2 Q1 Q3 Q2 ELSE Q3);
-SET &LQ_QY = IF &LST_QTR EQ 'Q4' THEN &LST_QTR || '-' || &LY_YYYY ELSE &LST_QTR || '-' || &RN_YYYY;
 
-SET &TQ_BDT = IF &CUR_QTR EQ 'Q1' THEN &Q1_BDT ELSE
-              IF &CUR_QTR EQ 'Q2' THEN &Q2_BDT ELSE
-              IF &CUR_QTR EQ 'Q3' THEN &Q3_BDT ELSE &Q4_BDT;
-SET &TQ_EDT = IF &CUR_QTR EQ 'Q1' THEN &Q1_EDT ELSE
-              IF &CUR_QTR EQ 'Q2' THEN &Q2_EDT ELSE
-              IF &CUR_QTR EQ 'Q3' THEN &Q3_EDT ELSE &Q4_EDT;
-SET &LQ_BDT = IF &CUR_QTR EQ 'Q1' THEN &Q4LY_BDT ELSE
-              IF &CUR_QTR EQ 'Q2' THEN &Q1_BDT ELSE
-              IF &CUR_QTR EQ 'Q3' THEN &Q2_BDT ELSE &Q3_BDT;
-SET &LQ_EDT = IF &CUR_QTR EQ 'Q1' THEN &Q4LY_EDT ELSE
-              IF &CUR_QTR EQ 'Q2' THEN &Q1_EDT ELSE
-              IF &CUR_QTR EQ 'Q3' THEN &Q2_EDT ELSE &Q3_EDT;
 
 
 
 
 
 
 
-SET &iso_date_alpha = HYYWD(HDTTM(DATECVT(&LW_EDT,'I8YYMD','YYMD'), 8, 'HYYMDIA'), 'A10');
-SET &iso_year_alpha = EDIT(&iso_date_alpha, '9999$$$$$$$');
-SET &iso_week_alpha = EDIT(&iso_date_alpha, '$$$$$$99$$');
 
-SET &ISO_YEAR = ATODBL(&iso_year_alpha, '4', 'I4');
-SET &ISO_WEEK = ATODBL(&iso_week_alpha, '2', 'I2');
 
-* Temporary correction until Dec 31, 2012. The '+1' must be removed on Jan 1, 2013
-*-SET &ISO_WEEK = &ISO_WEEK + 1;
-* Fixed on Jan 7, 2013:
-SET &ISO_WEEK = &ISO_WEEK;
 
 
 
 
 
 
-*-TYPE   Value                                             DATE FMT      Parm fld
-*-TYPE
-*-TYPE   &ASOFDATE     Default to Current Date (YYMD)       YYYYMMDD      parm = &|ASOFDATE
-*-TYPE   &TOD1        Time of Day (12:05)                  YYYYMMDD      parm = &|TOD1
-*-TYPE   &DOW          Day of the week                      MON           parm = &|DOW
-*-TYPE   &WK_NO           Current week number                  I2            parm = &|WK_NO
-*-TYPE   &YR_NO         Current year number                  I4            parm = &|YR_NO
-*-TYPE   &TW_BDT     This week begin date                 YYYYMMDD      parm = &|TW_BDT
-*-TYPE   &TW_EDT     This week End date                   YYYYMMDD      parm = &|TW_EDT
-*-TYPE   &LW_BDT     Last week Begin date                 YYYYMMDD      parm = &|LW_BDT
-*-TYPE   &LW_EDT     Last week End date                   YYYYMMDD      parm = &|LW_EDT
-*-TYPE   &LW_DT     Current Date minus 7 days            YYYYMMDD      parm = &|LW_DT
-*-TYPE   &LW_WK_NO           Last week number                     I2            parm = &|LW_WK_NO
-*-TYPE   &LW_YR_NO         Last week year number                I4            parm = &|LW_YR_NO
-*-TYPE   &PD_DT     Prev day date (i.e. yest - 1 )       YYYYMMDD      parm = &|PD_DT
-*-TYPE   &LD_DT     Last day date (i.e. yesterday)       YYYYMMDD      parm = &|LD_DT
-*-TYPE   &TM_BDT     This month Begin date                YYYYMMDD      parm = &|TM_BDT
-*-TYPE   &TM_EDT     This month End date                  YYYYMMDD      parm = &|TM_EDT
-*-TYPE   &LM_EDT     Last month End Date                  YYYYMMDD      parm = &|LM_EDT
-*-TYPE   &LM_BDT     Last month Begin Date                YYYYMMDD      parm = &|LM_BDT
-*-TYPE   &LM_YTD_BDT     Last month YTD Begin Date            YYYYMMDD      parm = &|LM_YTD_BDT
-*-TYPE   &TY_BDT     This year Begin Date                 YYYYMMDD      parm = &|TY_BDT
-*-TYPE   &TY_EDT     This year End Date                   YYYYMMDD      parm = &|TY_EDT
-*-TYPE   &LY_BDT     Last year Begin Date                 YYYYMMDD      parm = &|LY_BDT
-*-TYPE   &LY_EDT     Last year End Date                   YYYYMMDD      parm = &|LY_EDT
-*-TYPE   &TM_LBDT     This month Last business day         YYYYMMDD      parm = &|TM_LBDT
-*-TYPE   &TM_LBDT     Last month Last businees day         YYYYMMDD      parm = &|TM_LBDT
-*-TYPE   &MM_BDT     Mid Month Begin date                 YYYYMMDD      parm = &|MM_BDT
-*-TYPE   &MM_EDT     Mid Month end date                   YYYYMMDD      parm = &|MM_EDT
-*-TYPE   &Q1_BDT     Q1 Begin date                        YYYYMMDD      parm = &|Q1_BDT
-*-TYPE   &Q1_EDT     Q1 End date                          YYYYMMDD      parm = &|Q1_EDT
-*-TYPE   &Q2_BDT     Q2 Begin date                        YYYYMMDD      parm = &|Q2_BDT
-*-TYPE   &Q2_EDT     Q2 End date                          YYYYMMDD      parm = &|Q2_EDT
-*-TYPE   &Q3_BDT     Q3 Begin date                        YYYYMMDD      parm = &|Q3_BDT
-*-TYPE   &Q3_EDT     Q3 End date                          YYYYMMDD      parm = &|Q3_EDT
-*-TYPE   &Q4_BDT     Q4 Begin date                        YYYYMMDD      parm = &|Q4_BDT
-*-TYPE   &Q4_EDT     Q4 End date                          YYYYMMDD      parm = &|Q4_EDT
-*-TYPE   &Q1LY_BDT     Q1 Last Year Begin date              YYYYMMDD      parm = &|Q1LY_BDT
-*-TYPE   &Q1LY_EDT     Q1 Last Year End date                YYYYMMDD      parm = &|Q1LY_EDT
-*-TYPE   &Q2LY_BDT     Q2 Last Year Begin date              YYYYMMDD      parm = &|Q2LY_BDT
-*-TYPE   &Q2LY_EDT     Q2 Last Year End date                YYYYMMDD      parm = &|Q2LY_EDT
-*-TYPE   &Q3LY_BDT     Q3 Last Year Begin date              YYYYMMDD      parm = &|Q3LY_BDT
-*-TYPE   &Q3LY_EDT     Q4 Last Year End date                YYYYMMDD      parm = &|Q3LY_EDT
-*-TYPE   &Q4LY_BDT     Q4 Last Year Begin date              YYYYMMDD      parm = &|Q4LY_BDT
-*-TYPE   &Q4LY_EDT     Q4 Last Year End date                YYYYMMDD      parm = &|Q4LY_EDT
-*-TYPE   &TQ_BDT     This Quarter Begin date              YYYYMMDD      parm = &|TQ_BDT
-*-TYPE   &TQ_EDT     This Quarter End date                YYYYMMDD      parm = &|TQ_EDT
-*-TYPE   &LQ_BDT     Last Quarter Begin date              YYYYMMDD      parm = &|LQ_BDT
-*-TYPE   &LQ_EDT     Last Quarter End date                YYYYMMDD      parm = &|LQ_EDT
-*-TYPE   &TQ_QY      This Quarter Quarter-Year            QQ-YYYY       parm = &|TQ_QY
-*-TYPE   &LQ_QY      Last Quarter Quarter-Year            QQ-YYYY       parm = &|LQ_QY
-*-TYPE   &CPP_BDT     Current Pay Period Begin Date        YYYYMMDD      parm = &|CPP_BDT
-*-TYPE   &CPP_EDT     Current Pay Period End Date          YYYYMMDD      parm = &|CPP_EDT
-*-TYPE   &LPP_BDT     Last Pay Period Begin Date           YYYYMMDD      parm = &|LPP_BDT
-*-TYPE   &LPP_EDT     Last Pay Period End Date             YYYYMMDD      parm = &|LPP_EDT
 
-*-TYPE   &ISO_YEAR         ISO year number                      I4            parm = &|ISO_YEAR
-*-TYPE   &ISO_WEEK           ISO week number                      I2            parm = &|ISO_WEEK
 
