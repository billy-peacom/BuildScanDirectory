-* File intl_otp_carrier_summary.fex
-************************************************************-
-* International OTP Report Summary
-* 20190130 Anton Haiduk
-************************************************************-

-*-INCLUDE IBFS:/WFC/Repository/SOC/Production/China_ARR_and_DEP/intl_otp_parameters.fex
-INCLUDE ./intl_otp_parameters.fex


TABLE FILE INTL_OTP_HOLD_ALL
SUM
	FLIGHTS
	DEP_DELAY_TT_00
	DEP_DELAY_TT_05
	ARR_DELAY_TT_00
	ARR_DELAY_TT_14
	MTA_EVENTS
	MTA_VIOLATIONS
	MTA_SUCCESSES
	DIV

	FLIGHTS_INB
	ARR_DELAY_TT_00_INB
	ARR_DELAY_TT_14_INB

	FCST_FLTS_TGT
	DEP_DLYS_TGT
	A00_DLYS_TGT
	A14_DLYS_TGT
	MTA_VIOL_TGT
	MTA_SUCCESS_TGT
	MTA_EVENTS_TGT
	FCST_CNLS_TGT

	FCST_FLTS_TGT_INB
	A00_DLYS_TGT_INB
	A14_DLYS_TGT_INB

	CNL
	RTG

BY PERIOD_NAME
BY CARRIER_NME
BY TOTAL COMPUTE CARRIER_SORT/I11 = 1;
BY REGION

ON TABLE HOLD AS INTL_OTP_HOLD_ALL_1 FORMAT FOCUS

END



TABLE FILE INTL_OTP_HOLD_ALL
SUM
	FLIGHTS
	DEP_DELAY_TT_00
	DEP_DELAY_TT_05
	ARR_DELAY_TT_00
	ARR_DELAY_TT_14
	MTA_EVENTS
	MTA_VIOLATIONS
	MTA_SUCCESSES
	DIV

	FLIGHTS_INB
	ARR_DELAY_TT_00_INB
	ARR_DELAY_TT_14_INB

	FCST_FLTS_TGT
	DEP_DLYS_TGT
	A00_DLYS_TGT
	A14_DLYS_TGT
	MTA_VIOL_TGT
	MTA_SUCCESS_TGT
	MTA_EVENTS_TGT
	FCST_CNLS_TGT

	FCST_FLTS_TGT_INB
	A00_DLYS_TGT_INB
	A14_DLYS_TGT_INB

	CNL
	RTG

BY PERIOD_NAME
BY TOTAL COMPUTE CARRIER_NME/A100V MISSING ON = 'Air Canada + Rouge';
BY TOTAL COMPUTE CARRIER_SORT/I11 = 99;
BY REGION

ON TABLE HOLD AS INTL_OTP_HOLD_ALL_2 FORMAT FOCUS

END


TABLE FILE INTL_OTP_HOLD_ALL_1
PRINT *

ON TABLE HOLD AS INTL_OTP_HOLD_ALL_3 FORMAT FOCUS

MORE
FILE INTL_OTP_HOLD_ALL_2

END




TABLE FILE INTL_OTP_HOLD_ALL_3
SUM
	COMPUTE RANK/I11 = IF PERIOD_NAME EQ LAST PERIOD_NAME THEN RANK + 1 ELSE 1; NOPRINT

	FLIGHTS/I11C AS FLTS

	COMPUTE D00/P12% MISSING ON = 100*(1 - DEP_DELAY_TT_00/FLIGHTS); AS D0
    COMPUTE D00_TGT/P12% MISSING ON = 100*(1 - DEP_DLYS_TGT/FCST_FLTS_TGT); AS TGT
	COMPUTE D00_COLOR/A20 = IF D00_TGT EQ MISSING OR D00 IS MISSING THEN 'BLACK' ELSE IF D00 GE D00_TGT THEN 'GREEN' ELSE IF D00 LT D00_TGT THEN 'RED' ELSE 'BLACK'; NOPRINT

	COMPUTE D05/P12% MISSING ON = 100*(1 - DEP_DELAY_TT_05/FLIGHTS); AS D5

	COMPUTE A00_PERF/P12% MISSING ON = 100*(1 - ARR_DELAY_TT_00/FLIGHTS); AS A0
	COMPUTE A00_TGT/P12% MISSING ON = 100*(1 - A00_DLYS_TGT/FCST_FLTS_TGT); AS 'TGT'
	COMPUTE A00_COLOR/A20 = IF A00_TGT EQ MISSING OR A00_PERF IS MISSING THEN 'BLACK' ELSE IF A00_PERF GE A00_TGT THEN 'GREEN' ELSE IF A00_PERF LT A00_TGT THEN 'RED' ELSE 'BLACK'; NOPRINT

	-* COMPUTE A14/P12% MISSING ON = 100*(1 - A14_DLYS_TGT/FLIGHTS);
	-* COMPUTE A14_TGT/P12% MISSING ON = 100*(1 - A14_DLYS_TGT/FCST_FLTS_TGT); AS 'TGT'
    -* COMPUTE A14_COLOR/A20 = IF A14_TGT EQ MISSING THEN 'BLACK' ELSE IF A14 GE A14_TGT THEN 'GREEN' ELSE IF A14 LT A14_TGT THEN 'RED' ELSE 'BLACK'; NOPRINT

	COMPUTE T00/P12% MISSING ON = 100*(MTA_EVENTS - MTA_VIOLATIONS)/MTA_EVENTS; AS T0
	COMPUTE T00_TGT/P12% MISSING ON = 100*(1 - MTA_VIOL_TGT/MTA_EVENTS_TGT); AS 'TGT'
	COMPUTE T00_COLOR/A20 = IF T00_TGT EQ MISSING OR T00 IS MISSING THEN 'BLACK' ELSE IF T00 GE T00_TGT THEN 'GREEN' ELSE IF T00 LT T00_TGT THEN 'RED' ELSE 'BLACK'; NOPRINT

	COMPUTE MTA_EXPOSURE/P12% = 100*MTA_EVENTS/FLIGHTS; AS 'MTA,EXP'

	MTA_EVENTS AS 'MTA,EVENTS'
	MTA_SUCCESSES AS 'MTA,SCSS'

	COMPUTE A00_INB/P12% MISSING ON = 100*(1 - ARR_DELAY_TT_00_INB/FLIGHTS_INB); AS 'A0,INB'
	COMPUTE A00_TGT_INB/P12% MISSING ON = 100*(1 - A00_DLYS_TGT_INB/FCST_FLTS_TGT_INB); AS TGT
	COMPUTE A00_INB_COLOR/A20 = IF A00_TGT_INB EQ MISSING OR A00_INB IS MISSING THEN 'BLACK' ELSE IF A00_INB GE A00_TGT_INB THEN 'GREEN' ELSE IF A00_INB LT A00_TGT_INB THEN 'RED' ELSE 'BLACK'; NOPRINT

	-* COMPUTE A14_INB/P12% MISSING ON = 100*(1 - ARR_DELAY_TT_14_INB/FLIGHTS_INB); AS 'A14 INB'
	-* COMPUTE A14_TGT_INB/P12% MISSING ON = 100*(1 - A14_DLYS_TGT_INB/FCST_FLTS_TGT_INB); AS 'A14 TGT INB'
	-* COMPUTE A14_INB_COLOR/A20 = IF A14_TGT_INB EQ MISSING THEN 'BLACK' ELSE IF A14_INB GE A14_TGT_INB THEN 'GREEN' ELSE IF A14_INB LT A14_TGT_INB THEN 'RED' ELSE 'BLACK'; NOPRINT

	COMPUTE CF/P12.1% MISSING ON = 100*(1 - CNL/(FLIGHTS + CNL));
	COMPUTE CF_TGT/P12.1% MISSING ON = 100*(1 - FCST_CNLS_TGT/(FCST_FLTS_TGT + FCST_CNLS_TGT)); AS TGT
	COMPUTE CF_COLOR/A20 = IF CF_TGT EQ MISSING OR CF IS MISSING THEN 'BLACK' ELSE IF CF GE CF_TGT THEN 'GREEN' ELSE IF CF LT CF_TGT THEN 'RED' ELSE 'BLACK'; NOPRINT

	CNL
	RTG
	DIV

BY HIGHEST PERIOD_NAME AS ''
BY CARRIER_SORT NOPRINT
BY CARRIER_NME AS ''

WHERE REGION EQ &REGIONS;

ON TABLE PCHOLD FORMAT PDF

ON TABLE SET PAGE-NUM NOLEAD
ON TABLE NOTOTAL
ON TABLE SET HTMLCSS ON
ON TABLE SET STYLE *
-*	INCLUDE = IBFS:/WFC/Repository/SOC/Production/China_ARR_and_DEP/intl_otp.sty,
    INCLUDE = /EDA/EDASERVE/ops_analytics/styles/intl_otp.sty,
$
    PAGESIZE='Letter',
    ORIENTATION=LANDSCAPE,
$
	TITLETEXT='INTL OPS SUMMARY',
$
	DEFMACRO=COND_UNDERLINE,
    MACTYPE=RULE,
    WHEN=RANK EQ 1,
$
    DEFMACRO=COND_D00_GREEN,
    MACTYPE=RULE,
    WHEN=D00_COLOR EQ 'GREEN',
$
    DEFMACRO=COND_D00_RED,
    MACTYPE=RULE,
    WHEN=D00_COLOR EQ 'RED',
$
    DEFMACRO=COND_A00_GREEN,
    MACTYPE=RULE,
    WHEN=A00_COLOR EQ 'GREEN',
$
    DEFMACRO=COND_A00_RED,
    MACTYPE=RULE,
    WHEN=A00_COLOR EQ 'RED',
$
    DEFMACRO=COND_T00_RED,
    MACTYPE=RULE,
    WHEN=T00_COLOR EQ 'RED',
$
    DEFMACRO=COND_T00_GREEN,
    MACTYPE=RULE,
    WHEN=T00_COLOR EQ 'GREEN',
$
    DEFMACRO=COND_A00_INB_GREEN,
    MACTYPE=RULE,
    WHEN=A00_INB_COLOR EQ 'GREEN',
$
    DEFMACRO=COND_A00_INB_RED,
    MACTYPE=RULE,
    WHEN=A00_INB_COLOR EQ 'RED',
$
    DEFMACRO=COND_CF_GREEN,
    MACTYPE=RULE,
    WHEN=CF_COLOR EQ 'GREEN',
$
    DEFMACRO=COND_CF_RED,
    MACTYPE=RULE,
    WHEN=CF_COLOR EQ 'RED',
$

TYPE=REPORT,
     GRAPHCOLOR='GREEN',
$
TYPE=REPORT,
     RIGHTGAP=0.04,
     LEFTGAP=0.04,
     TOPGAP=0.00,
     BOTTOMGAP=0.01,
$
TYPE=DATA,
     SIZE=6,
$
TYPE=TITLE,
     SIZE=6,
$
TYPE=SUBTOTAL,
	BY=1,
	SIZE=6,
	STYLE=BOLD,
$
TYPE=DATA,
     COLUMN=N1,
     STYLE=BOLD,
$
TYPE=DATA,
     COLUMN=N2,
     STYLE=BOLD,
$
-* TYPE=DATA,
     -* COLUMN=N3,
     -* STYLE=BOLD,
-* $
TYPE=DATA,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=OFF,
     BORDER-LEFT=OFF,
     BORDER-RIGHT=OFF,
     BORDER-TOP-COLOR=RGB(75 79 84),
     MACRO=COND_UNDERLINE,
$
TYPE=DATA,
     COLUMN=D00,
     COLOR='GREEN',
     MACRO=COND_D00_GREEN,
$
TYPE=DATA,
     COLUMN=D00,
     COLOR='RED',
     MACRO=COND_D00_RED,
$
TYPE=DATA,
     COLUMN=A00_PERF,
     COLOR='GREEN',
     MACRO=COND_A00_GREEN,
$
TYPE=DATA,
     COLUMN=A00_PERF,
     COLOR='RED',
     MACRO=COND_A00_RED,
$
TYPE=DATA,
     COLUMN=T00,
     COLOR='GREEN',
     MACRO=COND_T00_GREEN,
$
TYPE=DATA,
     COLUMN=T00,
     COLOR='RED',
     MACRO=COND_T00_RED,
$
TYPE=DATA,
     COLUMN=A00_INB,
     COLOR='GREEN',
     MACRO=COND_A00_INB_GREEN,
$
TYPE=DATA,
     COLUMN=A00_INB,
     COLOR='RED',
     MACRO=COND_A00_INB_RED,
$
TYPE=DATA,
     COLUMN=CF,
     COLOR='GREEN',
     MACRO=COND_CF_GREEN,
$
TYPE=DATA,
     COLUMN=CF,
     COLOR='RED',
     MACRO=COND_CF_RED,
$




TYPE=REPORT,
     OBJECT=MENU,
     COLOR='WHITE',
     HOVER-COLOR=RGB(66 70 73),
     BACKCOLOR=RGB(102 102 102),
     HOVER-BACKCOLOR=RGB(218 225 232),
     BORDER-COLOR='WHITE',
$
TYPE=REPORT,
     OBJECT=STATUS-AREA,
     COLOR='WHITE',
     BACKCOLOR=RGB(102 102 102),
$
TYPE=REPORT,
     OBJECT=CURRENT-ROW,
     HOVER-BACKCOLOR=RGB(218 225 232),
     BACKCOLOR=RGB(200 200 200),
$
TYPE=REPORT,
     OBJECT=CALC-AREA,
     COLOR='WHITE',
     BACKCOLOR=RGB(102 102 102),
$

ENDSTYLE
END
