-* File: ftw_daily_netline_extract.fex
-*Procedure by Pranav Dobhal AC044640 Started at 1/14/2013 9:47:57 AM
-* Modified by Konstantin Koton AC048724, 2015-12-02 (Migrated to WF8)
-* Refactored by Konstantin Koton AC048724, 2019-02-14 (FEX run time reduction)


-*INCLUDE IBFS:/WFC/Repository/SOC/Production/Supporting_Files/common_dates_ibi.fex
-*-INCLUDE IBFS:/EDA/EDASERVE/Operations_Analytics/procedures/common_dates_ibi.fex
-INCLUDE IBFS:/EDA/EDASERVE/ops_analytics/procedures/common_dates_ibi.fex

SET HOLDLIST = PRINTONLY
SET ASNAMES = ON
SET HOLDATTR = OFF
SET TITLELINE = SKIP
SET DROPBLNKLINE = ON
SET NODATA = ''

-SET &STR_DT = AYMD(&LD_DT, 1, 'I8YYMD');
-SET &END_DT = AYMD(&LD_DT, 3, 'I8YYMD');

ENGINE SQLORA SET DEFAULT_CONNECTION netline
SQL SQLORA PREPARE SQLOUT FOR

SELECT
 A.LEG_NO AS LEG_NO
,A.FN_CARRIER
,A.FN_NUMBER AS FLIGHT_NUMBER
,(A.DAY_OF_ORIGIN + A.LDO_OFFSET) AS LOCAL_DATE
,CAST(A.DEP_AP_SCHED AS CHAR(3)) AS DEP_AP_SCHED
,A.DEP_SCHED_DT AS SCHED_DEP_DATE_UTC
,CAST(A.ARR_AP_SCHED AS CHAR(3)) AS ARR_AP_SCHED
,A.ARR_AP_SCHED AS WAB_BAG_REGION
,A.ARR_SCHED_DT AS SCHED_ARR_DATE_UTC
,A.AC_REGISTRATION
,CAST(TRIM(leading 'J' from REGEXP_SUBSTR(
    substr(a.AC_VERSION, 1, length(a.AC_VERSION) - 3),
    '([J])([[:digit:]]{1,})', 1, 1)) AS INT) as J_COUNT
,CAST(TRIM(leading 'W' from REGEXP_SUBSTR(
    substr(a.AC_VERSION, 1, length(a.AC_VERSION) - 3),
    '([W])([[:digit:]]{1,})', 1, 1)) AS INT) as W_COUNT
,CAST(TRIM(leading 'Y' from REGEXP_SUBSTR(
    substr(a.AC_VERSION, 1, length(a.AC_VERSION) - 3),
    '([Y])([[:digit:]]{1,})', 1, 1))AS INT)  as Y_COUNT
,COALESCE((CASE
	WHEN A.AC_REGISTRATION LIKE '%XX%'
		THEN '0'
	WHEN A.AC_REGISTRATION LIKE '%V%'
		THEN '0'
		ELSE SUBSTR(A.AC_REGISTRATION, 3, LENGTH(A.AC_REGISTRATION) - 2)
END), '0') AS ACFT_REGS_CDE
,A.AC_SUBTYPE AS ACFT_SUBTYPE_CDE
, CAST(CASE WHEN a.ARR_AP_SCHED in ('SYD') THEN 'AUSTRALIA'
	 WHEN a.ARR_AP_SCHED in ('BOG','CCS','EZE','GRU','LIM','MEX','SCL') THEN 'SOUTH AMERICA'
	 WHEN a.ARR_AP_SCHED in ('DEL','HKG','ICN','KIX','NGO','NRT','PEK','PVG','SEL','SIN') THEN 'ASIA'
	 ELSE 'DEFAULT' END as nvarchar2(20)) AS  PAX_WT_REGION
,DEP.COUNTRY_CODE AS DEP_CC
,ARR.COUNTRY_CODE AS ARR_CC
,CASE WHEN DEP.COUNTRY_CODE = 'CA' and ARR.COUNTRY_CODE = 'CA' THEN 'DOM'
WHEN DEP.COUNTRY_CODE = 'US' OR ARR.COUNTRY_CODE = 'US' THEN 'TB'
ELSE 'INT' END AS SECTOR

FROM SCHEDOPS.LEG A
LEFT JOIN schedops.AIRPORT DEP
	ON (a.DEP_AP_SCHED = DEP.IATA_AP_CODE)
		AND (sysdate between DEP.VALID_SINCE AND DEP.VALID_UNTIL)
		AND (DEP.OPERATING_CARRIER = 'XXX')

LEFT JOIN schedops.AIRPORT ARR
	ON (a.ARR_AP_SCHED = ARR.IATA_AP_CODE)
		AND (sysdate between ARR.VALID_SINCE AND ARR.VALID_UNTIL)
		AND (ARR.OPERATING_CARRIER = 'XXX')

WHERE (A.WHAT_IF = '_')
	AND (A.LEG_TYPE <> 'Y')
	AND (A.LEG_STATE NOT IN ('DEL', 'INC', 'RTR'))
	AND (A.FN_CARRIER IN ('AC', 'ZX', 'QK'))
	AND (A.DAY_OF_ORIGIN BETWEEN TO_DATE('&LD_DT', 'YYYYMMDD') AND TO_DATE('&END_DT', 'YYYYMMDD') )
END


TABLE FILE SQLOUT
PRINT
     LEG_NO
     FN_CARRIER
     FLIGHT_NUMBER
     COMPUTE LOCAL_DATE_1/YYMD = HDATE(LOCAL_DATE, 'YYMD'); AS 'LOCAL_DATE'
     DEP_AP_SCHED/A3
     SCHED_DEP_DATE_UTC
     ARR_AP_SCHED/A3
     WAB_BAG_REGION
     SCHED_ARR_DATE_UTC
     AC_REGISTRATION
     J_COUNT
	 W_COUNT
     Y_COUNT
     ACFT_REGS_CDE/A8V
-*     AC_SUBTYPE
     PAX_WT_REGION
-*	 DEP_AP
-*	 ARR_AP
	 ACFT_SUBTYPE_CDE/A3
     SECTOR
	 COMPUTE JOIN_KEY/A10V = DEP_AP_SCHED || ARR_AP_SCHED;

ON TABLE HOLD AS FTW_HOLD1 FORMAT XFOCUS INDEX JOIN_KEY  PAX_WT_REGION WAB_BAG_REGION SECTOR LEG_NO ACFT_SUBTYPE_CDE ACFT_REGS_CDE
END

TABLE FILE FTW_AIRCRAFT_INFO
PRINT
     ACFT_SUBTYPE_CDE
     ACFT_REGS_CDE
     BAG_CAPACITY
     ALLWD_WT_EST
     ULD_TYPE
     ULD_CAPACITY
     BAG_PER_ULD
     ULD_WT

WHERE ((EFFECTIVE_START_DT LE '&LD_DT') AND (EFFECTIVE_END_DT GE '&LD_DT'));
ON TABLE HOLD AS FTW_HOLD_AIRCRAFT_INFO FORMAT XFOCUS INDEX ACFT_SUBTYPE_CDE ACFT_REGS_CDE
END

TABLE FILE FTW_WAB_PAX_WTS
PRINT
	COMPUTE WT_REGION/A40V = REGION;
     A_WEIGHT
     M_WEIGHT
     F_WEIGHT
     C_WEIGHT
     I_WEIGHT

WHERE ((EFFECTIVE_START_DT LE '&LD_DT') AND (EFFECTIVE_END_DT GE '&LD_DT'));
ON TABLE HOLD AS FTW_HOLD_WAB_PAX_WTS FORMAT XFOCUS INDEX WT_REGION
END

TABLE FILE FTW_WAB_BAG_WTS
PRINT
	COMPUTE BAG_REGION/A3V = REGION;
     BAG_WEIGHT

WHERE ((EFFECTIVE_START_DT LE '&LD_DT') AND (EFFECTIVE_END_DT GE '&LD_DT'));
ON TABLE HOLD AS FTW_HOLD_WAB_BAG_WTS FORMAT XFOCUS INDEX BAG_REGION
END

TABLE FILE FTW_ESTMTD_BAGS
PRINT
     DEP_STN
     ARR_STN
     BAG_PAX_RATIO
     BAG_WEIGHT
     EFFECTIVE_START_DT
     EFFECTIVE_END_DT
	 COMPUTE JOIN_KEY/A10V = DEP_STN || ARR_STN;

WHERE ((EFFECTIVE_START_DT LE '&LD_DT') AND (EFFECTIVE_END_DT GE '&LD_DT'));
ON TABLE HOLD AS FTW_HOLD_ESTMTD_BAGS FORMAT XFOCUS INDEX JOIN_KEY
END

TABLE FILE LEG_SCHEDULED_CABIN
PRINT
     LEG_ID
-*     TOT_CPCITY_CNT
     TOT_BOOK_CNT
-*     CABJ_BOOK_CNT
-*     CABO_BOOK_CNT
-*     CABY_BOOK_AMT

WHERE (FLIGHT_ORIG_DTE FROM '&STR_DT' TO '&END_DT');
ON TABLE HOLD AS FTW_HOLD4 FORMAT XFOCUS INDEX LEG_ID
END

JOIN CLEAR *
JOIN LEFT_OUTER
	PAX_WT_REGION IN FTW_HOLD1 TO MULTIPLE
	WT_REGION IN FTW_HOLD_WAB_PAX_WTS TAG J1 AS J1
END

JOIN LEFT_OUTER
	FILE FTW_HOLD1 AT ACFT_SUBTYPE_CDE TO MULTIPLE
	FILE FTW_HOLD_AIRCRAFT_INFO AT ACFT_SUBTYPE_CDE TAG J2 AS J2
	WHERE (FTW_HOLD1.ACFT_SUBTYPE_CDE EQ J2.ACFT_SUBTYPE_CDE) AND (FTW_HOLD1.ACFT_REGS_CDE EQ J2.ACFT_REGS_CDE);
END

JOIN LEFT_OUTER
	FILE FTW_HOLD1 AT ACFT_SUBTYPE_CDE TO MULTIPLE
	FILE FTW_HOLD_AIRCRAFT_INFO AT ACFT_SUBTYPE_CDE TAG J10 AS J10
	WHERE (FTW_HOLD1.ACFT_SUBTYPE_CDE EQ J10.ACFT_SUBTYPE_CDE) AND (J10.ACFT_REGS_CDE EQ '0');
END

JOIN LEFT_OUTER
	JOIN_KEY IN FTW_HOLD1 TO MULTIPLE
	JOIN_KEY IN FTW_HOLD_ESTMTD_BAGS TAG J6 AS J6
END

JOIN LEFT_OUTER
	WAB_BAG_REGION IN FTW_HOLD1 TO MULTIPLE
	BAG_REGION IN FTW_HOLD_WAB_BAG_WTS TAG J7 AS J7
END

JOIN LEFT_OUTER
	SECTOR IN FTW_HOLD1 TO MULTIPLE
	BAG_REGION IN FTW_HOLD_WAB_BAG_WTS TAG J8 AS J8
END

JOIN LEFT_OUTER
	LEG_NO IN FTW_HOLD1 TO MULTIPLE
	LEG_ID IN FTW_HOLD4 TAG J9 AS J9
END


APP HOLD &&HOLD_OPS
TABLE FILE FTW_HOLD1
PRINT
     LEG_NO
     FN_CARRIER
     FLIGHT_NUMBER
     LOCAL_DATE
     DEP_AP_SCHED
     SCHED_DEP_DATE_UTC
     ARR_AP_SCHED
     WAB_BAG_REGION
     SCHED_ARR_DATE_UTC
     AC_REGISTRATION
     J_COUNT
	 W_COUNT
     Y_COUNT
-*     ACFT_REGS_CDE
-*     ACFT_SUBTYPE_CDE
     PAX_WT_REGION
     SECTOR
     J1.A_WEIGHT
     J1.M_WEIGHT
     J1.F_WEIGHT
     J1.C_WEIGHT
     J1.I_WEIGHT

     COMPUTE ACFT_SUBTYPE_CDE/A3 MISSING ON = IF (J2.ACFT_SUBTYPE_CDE IS MISSING) THEN FTW_HOLD1.ACFT_SUBTYPE_CDE ELSE J2.ACFT_SUBTYPE_CDE;
     COMPUTE ACFT_REGS_CDE/A10 MISSING ON   = IF (J2.ACFT_REGS_CDE EQ '0') THEN J2.ACFT_REGS_CDE ELSE FTW_HOLD1.ACFT_REGS_CDE;
     COMPUTE BAG_CAPACITY/I10 MISSING ON    = IF (J2.BAG_CAPACITY IS MISSING) THEN J10.BAG_CAPACITY ELSE J2.BAG_CAPACITY;
     COMPUTE ALLWD_WT_EST/I10 MISSING ON    = IF (J2.ALLWD_WT_EST IS MISSING) THEN J10.ALLWD_WT_EST ELSE J2.ALLWD_WT_EST;
     COMPUTE ULD_TYPE/A10 MISSING ON        = IF (J2.ULD_TYPE IS MISSING) THEN J10.ULD_TYPE ELSE J2.ULD_TYPE;
     COMPUTE ULD_CAPACITY/I10 MISSING ON    = IF (J2.ULD_CAPACITY IS MISSING) THEN J10.ULD_CAPACITY ELSE J2.ULD_CAPACITY;
     COMPUTE BAG_PER_ULD/I10 MISSING ON     = IF (J2.BAG_PER_ULD IS MISSING) THEN J10.BAG_PER_ULD ELSE J2.BAG_PER_ULD;
     COMPUTE ULD_WT/D10.2 MISSING ON        = IF (J2.ULD_WT IS MISSING) THEN J10.ULD_WT ELSE J2.ULD_WT;

     COMPUTE EST_BAG_PAX_RATIO/D12.6 = J6.BAG_PAX_RATIO;
     COMPUTE EST_BAG_WEIGHT/I10 = J6.BAG_WEIGHT;
     COMPUTE WAB_BAG_WEIGHT = IF (J7.BAG_WEIGHT IS MISSING) THEN J8.BAG_WEIGHT ELSE J7.BAG_WEIGHT;

     COMPUTE ACT_PAX_BOOKED_COUNT/I10 = J9.TOT_BOOK_CNT;
     COMPUTE AIRCRAFT_SEAT_CAPACITY_TOTAL/I10 = Y_COUNT + J_COUNT + W_COUNT;
	 COMPUTE AIRCRAFT_SEAT_CAPACITY_TOTAL_J/I10 = J_COUNT;
	 COMPUTE AIRCRAFT_SEAT_CAPACITY_TOTAL_W/I10 = W_COUNT;
	 COMPUTE AIRCRAFT_SEAT_CAPACITY_TOTAL_Y/I10 = Y_COUNT;

ON TABLE HOLD AS FTW_NETLINE_EXTRACT FORMAT XFOCUS INDEX LEG_NO
END
APP HOLD
