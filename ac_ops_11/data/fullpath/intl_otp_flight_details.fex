-* File intl_otp_flight_details.fex
-************************************************************-
-* International OTP Report - flight details
-* 20190130 Anton Haiduk
-************************************************************-

-*-INCLUDE IBFS:/WFC/Repository/SOC/Production/China_ARR_and_DEP/intl_otp_parameters.fex
-INCLUDE ./intl_otp_parameters.fex

-DEFAULT &INB_OUTB_FLAG = 'INB'

-SET &FILTER = IF &INB_OUTB_FLAG EQ 'OUTB' THEN 'LEG_ACTL_ORIG' ELSE IF &INB_OUTB_FLAG EQ 'INB' THEN 'LEG_ACTL_DEST' ELSE MISSING;

-SET &TITLE = IF &INB_OUTB_FLAG EQ 'OUTB' THEN 'Departing' ELSE IF &INB_OUTB_FLAG EQ 'INB' THEN 'Arriving' ELSE MISSING;


DEFINE FILE LEG_FLOWN
	DELAY_RMRKS_1/A2048V MISSING ON = TRIM('L', SQUEEZ(2048, DELAY_RMRKS, 'A2048V'), 2048, ' ', 1, 'A2048V');
	CARRIER_NME_2/A100V = IF CARRIER_NME EQ 'Air Canada Rouge' THEN 'Rouge' ELSE CARRIER_NME;
END

TABLE FILE LEG_FLOWN
PRINT
	COMPUTE RANK/I11 = IF CARRIER_NME EQ LAST CARRIER_NME THEN RANK + 1 ELSE 1; NOPRINT
	ACFT_SUBTYPE_CDE AS FLEET
	ACFT_REGS_CDE AS FIN
	LEG_DEP_DELAY_MINS AS 'DEP,DLY'
	LEG_ARR_DELAY_MINS AS 'ARR,DLY'
	LEG_FUNC_DELAY_MINS AS 'FUNC,DLY'
	LEG_DEP_DELAY_PRMY_CDE AS 'PRIM,DLY'
	LEG_DEP_DELAY_SECD_CDE AS 'SCNDRY,DLY'
	PAX_FLWN_CNT AS PAX
	-*ACTUAL_GROUND_TME
	ACTL_TAXI_OUT AS 'TXO'
	ACTL_TAXI_IN AS 'TXI'
	LEG_ACTL_BLK_MINS AS 'ACTL,BLOCK'
	LEG_SCHD_BLK_MINS AS 'SKD,BLOCK'
	COMPUTE BKD_SEAT_UTIL/P12.1% = 100*PAX_BOOK_CNT/SEATS_TOTAL; AS 'BOOKED,SEAT,UTIL'
	DELAY_RMRKS_1 AS 'DELAY COMMENTS'

BY	CARRIER_NME_2 AS ''
BY	&FILTER NOPRINT
BY	FLIGHT_NUM AS FLT
BY	LEG_ACTL_ORIG AS ORIG
BY	LEG_ACTL_DEST AS DEST

WHERE CARRIER_CDE EQ 'AC' OR 'QK' OR 'ZX';
WHERE OTP_INCLUDE_FLAG EQ 1;
WHERE FLIGHT_ORIG_LCL_DTE EQ &YST_DATE;

-*WHERE &FILTER EQ &STATIONS;
WHERE ORIG_REGION.REGIONS EQ &REGIONS;

ON TABLE PCHOLD FORMAT PDF

ON TABLE SUBHEAD
"&TITLE Flight Details for &YST_DATE"

ON TABLE SET STYLE *
-*     INCLUDE = IBFS:/WFC/Repository/SOC/Production/China_ARR_and_DEP/intl_otp.sty,
     INCLUDE = /EDA/EDASERVE/Ops_Analytics/styles/intl_otp.sty,
$
     PAGESIZE='Letter',
     SQUEEZE=ON,
     ORIENTATION=LANDSCAPE,
$
     DEFMACRO=COND_UNDERLINE,
     MACTYPE=RULE,
     WHEN=RANK EQ 1,
$
TYPE=REPORT,
     GRAPHCOLOR='GREEN',
$
TYPE=REPORT,
     TOPGAP=0.0,
     BOTTOMGAP=0.01,
$
TYPE=DATA,
     SIZE=6,
$
TYPE=TITLE,
     SIZE=6,
$
TYPE=DATA,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=OFF,
     BORDER-LEFT=OFF,
     BORDER-RIGHT=OFF,
     BORDER-TOP-COLOR=RGB(75 79 84),
     MACRO=COND_UNDERLINE,
$
TYPE=REPORT,
     OBJECT=MENU,
     COLOR='WHITE',
     HOVER-COLOR=RGB(66 70 73),
     BACKCOLOR=RGB(102 102 102),
     HOVER-BACKCOLOR=RGB(218 225 232),
     BORDER-COLOR='WHITE',
$
TYPE=REPORT,
     OBJECT=STATUS-AREA,
     COLOR='WHITE',
     BACKCOLOR=RGB(102 102 102),
$
TYPE=REPORT,
     OBJECT=CURRENT-ROW,
     HOVER-BACKCOLOR=RGB(218 225 232),
     BACKCOLOR=RGB(200 200 200),
$
TYPE=REPORT,
     OBJECT=CALC-AREA,
     COLOR='WHITE',
     BACKCOLOR=RGB(102 102 102),
$
TYPE=REPORT,
     COLUMN=DELAY_RMRKS_1,
     WRAP=3.5,
$
TYPE=DATA,
     COLUMN=DELAY_RMRKS_1,
     BORDER-TOP=LIGHT,
     BORDER-BOTTOM=OFF,
     BORDER-LEFT=OFF,
     BORDER-RIGHT=OFF,
     BORDER-TOP-COLOR=RGB(75 79 84),
     MACRO=COND_UNDERLINE,
$
ENDSTYLE
END

