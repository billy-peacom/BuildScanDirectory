FILENAME=leg_flown, SUFFIX=SQLSNO  , $
   SEGMENT=LEG_FLOWN, SEGTYPE=S0, $
     FIELDNAME=CARRIER_CDE, ALIAS=CARRIER_CDE, USAGE=A3V, ACTUAL=A3V,
       MISSING=ON, $
     FIELDNAME=FLIGHT_NUM, ALIAS=FLIGHT_NUM, USAGE=I6, ACTUAL=I4,
       MISSING=ON, $
     FIELDNAME=FLIGHT_SUFFIX_CDE, ALIAS=FLIGHT_SUFFIX_CDE, USAGE=A1, ACTUAL=A1,
       MISSING=ON, $
     FIELDNAME=FLIGHT_ORIG_DTE, ALIAS=FLIGHT_ORIG_DTE, USAGE=YYMD, ACTUAL=DATE,
       MISSING=ON, $
     FIELDNAME=FLIGHT_LEG_NUM, ALIAS=FLIGHT_LEG_NUM, USAGE=I6, ACTUAL=I4,
       MISSING=ON, $
     FIELDNAME=FLIGHT_MULTILEG_IND, ALIAS=FLIGHT_MULTILEG_IND, USAGE=A1, ACTUAL=A1,
       MISSING=ON, $
     FIELDNAME=ACFT_OWNER_CDE, ALIAS=ACFT_OWNER_CDE, USAGE=A3, ACTUAL=A3,
       MISSING=ON, $
     FIELDNAME=ACFT_SUBTYPE_CDE, ALIAS=ACFT_SUBTYPE_CDE, USAGE=A3, ACTUAL=A3,
       MISSING=ON, $
     FIELDNAME=ACFT_LOGL_NUM, ALIAS=ACFT_LOGL_NUM, USAGE=P11, ACTUAL=P8,
       MISSING=ON, $
     FIELDNAME=ACFT_VERS_CDE, ALIAS=ACFT_VERS_CDE, USAGE=A20, ACTUAL=A20,
       MISSING=ON, $
     FIELDNAME=ACFT_REGS_CDE, ALIAS=ACFT_REGS_CDE, USAGE=A10, ACTUAL=A10,
       MISSING=ON, $
     FIELDNAME=COMCL_CARRIER_CDE, ALIAS=COMCL_CARRIER_CDE, USAGE=A3, ACTUAL=A3,
       MISSING=ON, $
     FIELDNAME=OPER_CARRIER_CDE, ALIAS=OPER_CARRIER_CDE, USAGE=A3, ACTUAL=A3,
       MISSING=ON, $
     FIELDNAME=LEG_SCHD_ORIG, ALIAS=LEG_SCHD_ORIG, USAGE=A3, ACTUAL=A3,
       MISSING=ON, $
     FIELDNAME=LEG_SCHD_DEP_DTE, ALIAS=LEG_SCHD_DEP_DTE, USAGE=YYMD, ACTUAL=DATE,
       MISSING=ON, $
     FIELDNAME=LEG_SCHD_DEP_TME, ALIAS=LEG_SCHD_DEP_TME, USAGE=HHIS, ACTUAL=HHIS,
       MISSING=ON, $
     FIELDNAME=LEG_SCHD_DEP_TMS, ALIAS=LEG_SCHD_DEP_TMS, USAGE=HYYMDS, ACTUAL=HYYMDS,
       MISSING=ON, $
     FIELDNAME=LEG_SCHD_DEST, ALIAS=LEG_SCHD_DEST, USAGE=A3, ACTUAL=A3,
       MISSING=ON, $
     FIELDNAME=LEG_SCHD_ARR_DTE, ALIAS=LEG_SCHD_ARR_DTE, USAGE=YYMD, ACTUAL=DATE,
       MISSING=ON, $
     FIELDNAME=LEG_SCHD_ARR_TME, ALIAS=LEG_SCHD_ARR_TME, USAGE=HHIS, ACTUAL=HHIS,
       MISSING=ON, $
     FIELDNAME=LEG_SCHD_ARR_TMS, ALIAS=LEG_SCHD_ARR_TMS, USAGE=HYYMDS, ACTUAL=HYYMDS,
       MISSING=ON, $
     FIELDNAME=LEG_SECTOR_CDE, ALIAS=LEG_SECTOR_CDE, USAGE=A6V, ACTUAL=A6V,
       MISSING=ON, $
     FIELDNAME=LEG_SUBSECTOR_CDE, ALIAS=LEG_SUBSECTOR_CDE, USAGE=A16V, ACTUAL=A16V,
       MISSING=ON, $
     FIELDNAME=LEG_ACTL_ORIG, ALIAS=LEG_ACTL_ORIG, USAGE=A3, ACTUAL=A3,
       MISSING=ON, $
     FIELDNAME=LEG_ACTL_DEP_DTE, ALIAS=LEG_ACTL_DEP_DTE, USAGE=YYMD, ACTUAL=DATE,
       MISSING=ON, $
     FIELDNAME=LEG_ACTL_DEP_TME, ALIAS=LEG_ACTL_DEP_TME, USAGE=HHIS, ACTUAL=HHIS,
       MISSING=ON, $
     FIELDNAME=LEG_ACTL_DEP_TMS, ALIAS=LEG_ACTL_DEP_TMS, USAGE=HYYMDS, ACTUAL=HYYMDS,
       MISSING=ON, $
     FIELDNAME=LEG_ACTL_DEST, ALIAS=LEG_ACTL_DEST, USAGE=A3, ACTUAL=A3,
       MISSING=ON, $
     FIELDNAME=LEG_ACTL_ARR_DTE, ALIAS=LEG_ACTL_ARR_DTE, USAGE=YYMD, ACTUAL=DATE,
       MISSING=ON, $
     FIELDNAME=LEG_ACTL_ARR_TME, ALIAS=LEG_ACTL_ARR_TME, USAGE=HHIS, ACTUAL=HHIS,
       MISSING=ON, $
     FIELDNAME=LEG_ACTL_ARR_TMS, ALIAS=LEG_ACTL_ARR_TMS, USAGE=HYYMDS, ACTUAL=HYYMDS,
       MISSING=ON, $
     FIELDNAME=LEG_STATE_CDE, ALIAS=LEG_STATE_CDE, USAGE=A3, ACTUAL=A3,
       MISSING=ON, $
     FIELDNAME=LEG_DVRT_IND, ALIAS=LEG_DVRT_IND, USAGE=A1, ACTUAL=A1,
       MISSING=ON, $
     FIELDNAME=LEG_ADHOC_IND, ALIAS=LEG_ADHOC_IND, USAGE=A1, ACTUAL=A1,
       MISSING=ON, $
     FIELDNAME=LEG_TYPE_CDE, ALIAS=LEG_TYPE_CDE, USAGE=A1, ACTUAL=A1,
       MISSING=ON, $
     FIELDNAME=LEG_DEP_STAND, ALIAS=LEG_DEP_STAND, USAGE=A3, ACTUAL=A3,
       MISSING=ON, $
     FIELDNAME=LEG_DEP_GATE, ALIAS=LEG_DEP_GATE, USAGE=A3, ACTUAL=A3,
       MISSING=ON, $
     FIELDNAME=LEG_ARR_STAND, ALIAS=LEG_ARR_STAND, USAGE=A3, ACTUAL=A3,
       MISSING=ON, $
     FIELDNAME=LEG_ARR_GATE, ALIAS=LEG_ARR_GATE, USAGE=A3, ACTUAL=A3,
       MISSING=ON, $
     FIELDNAME=LEG_AIRBORNE_DTE, ALIAS=LEG_AIRBORNE_DTE, USAGE=YYMD, ACTUAL=DATE,
       MISSING=ON, $
     FIELDNAME=LEG_AIRBORNE_TME, ALIAS=LEG_AIRBORNE_TME, USAGE=HHIS, ACTUAL=HHIS,
       MISSING=ON, $
     FIELDNAME=LEG_AIRBORNE_TMS, ALIAS=LEG_AIRBORNE_TMS, USAGE=HYYMDS, ACTUAL=HYYMDS,
       MISSING=ON, $
     FIELDNAME=LEG_LANDING_DTE, ALIAS=LEG_LANDING_DTE, USAGE=YYMD, ACTUAL=DATE,
       MISSING=ON, $
     FIELDNAME=LEG_LANDING_TME, ALIAS=LEG_LANDING_TME, USAGE=HHIS, ACTUAL=HHIS,
       MISSING=ON, $
     FIELDNAME=LEG_LANDING_TMS, ALIAS=LEG_LANDING_TMS, USAGE=HYYMDS, ACTUAL=HYYMDS,
       MISSING=ON, $
     FIELDNAME=LDO_OFFSET, ALIAS=LDO_OFFSET, USAGE=I6, ACTUAL=I4,
       MISSING=ON, $
     FIELDNAME=FLIGHT_ORIG_LCL_DTE, ALIAS=FLIGHT_ORIG_LCL_DTE, USAGE=YYMD, ACTUAL=DATE,
       MISSING=ON, $
     FIELDNAME=LEG_SCHD_DEP_LCL_DTE, ALIAS=LEG_SCHD_DEP_LCL_DTE, USAGE=YYMD, ACTUAL=DATE,
       MISSING=ON, $
     FIELDNAME=LEG_SCHD_DEP_LCL_TME, ALIAS=LEG_SCHD_DEP_LCL_TME, USAGE=HHIS, ACTUAL=HHIS,
       MISSING=ON, $
     FIELDNAME=LEG_SCHD_DEP_LCL_TMS, ALIAS=LEG_SCHD_DEP_LCL_TMS, USAGE=HYYMDS, ACTUAL=HYYMDS,
       MISSING=ON, $
     FIELDNAME=LEG_SCHD_ARR_LCL_DTE, ALIAS=LEG_SCHD_ARR_LCL_DTE, USAGE=YYMD, ACTUAL=DATE,
       MISSING=ON, $
     FIELDNAME=LEG_SCHD_ARR_LCL_TME, ALIAS=LEG_SCHD_ARR_LCL_TME, USAGE=HHIS, ACTUAL=HHIS,
       MISSING=ON, $
     FIELDNAME=LEG_SCHD_ARR_LCL_TMS, ALIAS=LEG_SCHD_ARR_LCL_TMS, USAGE=HYYMDS, ACTUAL=HYYMDS,
       MISSING=ON, $
     FIELDNAME=LEG_ACTL_DEP_LCL_DTE, ALIAS=LEG_ACTL_DEP_LCL_DTE, USAGE=YYMD, ACTUAL=DATE,
       MISSING=ON, $
     FIELDNAME=LEG_ACTL_DEP_LCL_TME, ALIAS=LEG_ACTL_DEP_LCL_TME, USAGE=HHIS, ACTUAL=HHIS,
       MISSING=ON, $
     FIELDNAME=LEG_ACTL_DEP_LCL_TMS, ALIAS=LEG_ACTL_DEP_LCL_TMS, USAGE=HYYMDS, ACTUAL=HYYMDS,
       MISSING=ON, $
     FIELDNAME=LEG_ACTL_ARR_LCL_DTE, ALIAS=LEG_ACTL_ARR_LCL_DTE, USAGE=YYMD, ACTUAL=DATE,
       MISSING=ON, $
     FIELDNAME=LEG_ACTL_ARR_LCL_TME, ALIAS=LEG_ACTL_ARR_LCL_TME, USAGE=HHIS, ACTUAL=HHIS,
       MISSING=ON, $
     FIELDNAME=LEG_ACTL_ARR_LCL_TMS, ALIAS=LEG_ACTL_ARR_LCL_TMS, USAGE=HYYMDS, ACTUAL=HYYMDS,
       MISSING=ON, $
     FIELDNAME=SCHED_GROUND_TME, ALIAS=SCHED_GROUND_TME, USAGE=I6, ACTUAL=I4,
       MISSING=ON, $
     FIELDNAME=ACTUAL_GROUND_TME, ALIAS=ACTUAL_GROUND_TME, USAGE=I6, ACTUAL=I4,
       MISSING=ON, $
     FIELDNAME=AVAIL_GROUND_TME, ALIAS=AVAIL_GROUND_TME, USAGE=I6, ACTUAL=I4,
       MISSING=ON, $
     FIELDNAME=TURN_TYP_CDE, ALIAS=TURN_TYP_CDE, USAGE=A2, ACTUAL=A2,
       MISSING=ON, $
     FIELDNAME=MINIMUM_GROUND_TME, ALIAS=MINIMUM_GROUND_TME, USAGE=I6, ACTUAL=I4,
       MISSING=ON, ACCESS_PROPERTY=(INTERNAL), $
     FIELDNAME=ORIG_SCHD_GROUND_TME, ALIAS=ORIG_SCHD_GROUND_TME, USAGE=I6, ACTUAL=I4,
       MISSING=ON, $
     FIELDNAME=LEG_MTA_EVENT_IND, ALIAS=LEG_MTA_EVENT_IND, USAGE=A2V, ACTUAL=A2V,
       MISSING=ON, ACCESS_PROPERTY=(INTERNAL), $
     FIELDNAME=LEG_STARTUP_IND, ALIAS=LEG_STARTUP_IND, USAGE=A2V, ACTUAL=A2V,
       MISSING=ON, $
     FIELDNAME=EST_TAXI_OUT_DURATION, ALIAS=EST_TAXI_OUT_DURATION, USAGE=I6, ACTUAL=I4,
       MISSING=ON, $
     FIELDNAME=EST_TAXI_IN_DURATION, ALIAS=EST_TAXI_IN_DURATION, USAGE=I6, ACTUAL=I4,
       MISSING=ON, $
     FIELDNAME=LEG_DEP_DELAY_IATA_CDE, ALIAS=LEG_DEP_DELAY_IATA_CDE, USAGE=I6, ACTUAL=I4,
       MISSING=ON, $
     FIELDNAME=LEG_DEP_DELAY_PRMY_CDE, ALIAS=LEG_DEP_DELAY_PRMY_CDE, USAGE=A3, ACTUAL=A3,
       MISSING=ON, $
     FIELDNAME=LEG_DEP_DELAY_SECD_CDE, ALIAS=LEG_DEP_DELAY_SECD_CDE, USAGE=A3, ACTUAL=A3,
       MISSING=ON, $
     FIELDNAME=LEG_DEP_DELAY_SUB_CDE, ALIAS=LEG_DEP_DELAY_SUB_CDE, USAGE=A5V, ACTUAL=A5V,
       MISSING=ON, $
     FIELDNAME=LEG_DEP_DELAY_MINS, ALIAS=LEG_DEP_DELAY_MINS, USAGE=I11, ACTUAL=I4,
       MISSING=ON, $
     FIELDNAME=LEG_ARR_DELAY_MINS, ALIAS=LEG_ARR_DELAY_MINS, USAGE=I11, ACTUAL=I4,
       MISSING=ON, $
     FIELDNAME=LEG_FCST_DEP_DELAY_DTE, ALIAS=LEG_FCST_DEP_DELAY_DTE, USAGE=YYMD, ACTUAL=DATE,
       MISSING=ON, $
     FIELDNAME=LEG_FCST_DEP_DELAY_TME, ALIAS=LEG_FCST_DEP_DELAY_TME, USAGE=HHIS, ACTUAL=HHIS,
       MISSING=ON, $
     FIELDNAME=LEG_FCST_ARR_DELAY_DTE, ALIAS=LEG_FCST_ARR_DELAY_DTE, USAGE=YYMD, ACTUAL=DATE,
       MISSING=ON, $
     FIELDNAME=LEG_FCST_ARR_DELAY_TME, ALIAS=LEG_FCST_ARR_DELAY_TME, USAGE=HHIS, ACTUAL=HHIS,
       MISSING=ON, $
     FIELDNAME=LEG_HUSTLE_MINS, ALIAS=LEG_HUSTLE_MINS, USAGE=I11, ACTUAL=I4,
       MISSING=ON, $
     FIELDNAME=LEG_FUNC_DELAY_MINS, ALIAS=LEG_FUNC_DELAY_MINS, USAGE=I11, ACTUAL=I4,
       MISSING=ON, $
     FIELDNAME=LEG_TTL_FUNC_DELAY_MINS, ALIAS=LEG_TTL_FUNC_DELAY_MINS, USAGE=I11, ACTUAL=I4,
       MISSING=ON, $
     FIELDNAME=LEG_EQP_DELAY_MINS, ALIAS=LEG_EQP_DELAY_MINS, USAGE=I11, ACTUAL=I4,
       MISSING=ON, $
     FIELDNAME=DELAY_RMRKS, ALIAS=DELAY_RMRKS, USAGE=A2048V, ACTUAL=A2048V,
       MISSING=ON, $
     FIELDNAME=LEG_SCHD_BLK_MINS, ALIAS=LEG_SCHD_BLK_MINS, USAGE=I11, ACTUAL=I4,
       MISSING=ON, $
     FIELDNAME=LEG_ACTL_BLK_MINS, ALIAS=LEG_ACTL_BLK_MINS, USAGE=I11, ACTUAL=I4,
       MISSING=ON, $
     FIELDNAME=PAX_BOOK_CNT, ALIAS=PAX_BOOK_CNT, USAGE=I11, ACTUAL=I4,
       MISSING=ON, $
     FIELDNAME=PAX_FLWN_CNT, ALIAS=PAX_FLWN_CNT, USAGE=I11, ACTUAL=I4,
       MISSING=ON, $
     FIELDNAME=LEG_PROBLEM_DESC, ALIAS=LEG_PROBLEM_DESC, USAGE=A20V, ACTUAL=A20V,
       MISSING=ON, $
     FIELDNAME=LEG_PRBD_DESC, ALIAS=LEG_PRBD_DESC, USAGE=A20V, ACTUAL=A20V,
       MISSING=ON, $
     FIELDNAME=LEG_CYCLES_AMT, ALIAS=LEG_CYCLES_AMT, USAGE=I11, ACTUAL=I4,
       MISSING=ON, $
     FIELDNAME=LEG_MARKED_IND, ALIAS=LEG_MARKED_IND, USAGE=A1, ACTUAL=A1,
       MISSING=ON, $
     FIELDNAME=LEG_EET_AMT, ALIAS=LEG_EET_AMT, USAGE=I11, ACTUAL=I4,
       MISSING=ON, $
     FIELDNAME=LEG_MARKER_DESC, ALIAS=LEG_MARKER_DESC, USAGE=A64V, ACTUAL=A64V,
       MISSING=ON, $
     FIELDNAME=ACARS_STATE_CDE, ALIAS=ACARS_STATE_CDE, USAGE=A3, ACTUAL=A3,
       MISSING=ON, $
     FIELDNAME=OPER_RMRK, ALIAS=OPER_RMRK, USAGE=A2048V, ACTUAL=A2048V,
       MISSING=ON, $
     FIELDNAME=INTRNL_RMRK, ALIAS=INTRNL_RMRK, USAGE=A2048V, ACTUAL=A2048V,
       MISSING=ON, $
     FIELDNAME=LEG_ID, ALIAS=LEG_ID, USAGE=P11, ACTUAL=P8,
       MISSING=ON, $
     FIELDNAME=PREV_LEG_ID, ALIAS=PREV_LEG_ID, USAGE=P11, ACTUAL=P8,
       MISSING=ON, $
     FIELDNAME=LEG_UPDATE_NUM, ALIAS=LEG_UPDATE_NUM, USAGE=P10, ACTUAL=P8,
       MISSING=ON, $
     FIELDNAME=LEG_UPDATE_KEY, ALIAS=LEG_UPDATE_KEY, USAGE=P11, ACTUAL=P8,
       MISSING=ON, $
     FIELDNAME=LEG_CLUSTER_NUM, ALIAS=LEG_CLUSTER_NUM, USAGE=P11, ACTUAL=P8,
       MISSING=ON, $
     FIELDNAME=LEG_CLUSTER_DTE, ALIAS=LEG_CLUSTER_DTE, USAGE=YYMD, ACTUAL=DATE,
       MISSING=ON, $
     FIELDNAME=POSTDATE, ALIAS=POSTDATE, USAGE=YYMD, ACTUAL=DATE,
       MISSING=ON, $
     FIELDNAME=POSTTIME, ALIAS=POSTTIME, USAGE=HHIS, ACTUAL=HHIS,
       MISSING=ON, $
     FIELDNAME=USER_CDE, ALIAS=USER_CDE, USAGE=A8, ACTUAL=A8, $
     FIELDNAME=ARR_DELAY_CDE_PRMY, ALIAS=ARR_DELAY_CDE_PRMY, USAGE=I6, ACTUAL=I4,
       MISSING=ON, $
     FIELDNAME=ARR_DELAY_SUB_CDE, ALIAS=ARR_DELAY_SUB_CDE, USAGE=A4V, ACTUAL=A4V,
       MISSING=ON, $
 
     DEFINE DEP_DELAY_TT_00/I11=IF LEG_DEP_DELAY_MINS GT 0 THEN 1 ELSE 0; $
     DEFINE DEP_DELAY_TT_05/I11=IF LEG_DEP_DELAY_MINS  GT 5 THEN 1 ELSE 0; $
     DEFINE DEP_DELAY_TT_10/I11=IF  LEG_DEP_DELAY_MINS GT 10 THEN 1 ELSE 0; $
     DEFINE DEP_DELAY_TT_15/I11=IF  LEG_DEP_DELAY_MINS GT 15 THEN 1 ELSE 0; $
     DEFINE DEP_DELAY_TT_14/I11=IF  LEG_DEP_DELAY_MINS GT 14 THEN 1 ELSE 0; $
     DEFINE DEP_DELAY_TT_60/I11=IF  LEG_DEP_DELAY_MINS GT 60 THEN 1 ELSE 0; $
     DEFINE DEP_DELAY_TT_M05/I11=IF LEG_DEP_DELAY_MINS GT -5 THEN 1 ELSE 0; $
 
     DEFINE ARR_DELAY_TT_00/I11=IF LEG_ARR_DELAY_MINS GT 0 THEN 1 ELSE 0; $
     DEFINE ARR_DELAY_TT_05/I11=IF LEG_ARR_DELAY_MINS GT 5 THEN 1 ELSE 0; $
     DEFINE ARR_DELAY_TT_14/I11=IF LEG_ARR_DELAY_MINS GT 14 THEN 1 ELSE 0; $
     DEFINE ARR_DELAY_TT_15/I11=IF LEG_ARR_DELAY_MINS GT 15 THEN 1 ELSE 0; $
     DEFINE ARR_DELAY_TT_60/I11=IF LEG_ARR_DELAY_MINS GT 60 THEN 1 ELSE 0; $
 
     DEFINE REVENUE_FLIGHT_FLAG/I11=IF ((UPPER(CARRIER_CDE) EQ 'AC' AND FLIGHT_NUM LT 7000) OR (UPPER(CARRIER_CDE) EQ 'QK' AND FLIGHT_NUM GE 7650 AND FLIGHT_NUM LE 8999) OR (UPPER(CARRIER_CDE) EQ 'ZX' AND FLIGHT_NUM GE 7300 AND  FLIGHT_NUM LE 7649)) THEN 1 ELSE 0; $
     DEFINE IRROP_DEP_3HR_DELAY/I11=IF  LEG_DEP_DELAY_MINS GT 180 THEN 1 ELSE 0; $
 
     DEFINE ACTL_TAXI_OUT/I11=HDIFF(LEG_AIRBORNE_TMS, LEG_ACTL_DEP_TMS, 'MINUTE', 'I11'); $
     DEFINE ACTL_TAXI_IN/I11=HDIFF(LEG_ACTL_ARR_TMS, LEG_LANDING_TMS, 'MINUTE', 'I11'); $
 
     DEFINE ACTL_BLOCK_TIME/I11 = LEG_ACTL_BLK_MINS;
     DEFINE SCHD_BLOCK_TIME/I11 = LEG_SCHD_BLK_MINS;
 
     DEFINE BLOCK_TIME_MINS_OVER_SCHD/I11=ACTL_BLOCK_TIME - SCHD_BLOCK_TIME;
       TITLE='Block Time Minutes over Planned', $
       DESCRIPTION='Actually, it is ESTIMATED airtime', $
 
     DEFINE ACTL_AIRTIME/I11=ACTL_BLOCK_TIME - ACTL_TAXI_OUT - ACTL_TAXI_IN; $
     DEFINE SCHD_AIRTIME/I11=SCHD_BLOCK_TIME - (EST_TAXI_OUT_DURATION + EST_TAXI_IN_DURATION);
 
     DEFINE TAXI_IN_MINS_OVER_SCHD/I11=ACTL_TAXI_IN - EST_TAXI_IN_DURATION; $
     DEFINE TAXI_OUT_MINS_OVER_SCHD/I11=ACTL_TAXI_OUT - EST_TAXI_OUT_DURATION; $
     DEFINE AIRTIME_MINS_OVER_SCHD/I11=ACTL_AIRTIME - SCHD_AIRTIME; $
     DEFINE ARR_DELAY_MINUTES_TT_14/I11 MISSING ON=IF LEG_ARR_DELAY_MINS GT 14 THEN LEG_ARR_DELAY_MINS - 14 ELSE MISSING; $
     DEFINE BLOCK_TIME_SUCCESSES/I11=IF BLOCK_TIME_MINS_OVER_SCHD LE 0 THEN 1 ELSE 0; $
     DEFINE TAXI_IN_SUCCESSES/I11=IF TAXI_IN_MINS_OVER_SCHD LE 0 THEN 1 ELSE 0; $
     DEFINE TAXI_OUT_SUCCESSES/I11=IF TAXI_OUT_MINS_OVER_SCHD LE 0 THEN 1 ELSE 0; $
     DEFINE AIRTIME_SUCCESSES/I11=IF AIRTIME_MINS_OVER_SCHD LE 0 THEN 1 ELSE 0; $
     DEFINE CITY_PAIR_NAME/A7=LEG_ACTL_ORIG||'-'||LEG_ACTL_DEST; $
 
     DEFINE STARTUP_FLAG_OLD/I11=IF (ACTUAL_GROUND_TME GT 240) AND (((UPPER(IATA_CDE) IN ('AC', 'RV')) AND (LEG_SCHD_DEP_LCL_TME FROM '05:00:00' TO '09:00:00')) OR ((UPPER(IATA_CDE) EQ 'QK') AND (LEG_SCHD_DEP_LCL_TME FROM '05:00:00' TO '09:50:00')) OR ((UPPER(IATA_CDE) IN ('ZX', '8K', 'RS')) AND (LEG_SCHD_DEP_LCL_TME FROM '05:00:00' TO '10:00:00'))) THEN 1 ELSE 0;
     DEFINE STARTUP_FLAG_NEW/I11=IF ((ACTUAL_GROUND_TME GT 240) AND (LEG_SCHD_DEP_LCL_TME FROM '05:00:00' TO '10:00:00')) THEN 1 ELSE 0;
$     DEFINE NEW_STARTUP_FLAG/I11=IF (FLIGHT_ORIG_LCL_DTE GE '20200801') THEN STARTUP_FLAG_NEW ELSE STARTUP_FLAG_OLD;, TITLE='Start-up Flag', DESCRIPTION='(F)', $ Has hard coded cutoff time and time range
    DEFINE NEW_STARTUP_FLAG/I11=IF (FLIGHT_ORIG_LCL_DTE GE '2020-08-01') THEN STARTUP_FLAG_NEW ELSE STARTUP_FLAG_OLD;, TITLE='Start-up Flag', DESCRIPTION='(F)', $ Has hard coded cutoff time and time range
 
     DEFINE QK_STARTUP_FLAG/I11=IF ACTUAL_GROUND_TME GT 240 AND LEG_SCHD_DEP_LCL_TME GE '05:00:00' AND LEG_SCHD_DEP_LCL_TME LE '09:50:00' THEN 1 ELSE 0; $ Has hard coded cutoff time and time range
     DEFINE UNCODED_DELAY_FLAG/I11=IF LEG_DEP_DELAY_PRMY_CDE EQ MISSING OR LEG_DEP_DELAY_SECD_CDE EQ MISSING THEN 1 ELSE 0; $
     DEFINE EXPECTED_EQI_FLAG/I11=IF (UPPER(LEG_MTA_EVENT_IND) EQ 'T') AND (DEP_DELAY_TT_00 EQ 1) THEN 1 ELSE 0; $
     DEFINE MISCODED_DELAY_FLAG/I11=IF (LEG_DEP_DELAY_SECD_CDE EQ MISSING) AND (DEP_DELAY_TT_00 EQ 1)
 THEN 1
 ELSE IF (UPPER(LEG_DEP_DELAY_SECD_CDE) EQ 'XXX') AND (DEP_DELAY_TT_00 EQ 1)
 THEN 1
 ELSE IF (EXPECTED_EQI_FLAG EQ 0) AND (UPPER(LEG_DEP_DELAY_SECD_CDE) EQ 'EQW' OR UPPER(LEG_DEP_DELAY_SECD_CDE) EQ 'EQI' OR UPPER(LEG_DEP_DELAY_SECD_CDE) EQ 'TRU') AND (DEP_DELAY_TT_00 EQ 1)
 THEN 1
 ELSE IF (EXPECTED_EQI_FLAG EQ 1) AND (UPPER(LEG_DEP_DELAY_SECD_CDE) NE 'EQT' AND UPPER(LEG_DEP_DELAY_SECD_CDE) NE 'EQM' AND UPPER(LEG_DEP_DELAY_SECD_CDE) NE 'EQW' AND UPPER(LEG_DEP_DELAY_SECD_CDE) NE 'EQI' AND UPPER(LEG_DEP_DELAY_SECD_CDE) NE 'TRU') AND (DEP_DELAY_TT_00 EQ 1)
 THEN 1
 ELSE 0; $
 
 -*    DEFINE Vers_Trim/A20=TRIM('B',ACFT_VERS_CDE,20,' ',1,'A20'); ACCESS_PROPERTY=(INTERNAL), $
 -*    DEFINE Without_lst3/A20=SUBSTR(ARGLEN(20, Vers_Trim, 'I11'), Vers_Trim ,1,ARGLEN(20, Vers_Trim, 'I11')-3,ARGLEN(20, Vers_Trim, 'I11')-3,'A20'); ACCESS_PROPERTY=(INTERNAL), $
 -*    DEFINE Vers_Patt/A20=PATTERN(20,Without_lst3,'A20'); ACCESS_PROPERTY=(INTERNAL), $
 -*    DEFINE Mask_nt/A20=STRREP(20,Vers_Patt,1,'A',2,'|$',20,'A20'); ACCESS_PROPERTY=(INTERNAL), $
 -*    DEFINE Mask/A20=TRIM('B',Mask_nt,ARGLEN(20,Mask_nt,'I11'),'|',1,'A20'); ACCESS_PROPERTY=(INTERNAL), $
 -*    DEFINE Vers_Pipe_Delim/A20=EDIT(Without_lst3,Mask); ACCESS_PROPERTY=(INTERNAL), $
 -*    DEFINE Tok1/A20=GETTOK(Vers_Pipe_Delim,20,1,'|',20,'A20'); ACCESS_PROPERTY=(INTERNAL), $
 -*    DEFINE Tok2/A20=GETTOK(Vers_Pipe_Delim,20,2,'|',20,'A20'); ACCESS_PROPERTY=(INTERNAL), $
 -*    DEFINE Tok3/A20=GETTOK(Vers_Pipe_Delim,20,3,'|',20,'A20'); ACCESS_PROPERTY=(INTERNAL), $
 -*    DEFINE Tok4/A20=GETTOK(Vers_Pipe_Delim,20,4,'|',20,'A20'); ACCESS_PROPERTY=(INTERNAL), $
 -*    DEFINE Tok5/A20=GETTOK(Vers_Pipe_Delim,20,5,'|',20,'A20'); ACCESS_PROPERTY=(INTERNAL), $
 -*    DEFINE Num1/D12=ATODBL(Tok1,'20','D12'); ACCESS_PROPERTY=(INTERNAL), $
 -*    DEFINE Num2/D12=ATODBL(Tok2,'20','D12'); ACCESS_PROPERTY=(INTERNAL), $
 -*    DEFINE Num3/D12=ATODBL(Tok3,'20','D12'); ACCESS_PROPERTY=(INTERNAL), $
 -*    DEFINE Num4/D12=ATODBL(Tok4,'20','D12'); ACCESS_PROPERTY=(INTERNAL), $
 -*    DEFINE Num5/D12=ATODBL(Tok5,'20','D12'); ACCESS_PROPERTY=(INTERNAL), $
 -*    DEFINE SEATS_TOTAL/D12=Num1 + Num2 + Num3 + Num4 + Num5; $
 
     DEFINE REGEX_MATCH/I1 = REGEX(SUBSTRING(UPPER(ACFT_VERS_CDE), ARGLEN(20, ACFT_VERS_CDE, 'I3') - 2, 3), '[A-Z]0[0-9]$');, $
     DEFINE CABIN_CONFIG/A20V = IF (REGEX_MATCH EQ 1) THEN TRIM_(BOTH, ' ', SUBSTRING(ACFT_VERS_CDE, 1, ARGLEN(20, ACFT_VERS_CDE, 'I3') - 3)) ELSE TRIM_(BOTH, ' ', ACFT_VERS_CDE);, $
     DEFINE CAB_J_PAT/A2 = PATTERN(2, TOKEN(UPPER(CABIN_CONFIG), 'J', 2), 'A2');, $
     DEFINE CAB_J/D12 = ATODBL(EDIT(TOKEN(UPPER(CABIN_CONFIG), 'J', 2), REPLACE(UPPER(CAB_J_PAT), 'A', '$')), '02', 'D12');, $
     DEFINE CAB_O_PAT/A2 = PATTERN(2, TOKEN(UPPER(CABIN_CONFIG), 'W', 2), 'A2');, $
     DEFINE CAB_O/D12 = ATODBL(EDIT(TOKEN(UPPER(CABIN_CONFIG), 'W', 2), REPLACE(UPPER(CAB_O_PAT), 'A', '$')), '02', 'D12');, $
     DEFINE CAB_Y_PAT/A3 = PATTERN(3, TOKEN(UPPER(CABIN_CONFIG), 'Y', 2), 'A3');, $
     DEFINE CAB_Y/D12 = ATODBL(EDIT(TOKEN(UPPER(CABIN_CONFIG), 'Y', 2), REPLACE(UPPER(CAB_Y_PAT), 'A', '$')), '03', 'D12');, $
     DEFINE SEATS_TOTAL/D12 = CAB_J + CAB_O + CAB_Y;, $
     DEFINE SEAT_UTILIZATION/D12.2=PAX_FLWN_CNT/SEATS_TOTAL*100; $
     DEFINE Time_Interval_Part/I11=HPART(LEG_SCHD_DEP_LCL_TME,'HOURS','I11'); ACCESS_PROPERTY=(INTERNAL), $
     DEFINE TIME_INTERVAL_SCHD_DEP_LCL/A20=DECODE Time_Interval_Part(0 '00:00-00:59 ' 1 '01:00-01:59' 2 '02:00-02:59' 3 '03:00-03:59' 4 '04:00-04:59' 5 '05:00-05:59' 6 '06:00-06:59' 7 '07:00-07:59' 8 '08:00-08:59' 9 '09:00-09:59' 10 '10:00-10:59' 11 '11:00-11:59' 12 '12:00-12:59' 13 '13:00-13:59' 14 '14:00-14:59' 15 '15:00-15:59' 16 '16:00-16:59' 17 '17:00-17:59' 18 '18:00-18:59' 19 '19:00-19:59' 20 '20:00-20:59' 21 '21:00-21:59' 22 '22:00-22:59' 23 '23:00-23:59' ); $
     DEFINE Time_Interval_Part_Arr/I11=HPART(LEG_SCHD_ARR_LCL_TME,'HOURS','I11'); ACCESS_PROPERTY=(INTERNAL), $
     DEFINE TIME_INTERVAL_SCHD_ARR_LCL/A20=DECODE Time_Interval_Part_Arr(0 '00:00-00:59 ' 1 '01:00-01:59' 2 '02:00-02:59' 3 '03:00-03:59' 4 '04:00-04:59' 5 '05:00-05:59' 6 '06:00-06:59' 7 '07:00-07:59' 8 '08:00-08:59' 9 '09:00-09:59' 10 '10:00-10:59' 11 '11:00-11:59' 12 '12:00-12:59' 13 '13:00-13:59' 14 '14:00-14:59' 15 '15:00-15:59' 16 '16:00-16:59' 17 '17:00-17:59' 18 '18:00-18:59' 19 '19:00-19:59' 20 '20:00-20:59' 21 '21:00-21:59' 22 '22:00-22:59' 23 '23:00-23:59' ); $
     DEFINE RAPIDAIR_EAST_FLAG/I11=IF ( UPPER(CARRIER_CDE) EQ 'AC' AND
     (UPPER(LEG_SCHD_ORIG) EQ 'YYZ' AND UPPER(LEG_SCHD_DEST) = 'YUL') OR
     (UPPER(LEG_SCHD_ORIG) EQ 'YYZ' AND UPPER(LEG_SCHD_DEST) = 'YOW') OR
     (UPPER(LEG_SCHD_ORIG) EQ 'YUL' AND UPPER(LEG_SCHD_DEST) = 'YYZ') OR
     (UPPER(LEG_SCHD_ORIG) EQ 'YOW' AND UPPER(LEG_SCHD_DEST) = 'YYZ')
   ) THEN 1
 ELSE 0; $
     DEFINE FIN_NUM/A4=EDIT(ACFT_REGS_CDE,'$$999'); $
     DEFINE FLT_DTE_KEY/A8YY|M|D=FLIGHT_ORIG_LCL_DTE; $
     DEFINE DATE_KEY/A8=FLT_DTE_KEY; $
 
     DEFINE FLEET_TYPE/A10=IF (UPPER(ACFT_SUBTYPE_CDE) IN ('333', '343', '763', '767', '76P', '76F', '77L', '77P', '77W', '788', '789')) THEN 'WIDE' ELSE 'NARROW'; $
 
     DEFINE LEG_ARR_Count/I11=IF (UPPER(LEG_STATE_CDE) EQ 'ARR') THEN 1 ELSE 0; $
 -*    DEFINE DEP_DELAY_PRMY_CDE/A5=IF  DEP_DELAY_TT_00 EQ 1 THEN  IF LEG_DEP_DELAY_PRMY_CDE EQ MISSING THEN 'ACS' ELSE LEG_DEP_DELAY_PRMY_CDE ELSE 'XXX'; $
 -*  DEFINE DEP_DELAY_SECD_CDE/A5=IF  DEP_DELAY_TT_00 EQ 1 THEN  IF LEG_DEP_DELAY_SECD_CDE EQ MISSING THEN 'ACU' ELSE LEG_DEP_DELAY_SECD_CDE ELSE 'XXX'; $
 
 -*    DEFINE DEP_DELAY_PRMY_CDE_AUX/A5=IF  DEP_DELAY_TT_00 EQ 1 THEN  IF LEG_DEP_DELAY_PRMY_CDE EQ MISSING THEN 'ACS' ELSE LEG_DEP_DELAY_PRMY_CDE ELSE 'XXX'; ACCESS_PROPERTY=(INTERNAL), $
 -*    DEFINE DEP_DELAY_PRMY_CDE/A5=IF  (IATA_CDE EQ 'AC' OR 'RV') AND FLIGHT_ORIG_LCL_DTE GE '20160101' AND DEP_DELAY_SECD_CDE EQ 'FOC' OR 'FOX' OR 'IFX' THEN 'SOC' ELSE (IF CARRIER_CDE EQ 'QK' AND FLIGHT_ORIG_LCL_DTE GE '20161001' AND DEP_DELAY_SECD_CDE EQ 'ANM' THEN 'IFS' ELSE DEP_DELAY_PRMY_CDE_AUX); $
 
     DEFINE DEP_DELAY_PRMY_CDE/A5 = LEG_DEP_DELAY_PRMY_CDE; $
     DEFINE DEP_DELAY_SECD_CDE/A5 = LEG_DEP_DELAY_SECD_CDE; $
     DEFINE DEP_DELAY_IATA_CDE/I4 = LEG_DEP_DELAY_IATA_CDE; $
 
 -*    DEFINE DEP_DELAY_SECD_CDE/A5=IF (DEP_DELAY_TT_00 EQ 1)
 -*        THEN (IF (LEG_DEP_DELAY_SECD_CDE EQ MISSING) AND (FLIGHT_ORIG_LCL_DTE LT '20180701')
 -*            THEN 'ACU'
 -*            ELSE LEG_DEP_DELAY_SECD_CDE)
 -*        ELSE 'XXX'; $
 
 -*    DEFINE DEP_DELAY_PRMY_CDE_AUX/A5=IF (DEP_DELAY_TT_00 EQ 1)
 -*        THEN (IF (LEG_DEP_DELAY_PRMY_CDE EQ MISSING) AND (FLIGHT_ORIG_LCL_DTE LT '20180701')
 -*            THEN 'ACS'
 -*            ELSE LEG_DEP_DELAY_PRMY_CDE)
 -*        ELSE 'XXX'; ACCESS_PROPERTY=(INTERNAL), $
 -*    DEFINE DEP_DELAY_PRMY_CDE/A5=IF (IATA_CDE IN ('AC', 'RV')) AND (FLIGHT_ORIG_LCL_DTE FROM '20160101' TO '20180630') AND (DEP_DELAY_SECD_CDE IN ('FOC', 'FOX', 'IFX')) THEN 'SOC' ELSE (IF (CARRIER_CDE EQ 'QK') AND (FLIGHT_ORIG_LCL_DTE GE '20161001') AND (DEP_DELAY_SECD_CDE EQ 'ANM') THEN 'IFS' ELSE DEP_DELAY_PRMY_CDE_AUX); $
 
 -*    DEFINE DEP_DELAY_IATA_CDE/I4=IF  DEP_DELAY_TT_00 EQ 1 THEN  IF LEG_DEP_DELAY_IATA_CDE EQ MISSING THEN 0 ELSE LEG_DEP_DELAY_IATA_CDE ELSE 99; $
 
 
     DEFINE FLIGHT_REGION_WOR/A20=IF UPPER(CITY_PAIR_NAME) EQ 'YYZ-YVR' OR 'YYZ-YYC' OR 'YYZ-YEG' OR 'YHZ-YYC' OR 'YUL-YVR' OR 'YUL-YYC' OR 'YUL-YEG' OR 'YOW-YVR' OR 'YOW-YYC' THEN 'Transcon CDA W' ELSE
       IF UPPER(CITY_PAIR_NAME) EQ 'YVR-YYZ' OR 'YYC-YYZ' OR 'YEG-YYZ' OR 'YYC-YHZ' OR 'YVR-YUL' OR 'YUL-YYC' OR 'YEG-YUL' OR 'YVR-YOW' OR 'YOW-YYC' THEN 'Transcon CDA E' ELSE
       IF UPPER(CITY_PAIR_NAME) EQ 'YYZ-LAX' OR 'YYZ-LAS' OR 'YYZ-SFO' OR 'YYZ-SAN' OR 'YUL-LAX' OR 'YUL-LAS' OR 'YUL-SFO' OR 'YYZ-PHX' THEN 'Transcon US W' ELSE
       IF UPPER(CITY_PAIR_NAME) EQ 'LAX-YYZ' OR 'LAS-YYZ' OR 'SFO-YYZ' OR 'SAN-YYZ' OR 'LAX-YUL' OR 'LAS-YUL' OR 'SFO-YUL' OR 'PHX-YYZ' THEN 'Transcon US E' ELSE
       IF UPPER(LEG_ACTL_ORIG) EQ 'LHR' OR 'FRA' OR 'CDG' OR 'MUC' OR 'FCO' OR 'BRU' THEN 'North Atlantic W' ELSE
       IF UPPER(LEG_ACTL_DEST) EQ 'LHR' OR 'FRA' OR 'CDG' OR 'MUC' OR 'FCO' OR 'BRU' THEN 'North Atlantic E' ELSE
       IF UPPER(LEG_ACTL_DEST) EQ 'PEK' OR 'ICN' OR 'NRT' OR 'HKG' OR 'PVG' THEN 'North Pacific W' ELSE
       IF UPPER(LEG_ACTL_ORIG) EQ 'PEK' OR 'ICN' OR 'NRT' OR 'HKG' OR 'PVG' THEN 'North Pacific E' ELSE
       IF UPPER(LEG_ACTL_ORIG) EQ 'KIN' OR 'MBJ' OR 'NAS' OR 'CCS' OR 'POS' OR 'SCL' OR 'EZE' OR 'HAV' OR 'VRA' THEN 'Caribbean N' ELSE
       IF UPPER(LEG_ACTL_DEST) EQ 'KIN' OR 'MBJ' OR 'NAS' OR 'CCS' OR 'POS' OR 'SCL' OR 'EZE' OR 'HAV' OR 'VRA' THEN 'Caribbean S' ELSE
       IF UPPER(CITY_PAIR_NAME) EQ 'BOS-YYZ' OR 'DCA-YYZ' OR 'EWR-YYZ' OR 'LGA-YYZ' OR 'PHL-YYZ' THEN 'From US NE' ELSE
       IF UPPER(CITY_PAIR_NAME) EQ 'YYZ-BOS' OR 'YYZ-DCA' OR 'YYZ-EWR' OR 'YYZ-LGA' OR 'YYZ-PHL' THEN 'To US NE' ELSE 'Other'; $
 
     DEFINE CPA_DLY_EXCL_QK_PRIORJAN2015/I1=IF DEP_DELAY_TT_00 EQ 0
 THEN 0 ELSE ( IF UPPER(DEP_DELAY_PRMY_CDE )EQ 'SEC' OR 'SKD' OR 'SOC' OR 'WXX'
 THEN 1
 ELSE ( IF UPPER(LEG_ACTL_ORIG) EQ 'YEG' OR 'YFC' OR 'YHZ' OR 'YOW' OR 'YUL' OR 'YVR' OR 'YWG' OR 'YYC' OR 'YYT' OR 'YYZ' AND DEP_DELAY_PRMY_CDE EQ 'ACS' OR 'CGO' OR 'GND' OR 'PSR'
 THEN 1  ELSE ( IF UPPER(LEG_ACTL_ORIG) EQ 'YQB' OR 'YQM' OR 'YQR' OR 'YQT' OR 'YSJ' OR 'YXE' OR 'YXY' OR 'YYG' AND DEP_DELAY_PRMY_CDE EQ 'ACS' OR 'CGO' OR 'GND'
 THEN 1
 ELSE ( IF UPPER(LEG_ACTL_ORIG) EQ 'BOS' OR 'DCA' OR 'EWR' OR 'LAX' OR 'LGA' OR 'ORD' AND DEP_DELAY_PRMY_CDE EQ 'PSR'
 THEN 1
 ELSE 0 )))); $
 
     DEFINE CPA_DLY_EXCL_QK_JAN2015/I1= IF DEP_DELAY_TT_00 EQ 1
 THEN (
 IF (UPPER(LEG_DEP_DELAY_PRMY_CDE) EQ 'WXX') THEN 1 ELSE
                 (
                                 IF (UPPER(LEG_DEP_DELAY_PRMY_CDE) EQ 'PSR') AND
                                 (UPPER(LEG_SCHD_ORIG) EQ 'YEG' OR 'YFC' OR 'YOW' OR 'YUL' OR 'YVR' OR 'YWG' OR 'YYC' OR 'YYZ' OR 'YYT' OR 'YHZ' OR 'YFC' OR 'BOS' OR 'DCA' OR 'EWR' OR 'LGA' OR 'ORD')
                                 THEN 1 ELSE
                                 (
                                 IF (UPPER(LEG_DEP_DELAY_PRMY_CDE) EQ 'ACS' OR 'CGO') AND
                                 (UPPER(LEG_SCHD_ORIG) EQ 'YEG' OR 'YFC' OR 'YOW' OR 'YUL' OR 'YVR' OR 'YWG' OR 'YYC' OR 'YYZ' OR 'YYT' OR 'YHZ' OR 'YFC' OR 'YQB' OR 'YQT' OR 'YQR' OR 'YXE' OR 'YXY' OR 'YQM' OR 'YSJ' OR 'YYG')
                                 THEN 1 ELSE
                                 (
                                                IF (UPPER(LEG_DEP_DELAY_PRMY_CDE) EQ 'GND') AND
                                                 (UPPER(LEG_SCHD_ORIG) EQ 'YEG' OR 'YFC' OR 'YOW' OR 'YUL' OR 'YVR' OR 'YWG' OR 'YYC' OR 'YYZ' OR 'YYT' OR 'YHZ' OR 'YFC' OR 'YQB' OR 'YQT' OR 'YQR' OR 'YXE' OR 'YXY' OR 'YQM' OR 'YSJ' OR 'YYG' OR 'BOS' OR 'DCA' OR 'EWR' OR 'LGA' OR 'ORD')
                                                 THEN 1 ELSE
                                                 (
                                                                 IF (UPPER(LEG_DEP_DELAY_SECD_CDE) EQ 'SEB' OR 'ANB') AND
                                                                 (UPPER(LEG_SCHD_ORIG) EQ 'YEG' OR 'YFC' OR 'YOW' OR 'YUL' OR 'YVR' OR 'YWG' OR 'YYC' OR 'YYZ' OR 'YYT' OR 'YHZ' OR 'YFC' OR 'BOS' OR 'DCA' OR 'EWR' OR 'LGA' OR 'ORD')
                                                                 THEN 1 ELSE
                                                                 (
                                                                                 IF (UPPER(LEG_DEP_DELAY_SECD_CDE) EQ 'SEE' OR 'SCS') AND
                                                                                 (UPPER(LEG_SCHD_ORIG) EQ 'BOS' OR 'DCA' OR 'EWR' OR 'LGA' OR 'ORD')
                                                                                 THEN 1 ELSE
                                                                                 (
                                                                                                 IF (UPPER(LEG_DEP_DELAY_SECD_CDE) EQ 'SEI' OR 'SES' OR 'SKD' OR 'SOA' OR 'SOP' OR 'SOW' OR 'SUR' OR 'CNX' OR 'ISS' OR 'SEJ' OR 'FDX' OR 'ANH' OR 'FDA') THEN 1 ELSE 0
                                                                                 )
 
                                                                 )
 
                                                 )
                                 )
                                 )
                 )
 
 
 )
 ELSE 0 ; $
$     DEFINE CPA_DLY_EXCL_QK/I1=IF FLIGHT_ORIG_LCL_DTE LT '20150101' THEN CPA_DLY_EXCL_QK_PRIORJAN2015 ELSE CPA_DLY_EXCL_QK_JAN2015; $
     DEFINE CPA_DLY_EXCL_QK/I1=IF FLIGHT_ORIG_LCL_DTE LT '2015-01-01' THEN CPA_DLY_EXCL_QK_PRIORJAN2015 ELSE CPA_DLY_EXCL_QK_JAN2015; $
 
     DEFINE CPA_DLY_EXCL_RS/I1=IF DEP_DELAY_TT_05 EQ 0 THEN 1 ELSE (IF UPPER(DEP_DELAY_PRMY_CDE) EQ 'EQP' OR 'TRU' OR 'SKD' OR 'SOC' OR 'WXX' THEN 1 ELSE (IF (UPPER(LEG_ACTL_ORIG) EQ 'YUL' OR 'YQM') AND (UPPER(DEP_DELAY_PRMY_CDE) EQ 'ACS' OR 'PSR' OR 'GND' OR 'CGO') THEN 1 ELSE 0)); $
 
 
 
-*    DEFINE FOCUS_FLT_FLAG/D12=
-*          IF LEG_FLOWN.LEG_FLOWN.FLIGHT_NUM EQ 32   THEN 1
-*     ELSE IF LEG_FLOWN.LEG_FLOWN.FLIGHT_NUM EQ 16   THEN 1
-*     ELSE IF LEG_FLOWN.LEG_FLOWN.FLIGHT_NUM EQ 88   THEN 1
-*     ELSE IF LEG_FLOWN.LEG_FLOWN.FLIGHT_NUM EQ 8    THEN 1
-*     ELSE IF LEG_FLOWN.LEG_FLOWN.FLIGHT_NUM EQ 737  THEN 1
-*     ELSE IF LEG_FLOWN.LEG_FLOWN.FLIGHT_NUM EQ 156  THEN 1
-*     ELSE IF LEG_FLOWN.LEG_FLOWN.FLIGHT_NUM EQ 872  THEN 1
-*     ELSE IF LEG_FLOWN.LEG_FLOWN.FLIGHT_NUM EQ 832  THEN 1
-*     ELSE IF LEG_FLOWN.LEG_FLOWN.FLIGHT_NUM EQ 874  THEN 1
-*     ELSE IF LEG_FLOWN.LEG_FLOWN.FLIGHT_NUM EQ 5    THEN 1
-*     ELSE IF LEG_FLOWN.LEG_FLOWN.FLIGHT_NUM EQ 56   THEN 1
-*     ELSE IF LEG_FLOWN.LEG_FLOWN.FLIGHT_NUM EQ 70   THEN 1
-*     ELSE 0; $
 
-*    DEFINE FOCUS_FLT_FLAG/I1 = IF (FLIGHT_NUM IN (8, 15,25,29,32,88,111,1178,153,156,874,877)) THEN 1 ELSE 0;
 
-*** Updated on 2017-05-29 (effective June 1)
-*    DEFINE FOCUS_FLT_FLAG/I1 = IF (FLIGHT_NUM IN (8, 16, 28, 32, 91, 118, 301, 844, 856, 871, 872, 877)) THEN 1 ELSE 0;
 
-*** Updated on 2017-12-01 (effective Dec 1, 2017)
-*    DEFINE FOCUS_FLT_FLAG/I1 = IF (FLIGHT_NUM IN (6, 16, 26, 28, 29, 32, 101, 301, 791, 737, 847, 1829)) THEN 1 ELSE 0;
 
-*** Updated on 2018-06-04 (effective Jun 4, 2018)
-*    DEFINE FOCUS_FLT_FLAG/I1 = IF (FLIGHT_NUM IN (16, 28, 30, 91, 101, 118, 301, 737, 844, 871, 872, 877)) THEN 1 ELSE 0;
 
-*** Updated on 2020-12-17 (effective Dec 17, 2020)
-*    DEFINE FOCUS_FLT_FLAG/I1 = IF (FLIGHT_NUM IN (4, 7, 25, 58, 103, 114, 128, 158, 162, 178, 201, 301, 317, 341, 469, 481, 603, 604, 611, 833, 871)) THEN 1 ELSE 0;
 
-*** Updated on 2023-03-22 (effective Jan 10, 2023)
-*    DEFINE FOCUS_FLT_FLAG/I1 = IF (FLIGHT_NUM IN (3, 33, 50, 56, 63, 64, 103, 121, 127, 128, 200, 210, 311, 420, 551, 661, 685, 743, 857, 1634)) THEN 1 ELSE 0;
 
 
-*** Updated 2023-05-18 to use reference table
   SEGMENT=FOCUS_FLIGHT_REF_NEW, SEGTYPE=KU, PARENT=LEG_FLOWN, CRFILE=ops_analytics/data/master/ops/FOCUS_FLIGHT_REF_NEW, CRINCLUDE=ALL, CRJOINTYPE=LEFT_OUTER,
     JOIN_WHERE=(LEG_FLOWN.FLIGHT_NUM EQ FOCUS_FLIGHT_REF_NEW.FLIGHT_NUM)
                 AND (LEG_FLOWN.CARRIER_CDE EQ FOCUS_FLIGHT_REF_NEW.CARRIER_CDE)
                 AND (LEG_FLOWN.FLIGHT_ORIG_LCL_DTE FROM FOCUS_FLIGHT_REF_NEW.EFF_FROM_DTE TO FOCUS_FLIGHT_REF_NEW.EFF_TO_DTE)
		 AND (LEG_FLOWN.LEG_SCHD_ORIG EQ FOCUS_FLIGHT_REF_NEW.ORIG);, $
    DEFINE FOCUS_FLT_FLAG/I1 = IF (FOCUS_FLIGHT_REF_NEW.FLIGHT_NUM NE MISSING) THEN 1 ELSE 0;
 
 
     DEFINE LEG_MTA_EVENT_NAME/A20 MISSING ON=IF (UPPER(LEG_MTA_EVENT_IND) EQ 'V') THEN 'MTA_VIOLATION'
 ELSE IF (UPPER(LEG_MTA_EVENT_IND) EQ 'T') THEN 'MTA_TURN'
 ELSE 'NON_MTA'; $
     DEFINE LEG_DEP_EQUIP_DELAY_MINS/I11 MISSING ON=IF  ( LEG_ID GT 0 AND DEP_DELAY_TT_00 EQ 1 AND LEG_MTA_EVENT_IND EQ 'T' )
 THEN  LEG_DEP_DELAY_MINS ELSE
 (IF (DEP_DELAY_TT_00 EQ 1 AND UPPER(LEG_MTA_EVENT_IND) EQ 'V' AND  PREV_LEG_ID NE MISSING)
 THEN  MINIMUM_GROUND_TME - AVAIL_GROUND_TME
 ELSE MISSING); $
     DEFINE LEG_DEP_FUNC_DELAY_MINS/I11 MISSING ON=IF DEP_DELAY_TT_00 EQ 1 AND  LEG_MTA_EVENT_IND EQ 'N'
 THEN LEG_DEP_DELAY_MINS ELSE
 (IF (DEP_DELAY_TT_00 EQ 1 AND UPPER(LEG_MTA_EVENT_IND) EQ 'V' AND LEG_ID NE MISSING)
 THEN  ACTUAL_GROUND_TME - MINIMUM_GROUND_TME
 ELSE MISSING); $
 
  DEFINE EXPECTED_EQI_FLAG/I11=IF (UPPER(LEG_MTA_EVENT_IND) EQ 'T') AND (DEP_DELAY_TT_00 EQ 1) THEN 1 ELSE 0; $
     DEFINE LEG_DEP_FUNC_DELAYS/I11 MISSING ON=IF LEG_DEP_FUNC_DELAY_MINS GT 0 THEN 1 ELSE 0; $
   DEFINE EXPECTED_DELAY_CODE_COMMENT/A100 MISSING ON=IF (LEG_DEP_DELAY_SECD_CDE EQ MISSING) AND (DEP_DELAY_TT_00 EQ 1)
 THEN 'Dep. Delay not entered in Communicator / Netline'
 ELSE IF (UPPER(LEG_DEP_DELAY_SECD_CDE) EQ 'XXX') AND (DEP_DELAY_TT_00 EQ 1)
 THEN 'Dep. Delay cannot be XXX'
 ELSE IF (EXPECTED_EQI_FLAG EQ 0) AND (UPPER(LEG_DEP_DELAY_SECD_CDE) EQ 'EQW' OR UPPER(LEG_DEP_DELAY_SECD_CDE) EQ 'EQI' OR UPPER(LEG_DEP_DELAY_SECD_CDE) EQ 'TRU') AND (DEP_DELAY_TT_00 EQ 1)
 THEN 'Can not be ' | LEG_DEP_DELAY_SECD_CDE | ' for ' | LEG_MTA_EVENT_NAME
 ELSE IF (EXPECTED_EQI_FLAG EQ 1) AND (UPPER(LEG_DEP_DELAY_SECD_CDE) NE 'EQT' AND UPPER(LEG_DEP_DELAY_SECD_CDE) NE 'EQM' AND UPPER(LEG_DEP_DELAY_SECD_CDE) NE 'EQW' AND UPPER(LEG_DEP_DELAY_SECD_CDE) NE 'EQI' AND UPPER(LEG_DEP_DELAY_SECD_CDE) NE 'TRU') AND (DEP_DELAY_TT_00 EQ 1)
 THEN 'Can not be ' | LEG_DEP_DELAY_SECD_CDE | ' for ' | LEG_MTA_EVENT_NAME
 ELSE MISSING; $
 
     DEFINE MTA_EVENTS/I11 =IF (UPPER(LEG_MTA_EVENT_IND) EQ 'T') OR (UPPER(LEG_MTA_EVENT_IND) EQ 'V') THEN 1 ELSE 0; $
     DEFINE MTA_VIOLATIONS/I11 =IF UPPER(LEG_MTA_EVENT_IND) EQ 'V' THEN 1 ELSE 0; $
 
 
 
   SEGMENT=DELAY_CODE_MASTER, SEGTYPE=KM, PARENT=LEG_FLOWN, CRFILE=ops_analytics/data/master/ops/DELAY_CODE_MASTER, CRINCLUDE=ALL, CRJOINTYPE=LEFT_OUTER,
     JOIN_WHERE=FLIGHT_ORIG_LCL_DTE GE EFFECTIVE_START_DT AND FLIGHT_ORIG_LCL_DTE LE EFFECTIVE_END_DT AND DEP_DELAY_IATA_CDE EQ DELAY_CODE_IATA_NETLINE;, $
   SEGMENT=DLY_CDE_1, SEGTYPE=KU, PARENT=LEG_FLOWN, CRFILE=ops_analytics/data/master/ops/LEG_FLOWN_DELAY, CRSEGMENT=LEG_FLOWN_DELAY, CRINCLUDE=ALL, CRJOINTYPE=LEFT_OUTER,
     JOIN_WHERE=DLY_CDE_1.LEG_DEP_DELAY_SUB_ID EQ 0 AND LEG_FLOWN.LEG_ID EQ DLY_CDE_1.LEG_ID AND UPPER(DLY_CDE_1.DELAY_TYPE) EQ 'U';, $
   SEGMENT=DLY_CDE_2, SEGTYPE=KU, PARENT=LEG_FLOWN, CRFILE=ops_analytics/data/master/ops/LEG_FLOWN_DELAY, CRSEGMENT=LEG_FLOWN_DELAY, CRINCLUDE=ALL, CRJOINTYPE=LEFT_OUTER,
     JOIN_WHERE=DLY_CDE_2.LEG_DEP_DELAY_SUB_ID EQ 1 AND LEG_FLOWN.LEG_ID EQ DLY_CDE_2.LEG_ID AND UPPER(DLY_CDE_2.DELAY_TYPE) EQ 'U';, $
   SEGMENT=DLY_CDE_3, SEGTYPE=KU, PARENT=LEG_FLOWN, CRFILE=ops_analytics/data/master/ops/LEG_FLOWN_DELAY, CRSEGMENT=LEG_FLOWN_DELAY, CRINCLUDE=ALL, CRJOINTYPE=LEFT_OUTER,
     JOIN_WHERE=DLY_CDE_3.LEG_DEP_DELAY_SUB_ID EQ 2 AND LEG_FLOWN.LEG_ID EQ DLY_CDE_3.LEG_ID AND UPPER(DLY_CDE_3.DELAY_TYPE) EQ 'U';, $
   SEGMENT=DLY_CDE_4, SEGTYPE=KU, PARENT=LEG_FLOWN, CRFILE=ops_analytics/data/master/ops/LEG_FLOWN_DELAY, CRSEGMENT=LEG_FLOWN_DELAY, CRINCLUDE=ALL, CRJOINTYPE=LEFT_OUTER,
     JOIN_WHERE=DLY_CDE_4.LEG_DEP_DELAY_SUB_ID EQ 3 AND LEG_FLOWN.LEG_ID EQ DLY_CDE_4.LEG_ID AND UPPER(DLY_CDE_4.DELAY_TYPE) EQ 'U';, $
 
   SEGMENT=ARR_DLY_CDE_1, SEGTYPE=KU, PARENT=LEG_FLOWN, CRFILE=ops_analytics/data/master/ops/LEG_FLOWN_DELAY, CRSEGMENT=LEG_FLOWN_DELAY, CRINCLUDE=ALL, CRJOINTYPE=LEFT_OUTER,
     JOIN_WHERE=ARR_DLY_CDE_1.LEG_DEP_DELAY_SUB_ID EQ 0 AND LEG_FLOWN.LEG_ID EQ ARR_DLY_CDE_1.LEG_ID AND UPPER(ARR_DLY_CDE_1.DELAY_TYPE) EQ 'I';, $
   SEGMENT=ARR_DLY_CDE_2, SEGTYPE=KU, PARENT=LEG_FLOWN, CRFILE=ops_analytics/data/master/ops/LEG_FLOWN_DELAY, CRSEGMENT=LEG_FLOWN_DELAY, CRINCLUDE=ALL, CRJOINTYPE=LEFT_OUTER,
     JOIN_WHERE=ARR_DLY_CDE_2.LEG_DEP_DELAY_SUB_ID EQ 1 AND LEG_FLOWN.LEG_ID EQ ARR_DLY_CDE_2.LEG_ID AND UPPER(ARR_DLY_CDE_2.DELAY_TYPE) EQ 'I';, $
   SEGMENT=ARR_DLY_CDE_3, SEGTYPE=KU, PARENT=LEG_FLOWN, CRFILE=ops_analytics/data/master/ops/LEG_FLOWN_DELAY, CRSEGMENT=LEG_FLOWN_DELAY, CRINCLUDE=ALL, CRJOINTYPE=LEFT_OUTER,
     JOIN_WHERE=ARR_DLY_CDE_3.LEG_DEP_DELAY_SUB_ID EQ 2 AND LEG_FLOWN.LEG_ID EQ ARR_DLY_CDE_3.LEG_ID AND UPPER(ARR_DLY_CDE_3.DELAY_TYPE) EQ 'I';, $
   SEGMENT=ARR_DLY_CDE_4, SEGTYPE=KU, PARENT=LEG_FLOWN, CRFILE=ops_analytics/data/master/ops/LEG_FLOWN_DELAY, CRSEGMENT=LEG_FLOWN_DELAY, CRINCLUDE=ALL, CRJOINTYPE=LEFT_OUTER,
     JOIN_WHERE=ARR_DLY_CDE_4.LEG_DEP_DELAY_SUB_ID EQ 3 AND LEG_FLOWN.LEG_ID EQ ARR_DLY_CDE_4.LEG_ID AND UPPER(ARR_DLY_CDE_4.DELAY_TYPE) EQ 'I';, $
 
$$$$$$
DEFINE LEG_FLOWN_CARRIER_CDE/A2V = TRIM('B', LEG_FLOWN.CARRIER_CDE,2, ' ', 1, 'A2');
$DEFINE REF_OPRN_FLIGHT_RANG_CARRIER_CDE/A2V = TRIM('B', REF_OPRN_FLIGHT_RANGE.CARRIER_CDE,2, ' ', 1, 'A2');
$$$$$
 
   SEGMENT=REF_OPRN_FLIGHT_RANGE, SEGTYPE=KU, PARENT=LEG_FLOWN, CRFILE=ops_analytics/data/master/ops/REF_OPRN_FLIGHT_RANGE, CRINCLUDE=ALL, CRJOINTYPE=LEFT_OUTER,
     JOIN_WHERE=LEG_FLOWN.FLIGHT_NUM FROM REF_OPRN_FLIGHT_RANGE.FLIGHT_RNG_STRT TO REF_OPRN_FLIGHT_RANGE.FLIGHT_RNG_END AND LEG_FLOWN.FLIGHT_ORIG_LCL_DTE FROM REF_OPRN_FLIGHT_RANGE.EFF_FROM_DTE TO REF_OPRN_FLIGHT_RANGE.EFF_TO_DTE AND LEG_FLOWN.CARRIER_CDE EQ REF_OPRN_FLIGHT_RANGE.CARRIER_CDE;, $
$ applying UPPER function on JOIN fields is causing a bad performance - YS 20220525
$     JOIN_WHERE=LEG_FLOWN.FLIGHT_NUM GE FLIGHT_RNG_STRT AND LEG_FLOWN.FLIGHT_NUM LE FLIGHT_RNG_END AND LEG_FLOWN.FLIGHT_ORIG_LCL_DTE GE EFF_FROM_DTE AND LEG_FLOWN.FLIGHT_ORIG_LCL_DTE LE EFF_TO_DTE AND UPPER(LEG_FLOWN.CARRIER_CDE) EQ UPPER(REF_OPRN_FLIGHT_RANGE.CARRIER_CDE);, $
$     JOIN_WHERE=LEG_FLOWN.FLIGHT_NUM GE FLIGHT_RNG_STRT AND LEG_FLOWN.FLIGHT_NUM LE FLIGHT_RNG_END AND LEG_FLOWN.FLIGHT_ORIG_LCL_DTE GE EFF_FROM_DTE AND LEG_FLOWN.FLIGHT_ORIG_LCL_DTE LE EFF_TO_DTE AND LEG_FLOWN.LEG_FLOWN_CARRIER_CDE EQ REF_OPRN_FLIGHT_RANGE.CARRIER_CDE;, $
 
     DEFINE CPA_DLY_EXCL/I1=IF CARRIER_NME EQ 'Air Canada Jazz' THEN CPA_DLY_EXCL_QK ELSE
 (IF CARRIER_NME EQ 'Sky Regional' THEN CPA_DLY_EXCL_RS ELSE 0); $
     DEFINE OTP_INCLUDE_FLAG/I11=IF (LEG_DVRT_IND IS MISSING) AND (REF_OPRN_FLIGHT_RANGE.OPRN_INCL_FLG EQ 1) AND (LEG_TYPE_CDE NE 'P') THEN 1 ELSE 0; $
   SEGMENT=FSIS_PAX, SEGTYPE=KU, PARENT=LEG_FLOWN, CRFILE=ops_analytics/data/master/ops/LEG_FLOWN_CABIN, CRSEGMENT=LEG_FLOWN_CABIN, CRINCLUDE=ALL, CRJOINTYPE=LEFT_OUTER,
     JOIN_WHERE=UPPER(FSIS_PAX.SEL_SRCE_CDE) EQ 'F' AND LEG_FLOWN.CARRIER_CDE EQ FSIS_PAX.CARRIER_CDE AND LEG_FLOWN.FLIGHT_NUM EQ FSIS_PAX.FLIGHT_NUM AND LEG_FLOWN.FLIGHT_SUFFIX_CDE EQ FSIS_PAX.FLIGHT_SUFFIX_CDE AND LEG_FLOWN.FLIGHT_ORIG_DTE EQ FSIS_PAX.FLIGHT_ORIG_DTE AND LEG_FLOWN.LEG_SCHD_ORIG EQ FSIS_PAX.LEG_SCHD_ORIG AND LEG_FLOWN.LEG_ID EQ FSIS_PAX.LEG_ID;, $
   SEGMENT=DCS_PAX, SEGTYPE=KU, PARENT=LEG_FLOWN, CRFILE=ops_analytics/data/master/ops/LEG_FLOWN_CABIN, CRSEGMENT=LEG_FLOWN_CABIN, CRINCLUDE=ALL, CRJOINTYPE=LEFT_OUTER,
     JOIN_WHERE=UPPER(DCS_PAX.SEL_SRCE_CDE) IN ('D', 'C') AND LEG_FLOWN.CARRIER_CDE EQ DCS_PAX.CARRIER_CDE AND LEG_FLOWN.FLIGHT_NUM EQ DCS_PAX.FLIGHT_NUM AND LEG_FLOWN.FLIGHT_SUFFIX_CDE EQ DCS_PAX.FLIGHT_SUFFIX_CDE AND LEG_FLOWN.FLIGHT_ORIG_DTE EQ DCS_PAX.FLIGHT_ORIG_DTE AND LEG_FLOWN.LEG_SCHD_ORIG EQ DCS_PAX.LEG_SCHD_ORIG AND LEG_FLOWN.LEG_ID EQ DCS_PAX.LEG_ID;, $
 
    DEFINE PAX_FLOWN/I11=IF ((DCS_PAX.TOT_PSGR_CNT EQ MISSING) OR (DCS_PAX.TOT_PSGR_CNT EQ 0)) THEN PAX_FLWN_CNT ELSE DCS_PAX.TOT_PSGR_CNT; $
    DEFINE INB_CNX_PAX/I11 = IF ((DCS_PAX.PSGR_CNX_IN_CNT EQ MISSING) OR (DCS_PAX.PSGR_CNX_IN_CNT EQ 0)) THEN FSIS_PAX.PSGR_CNX_IN_CNT ELSE DCS_PAX.PSGR_CNX_IN_CNT; $
    DEFINE OUTB_CNX_PAX/I11 = IF ((DCS_PAX.PSGR_CNX_OUT_CNT EQ MISSING) OR (DCS_PAX.PSGR_CNX_OUT_CNT EQ 0)) THEN FSIS_PAX.PSGR_CNX_OUT_CNT ELSE DCS_PAX.PSGR_CNX_OUT_CNT; $
 -*   DEFINE PAX_FLOWN/I11=IF (FLIGHT_ORIG_LCL_DTE GT '2019-11-18') THEN PAX_FLWN_CNT ELSE IF ((DCS_PAX.TOT_PSGR_CNT EQ MISSING) OR (DCS_PAX.TOT_PSGR_CNT EQ 0)) THEN FSIS_PAX.TOT_PSGR_CNT ELSE DCS_PAX.TOT_PSGR_CNT; $
 -*   DEFINE PAX_FLOWN/I11=PAX_FLWN_CNT; $
 
   SEGMENT=ORIG_REGION, SEGTYPE=KU, PARENT=LEG_FLOWN, CRFILE=ops_analytics/data/master/ops/STATION_MASTER, CRSEGMENT=STATION_MASTER, CRINCLUDE=ALL, CRJOINTYPE=LEFT_OUTER,
     JOIN_WHERE=LEG_FLOWN.LEG_SCHD_ORIG EQ ORIG_REGION.IATA_AIRPORT_CODE;,
     DESCRIPTION='ORIG', $
   SEGMENT=DEST_REGION, SEGTYPE=KU, PARENT=LEG_FLOWN, CRFILE=ops_analytics/data/master/ops/STATION_MASTER, CRSEGMENT=STATION_MASTER, CRINCLUDE=ALL, CRJOINTYPE=LEFT_OUTER,
     JOIN_WHERE=LEG_FLOWN.LEG_SCHD_DEST EQ DEST_REGION.IATA_AIRPORT_CODE;, $
     DEFINE FLIGHTS/I11=1; $
 
 
     DEFINE JAZZ_USA_STN/I1 = IF (UPPER(LEG_ACTL_ORIG) IN ('ACY', 'ATL', 'AUS', 'BWI', 'CLE', 'CMH', 'DTW', 'IAD', 'IAH', 'JFK', 'MSP', 'MSY', 'PDX', 'PIT', 'SEA', 'STL')) THEN 1 ELSE 0; $
     DEFINE JAZZ_CANADA_STN/I1 = IF (UPPER(LEG_ACTL_ORIG) IN ('YAM', 'YBC', 'YBG', 'YCD', 'YCG', 'YDF', 'YFC', 'YGK', 'YGP', 'YGR', 'YKA', 'YLW', 'YMM', 'YPR', 'YQF', 'YQB', 'YQG', 'YQL', 'YQM', 'YQQ', 'YQR', 'YQT', 'YQU', 'YQX', 'YQY', 'YSB', 'YSJ', 'YTS', 'YUY', 'YVO', 'YWK', 'YXC', 'YXE', 'YXH', 'YXJ', 'YXS', 'YXT', 'YXU', 'YXY', 'YYB', 'YYD', 'YYF', 'YYG', 'YYJ', 'YYR', 'YYY', 'YZF', 'YZP', 'YZR', 'YZV', 'ZBF')) THEN 1 ELSE 0; $
 
 
     COMPUTE DEP_DELAY_SUCCESS_RATE_TT_00/D12.4=1 - DEP_DELAY_TT_00/FLIGHTS;, $
     COMPUTE ARR_DELAY_SUCCESS_RATE_TT_14/D12.4=1 - ARR_DELAY_TT_14/FLIGHTS;, $
     COMPUTE ARR_DELAY_MIN_AVG_TT_14/D12.4=ARR_DELAY_MINUTES_TT_14/ARR_DELAY_TT_14;, $
     COMPUTE LEG_DEP_FUNC_DELAY_MINS_AVG/D12.4=LEG_DEP_FUNC_DELAY_MINS/ DEP_DELAY_TT_00;, $
     COMPUTE SCHD_BLOCK_TIME_AVG/D12.4=SCHD_BLOCK_TIME/FLIGHTS;, $
     COMPUTE ACTL_BLOCK_TIME_AVG/D12.4=ACTL_BLOCK_TIME/FLIGHTS;, $
     COMPUTE BLOCK_TIME_SUCCESS_RATE/D12.4=BLOCK_TIME_SUCCESSES/FLIGHTS;, $
     COMPUTE TAXI_IN_SUCCESS_RATE/D12.4=TAXI_IN_SUCCESSES/FLIGHTS;, $
     COMPUTE TAXI_OUT_SUCCESS_RATE/D12.4=TAXI_OUT_SUCCESSES/FLIGHTS;, $
     COMPUTE AIRTIME_SUCCESS_RATE/D12.4=AIRTIME_SUCCESSES/FLIGHTS;, $
     COMPUTE BLOCK_TIME_MINS_OVER_SCHD_AVG/D12.4=BLOCK_TIME_MINS_OVER_SCHD/FLIGHTS;, $
     COMPUTE TAXI_IN_MINS_OVER_SCHD_AVG/D12.4=TAXI_IN_MINS_OVER_SCHD/FLIGHTS;, $
     COMPUTE TAXI_OUT_MINS_OVER_SCHD_AVG/D12.4=TAXI_OUT_MINS_OVER_SCHD/FLIGHTS;, $
     COMPUTE AIRTIME_MINS_OVER_SCHD_AVG/D12.4=AIRTIME_MINS_OVER_SCHD/FLIGHTS;, $
     COMPUTE MTA_SUCCESS_RATE/D12.4=(MTA_EVENTS - MTA_VIOLATIONS)/MTA_EVENTS;, $
     COMPUTE MTA_VIOLATION_RATE/D12.4=MTA_VIOLATIONS/MTA_EVENTS;, $
 
