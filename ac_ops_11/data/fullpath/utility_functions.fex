-* File: utility_functions.fex
-* Created by Konstantin Koton AC048724, 2017-03-09
 
-DEFAULTH &&OPS_UTILITY_FUNCTIONS_LOADED = 0;
 
-IF (&&OPS_UTILITY_FUNCTIONS_LOADED EQ 1) GOTO :SKIP_UTILITY_FUNCTIONS_LOAD;
-SET &&OPS_UTILITY_FUNCTIONS_LOADED = 1;
 
-TYPE ************************************
-TYPE *** OPS UTILITY FUNCTIONS LOADED ***
-TYPE ************************************
 
DEFINE FUNCTION DECODE_MONTH(MONTH/I2, LENGTH/I2, CASE/A5)
	MONTH_NAME/A10 = DECODE MONTH (1 'January' 2 'February' 3 'March' 4 'April' 5 'May' 6 'June' 7 'July' 8 'August' 9 'September' 10 'October' 11 'November' 12 'December');
	SHORTENED_MONTH/A10V = IF (LENGTH LT 10) THEN SUBSTR(10, MONTH_NAME, 1, LENGTH + 1, LENGTH, 'A10V') ELSE MONTH_NAME;
	DECODE_MONTH/A10V = IF (CASE EQ 'UPPER') THEN UPCASE(10, SHORTENED_MONTH, 'A10V') ELSE IF (CASE EQ 'LOWER') THEN LOCASE(10, SHORTENED_MONTH, 'A10V') ELSE SHORTENED_MONTH;
END
-RUN
 
DEFINE FUNCTION DATE_FROM_WEEK(YEAR/I4, WEEK/I2, DAY/I1)
	START_OF_ISO_WEEK/YYMD = DATEMOV(DATECVT(EDIT(YEAR) || '0101', 'A8YYMD', 'YYMD'), 'BOW');
	THURSDAY/YYMD = DATEADD(START_OF_ISO_WEEK, 'D', 3);
	YEAR_OF_THURSDAY/I4 = HPART(HDTTM(THURSDAY, 8, 'HYYMDS'), 'YEAR', 'I4');
	FIRST_DAY_OF_ISO_WEEK_1/YYMD = IF (YEAR_OF_THURSDAY EQ YEAR) THEN START_OF_ISO_WEEK ELSE DATEADD(START_OF_ISO_WEEK, 'D', 7);
	FIRST_DAY_OF_ISO_WEEK/YYMD = DATEADD(FIRST_DAY_OF_ISO_WEEK_1, 'D', 7 * (WEEK - 1));
-*	DATE_FROM_WEEK/I8YYMD = IF (UPCASE(5, START_END, 'A5') EQ 'START') THEN DATECVT(FIRST_DAY_OF_ISO_WEEK, 'YYMD', 'I8YYMD') ELSE
-*							IF (UPCASE(5, START_END, 'A5') EQ 'END') THEN DATECVT(DATEADD(FIRST_DAY_OF_ISO_WEEK, 'D', 6), 'YYMD', 'I8YYMD') ELSE 0;
	DATE_FROM_WEEK/I8YYMD = DATEADD(FIRST_DAY_OF_ISO_WEEK, 'D', (DAY - 1));
END
-RUN
 
DEFINE FUNCTION WEEK_NUM_YYMD(DATE/YYMD)
	WEEK_NUM_YYMD/I2 = EDIT(EDIT(HYYWD(HDTTM(DATE, 8, 'HYYMDIA'), 'A10'), '$$$$$$99$$'));
END
-RUN
 
DEFINE FUNCTION WEEK_NUM_I8(DATE/I8YYMD)
	WEEK_NUM_I8/I2 = EDIT(EDIT(HYYWD(HDTTM(DATECVT(DATE, 'I8YYMD', 'YYMD'), 8, 'HYYMDIA'), 'A10'), '$$$$$$99$$'));
END
-RUN
 
DEFINE FUNCTION DISPLAY_DATE(DATE/I8, LENGTH/I2, CASE/A5)
	YEAR/A4 = EDIT(DATE, '9999$$$$');
	MONTH/A10V = DECODE_MONTH(IMOD(INT(DATE / 100), 100, 'I2'), LENGTH, CASE);
	DAY/A2 = EDIT(DATE, '$$$$$$99');
	DISPLAY_DATE/A20V = MONTH || (' ' | DAY) | ', ' | YEAR;
END
-RUN
 
 
 
 
 
 
-:SKIP_UTILITY_FUNCTIONS_LOAD
 
